
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ВзаиморасчетыСПокупателями Приход
	Движения.ВзаиморасчетыСПокупателями.Очистить();
	Движения.ВзаиморасчетыСПокупателями.БлокироватьДляИзменения	= Истина;
	Движения.ВзаиморасчетыСПокупателями.Записывать = Истина;
	
	//-------------------- Блокировка ------------------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрНакопления.ВзаиморасчетыСПокупателями");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	Блокировка.Заблокировать();
	//--------------------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыСПокупателямиОстатки.Проект КАК Проект,
		|	ВзаиморасчетыСПокупателямиОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ВзаиморасчетыСПокупателямиОстатки.СуммаВВалютеОстаток КАК СуммаВВалютеОстаток
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыСПокупателями.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Проект <> &ПроектАванс) КАК ВзаиморасчетыСПокупателямиОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВзаиморасчетыСПокупателямиОстатки.Проект.ДатаОплаты";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ПроектАванс", Справочники.Проекты.Авансы);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СуммаРаспределения	= СуммаПлатежа;
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Погашение			= Мин(СуммаРаспределения, Выборка.СуммаОстаток); 
			
			Движение = Движения.ВзаиморасчетыСПокупателями.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Проект = Выборка.Проект;
			Движение.Сумма = Погашение;
			Если Погашение = Выборка.СуммаОстаток Тогда
				ПогашениеВал	= Выборка.СуммаВВалютеОстаток;			
			Иначе	
				ПогашениеВал	= Выборка.СуммаВВалютеОстаток * Погашение/Выборка.СуммаОстаток;			
			КонецЕсли; 
			Движение.СуммаВВалюте = ПогашениеВал;
			
			СуммаРаспределения	= СуммаРаспределения - Погашение;		
		КонецЦикла;
	КонецЕсли;
	
	// Аванс
	Если СуммаРаспределения > 0 Тогда
		Движение = Движения.ВзаиморасчетыСПокупателями.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Справочники.Проекты.Авансы;
		Движение.Сумма = СуммаРаспределения;
	КонецЕсли;

КонецПроцедуры
