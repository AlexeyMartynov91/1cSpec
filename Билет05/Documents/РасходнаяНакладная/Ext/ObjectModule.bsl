
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ВзаиморасчетыСПокупателями Расход
	Движения.ВзаиморасчетыСПокупателями.Очистить();
	Движения.ВзаиморасчетыСПокупателями.БлокироватьДляИзменения	= Истина;
	Движения.ВзаиморасчетыСПокупателями.Записывать = Истина;
	
	ПроектАвансы	= Справочники.Проекты.Авансы;
	
	//------------- Блокировка -----------------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрНакопления.ВзаиморасчетыСПокупателями");		
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлБлокировки.УстановитьЗначение("Проект", ПроектАвансы);
	Блокировка.Заблокировать();
	//------------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	-ВзаиморасчетыСПокупателямиОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ВзаиморасчетыСПокупателямиОстатки.Контрагент КАК Контрагент
		|ИЗ
		|	РегистрНакопления.ВзаиморасчетыСПокупателями.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Проект = &ПроектАванс) КАК ВзаиморасчетыСПокупателямиОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = ВЫРАЗИТЬ(&Проект КАК Справочник.Проекты).Валюта) КАК КурсыВалютСрезПоследних";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("ПроектАванс", ПроектАвансы);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	РезультатАвансы	= РезультатыЗапроса[0];
	РезультатКурс	= РезультатыЗапроса[1];
	
	Курс	= 1;
	Если НЕ РезультатКурс.Пустой() Тогда
		ДанныеПоКурсуВалюты		= РезультатКурс.Выбрать();
		ДанныеПоКурсуВалюты.Следующий();
		Курс	= ДанныеПоКурсуВалюты.Курс;
	КонецЕсли; 
	
	ПогашениеАванса	= 0;
	
	Если НЕ РезультатАвансы.Пустой() Тогда
		ДанныеПоАвансам		= РезультатАвансы.Выбрать();
		ДанныеПоАвансам.Следующий();
		СуммаАванса	= ДанныеПоАвансам.СуммаОстаток;
		ПогашениеАванса	= Мин(СуммаАванса, СуммаПоДокументу);
		
		Движение = Движения.ВзаиморасчетыСПокупателями.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = ПроектАвансы;
		Движение.Сумма = ПогашениеАванса;
	КонецЕсли; 
	
	// если есть остаток, то это долг по проекту 
	ОстатокДолга	= СуммаПоДокументу - ПогашениеАванса;
	Если ОстатокДолга <> 0 Тогда
		Движение = Движения.ВзаиморасчетыСПокупателями.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Проект = Проект;
		Движение.Сумма = ОстатокДолга;
		Движение.СуммаВВалюте	= ОстатокДолга / Курс; 
	КонецЕсли;

	
	//------------- Движения по регистру бухгалтерии --------------
	
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.Записывать = Истина;
	
	//---------------- Блокировка ------------------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");	
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлБлокировки.ИсточникДанных	= СписокНоменклатуры;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
//	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер, "ИнвНомер");	// !! смотрим себестоимость по номенклатуре
	Блокировка.Заблокировать();
	//----------------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.ИнвНомер КАК ИнвНомер,
		|	РасходнаяНакладнаяСписокНоменклатуры.Сумма КАК Сумма,
		|	РасходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.ИнвНомер КАК ИнвНомер,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	ЕСТЬNULL(ЖурналПроводокОстаткиНоменкл.СуммаОстаток, 0) КАК СуммаОстатокНоменк,
		|	ЕСТЬNULL(ЖурналПроводокОстаткиНоменкл.КоличествоОстаток, 0) КАК КолОстатокНоменк,
		|	ЕСТЬNULL(ЖурналПроводокОстаткиИнвНом.КоличествоОстаток, 0) КАК КолОстатокИнвНомер,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ДокТЧ.ИнвНомер.Представление КАК ИнвНомерПредставление
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &Счет,
		|				&ВидыСубконтоНом,
		|				Субконто1 В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЖурналПроводокОстаткиНоменкл
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстаткиНоменкл.Субконто1
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &Счет,
		|				&ВидыСубконтоНомИнвНом,
		|				(Субконто1, Субконто2) В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура,
		|						вт_ДокТЧ.ИнвНомер
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЖурналПроводокОстаткиИнвНом
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстаткиИнвНом.Субконто1
		|			И вт_ДокТЧ.ИнвНомер = ЖурналПроводокОстаткиИнвНом.Субконто2";
	
	ВидыСубконтоНомИнвНом		= Новый Массив(2);
	ВидыСубконтоНомИнвНом[0]	= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыСубконтоНомИнвНом[1]	= ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер;
	
	ВидыСубконтоНом		= Новый Массив(1);
	ВидыСубконтоНом[0]	= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	
	Запрос.УстановитьПараметр("ВидыСубконтоНомИнвНом", ВидыСубконтоНомИнвНом);
	Запрос.УстановитьПараметр("ВидыСубконтоНом", ВидыСубконтоНом);	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Управленческий.Товары);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
		
	Выборка	= РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КолОстатокИнвНомер Тогда
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Не хватает товара %1 с инвентарным номером %2 (1шт.)", Выборка.НоменклатураПредставление, Выборка.ИнвНомерПредставление);  				
			Сообщение.Сообщить();
			Отказ	= Истина;
		КонецЕсли; 		
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли; 
		
		Если Выборка.Количество = Выборка.КолОстатокНоменк Тогда
			Себестоимость = Выборка.СуммаОстатокНоменк;
		Иначе
			Себестоимость = Выборка.СуммаОстатокНоменк * 1 / Выборка.КолОстатокНоменк;
		КонецЕсли;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
		Движение.Период = Дата;
		Движение.Сумма = Себестоимость;
		Движение.КоличествоКт	= 1;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер] = Выборка.ИнвНомер;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Подразделения] = Подразделение;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер] = Выборка.ИнвНомер;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
		Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.Период = Дата;
		Движение.Сумма = Выборка.Сумма;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер] = Выборка.ИнвНомер;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Подразделения] = Подразделение;
		
	КонецЦикла;

КонецПроцедуры
