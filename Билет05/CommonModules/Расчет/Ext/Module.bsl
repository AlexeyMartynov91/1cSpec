Процедура РассчитатьНачисленияУдержания(Регистратор) Экспорт

	// Оклад
	НаборЗаписей	= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);	
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия КАК Норма,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК Факт,
		|	ЗначенияОкладов.Оклад КАК Оклад
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(ВидРасчета = &ВидРасчетаОклад) КАК ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияОкладов КАК ЗначенияОкладов
		|		ПО ОсновныеНачисленияДанныеГрафика.Подразделение = ЗначенияОкладов.Подразделение
		|			И ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия >= ЗначенияОкладов.ЧасовОт
		|			И ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия < ЗначенияОкладов.ЧасовДо
		|ГДЕ
		|	ОсновныеНачисленияДанныеГрафика.Регистратор = &Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска		= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.Оклад * Выборка.Факт / Выборка.Норма;	
			Запись.Параметр		= Выборка.Оклад;
		КонецЕсли;
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);	
	
	// Надбавка
	НаборЗаписей	= РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);	
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДопНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ДопНачисления.Параметр, 0) КАК Параметр,
		|	ЕСТЬNULL(ЖурналПроводокОбороты.СуммаОборотКт, 0) КАК СуммаПродаж
		|ИЗ
		|	РегистрРасчета.ДопНачисления КАК ДопНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Обороты(&ДатаН, &ДатаК, , Счет = &Счет, &ВидыСубконто, , , ) КАК ЖурналПроводокОбороты
		|		ПО ДопНачисления.Подразделение = ЖурналПроводокОбороты.Субконто3
		|ГДЕ
		|	ДопНачисления.Регистратор = &Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	ВидыСубконто		= Новый Массив(3);
	ВидыСубконто[0]		= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыСубконто[1]		= ПланыВидовХарактеристик.ВидыСубконто.ИнвНомер;
	ВидыСубконто[2]		= ПланыВидовХарактеристик.ВидыСубконто.Подразделения;
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("ДатаК", КонецМесяца(ДобавитьМесяц(Регистратор.Дата, -1)));
	Запрос.УстановитьПараметр("ДатаН", НачалоМесяца(ДобавитьМесяц(Регистратор.Дата, -1)));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Управленческий.ПрибылиУбытки);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска		= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.СуммаПродаж * Выборка.Параметр / 100;	
		КонецЕсли;
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);	
	
	// Штраф
	НаборЗаписей	= РегистрыРасчета.Удержания.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);	
	НаборЗаписей.Прочитать();
	
	Для каждого Запись Из НаборЗаписей Цикл
		Запись.Результат	= 100 * Запись.Параметр;	
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);	

КонецПроцедуры
