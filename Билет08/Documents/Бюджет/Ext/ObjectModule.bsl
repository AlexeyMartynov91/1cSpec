
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр Бюджет 
	Движения.Бюджет.Очистить();
	Движения.Бюджет.БлокироватьДляИзменения	= Истина;
	Движения.Бюджет.Записывать = Истина;

	ДоляБюджетаПоМесяцам	= Новый Соответствие;
	
	НачалоТекущегоПериода	= ПериодНачало;
	ДнейЗаВесьПериод		= (ПериодКонец - ПериодНачало)/86400 + 1;
	Пока НачалоТекущегоПериода < ПериодКонец Цикл
	    КонецТекущегоПериода	= Мин(КонецМесяца(НачалоТекущегоПериода), КонецДня(ПериодКонец));
		
		ДнейВТекущемПериоде		= (КонецТекущегоПериода - НачалоТекущегоПериода + 1) / 86400;
		ДоляБюджетаПоМесяцам.Вставить(НачалоМесяца(НачалоТекущегоПериода), ДнейВТекущемПериоде/ДнейЗаВесьПериод);
		
		НачалоТекущегоПериода	= КонецТекущегоПериода	+1;
	КонецЦикла;	
	
	Для Каждого СтЗатрат Из СписокСтатейЗатрат Цикл
		План		= 0;
		Превышение	= 0;
		Для каждого ДоляБюджетаМес Из ДоляБюджетаПоМесяцам Цикл
			Движение = Движения.Бюджет.Добавить();
			Движение.Период = ДоляБюджетаМес.Ключ;
			Движение.Подразделение = Подразделение;
			Движение.СтатьяЗатрат = СтЗатрат.СтатьяЗатрат;
			Движение.План = СтЗатрат.Сумма * ДоляБюджетаМес.Значение;
			Движение.ПланПревышение = СтЗатрат.ВозможноеПревышение * ДоляБюджетаМес.Значение;
			
			План		= План + Движение.План;
			Превышение	= Превышение + Движение.ПланПревышение;
		КонецЦикла; 
		
		Если План <> СтЗатрат.Сумма Тогда
			Движение.План = Движение.План - (План - СтЗатрат.Сумма);
		КонецЕсли;
		Если Превышение <> СтЗатрат.ВозможноеПревышение Тогда
			Движение.ПланПревышение = Движение.ПланПревышение - (Превышение - СтЗатрат.ВозможноеПревышение);
		КонецЕсли;
	КонецЦикла;
	

КонецПроцедуры
