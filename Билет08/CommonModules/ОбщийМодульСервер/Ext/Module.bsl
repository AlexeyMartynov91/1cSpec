
Процедура ОбработкаПроведенияДокументовОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	
	// регистр Бюджет 
	Источник.Движения.Бюджет.Очистить();
	Источник.Движения.Бюджет.БлокироватьДляИзменения	= Истина;
	Источник.Движения.Бюджет.Записывать = Истина;
	
	Движение				= Источник.Движения.Бюджет.Добавить();
	Движение.Период 		= Источник.Дата;
	Движение.Подразделение	= Источник.Подразделение;
	Движение.СтатьяЗатрат	= Источник.СтатьяЗатрат;
	Движение.Факт			= Источник.СуммаПоДокументу;
	
	Источник.Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетОбороты.ФактОборот - БюджетОбороты.ПланОборот КАК ФактБольшеПлана,
		|	БюджетОбороты.ФактОборот - БюджетОбороты.ПланОборот - БюджетОбороты.ПланПревышениеОборот КАК ФактБольшеПланаИПревышения
		|ПОМЕСТИТЬ вт_РасчетПревышений
		|ИЗ
		|	РегистрНакопления.Бюджет.Обороты(
		|			&ДатаН,
		|			&ДатаК,
		|			,
		|			Подразделение = &Подразделение
		|				И СтатьяЗатрат = &СтатьяЗатрат) КАК БюджетОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_РасчетПревышений.ФактБольшеПлана КАК ФактБольшеПлана,
		|	вт_РасчетПревышений.ФактБольшеПланаИПревышения КАК ФактБольшеПланаИПревышения
		|ИЗ
		|	вт_РасчетПревышений КАК вт_РасчетПревышений
		|ГДЕ
		|	вт_РасчетПревышений.ФактБольшеПлана > 0";
	
	Запрос.УстановитьПараметр("ДатаК", КонецМесяца(Источник.Дата));
	Запрос.УстановитьПараметр("ДатаН", НачалоМесяца(Источник.Дата));
	Запрос.УстановитьПараметр("Подразделение",	Источник.Подразделение);
	Запрос.УстановитьПараметр("СтатьяЗатрат",	Источник.СтатьяЗатрат);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Сообщение		= Новый СообщениеПользователю;
		Если Выборка.ФактБольшеПланаИПревышения > 0 Тогда
			Сообщение.Текст	= СтрШаблон("Превышение запланированного бюджета сверх допустимого на %1", Выборка.ФактБольшеПланаИПревышения);
		Иначе
			Сообщение.Текст	= СтрШаблон("Превышение запланированного бюджета в пределах допустимого на %1", Выборка.ФактБольшеПлана);
		КонецЕсли;
		Сообщение.Сообщить();
		
	КонецЕсли;


КонецПроцедуры
