Процедура РассчитатьНаисления(Регистратор) Экспорт
	НаборЗаписей	= РегистрыРасчета.Начисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	КурсДоллара	= РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Регистратор.Дата, Новый Структура("Валюта", Справочники.Валюты.НайтиПоКоду("840",Истина))).Курс;
	КурсЕвро	= РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Регистратор.Дата, Новый Структура("Валюта", Справочники.Валюты.НайтиПоКоду("978",Истина))).Курс;
	
	Если КурсДоллара=0 ИЛИ КурсЕвро=0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В регистре курсов валют должны быть заполнены курсы доллара и евро";
		Сообщение.Сообщить();
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	// Оплата по часовому тарифуо
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
//		|	ЕСТЬNULL(НачисленияДанныеГрафика.РабочихЧасовПериодДействия, 0) КАК ЧасовПлан,
		|	ЕСТЬNULL(НачисленияДанныеГрафика.РабочихЧасовФактическийПериодДействия, 0) КАК ЧасовФакт,
		|	ЕСТЬNULL(ТарифныеСтавки.РазмерСтавки, 0) КАК ТарифнаяСтавка
		|ИЗ
		|	РегистрРасчета.Начисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &ВидРасчетаОплатаПоЧасовомуТарифу) КАК НачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки КАК ТарифныеСтавки
		|		ПО НачисленияДанныеГрафика.Подразделение = ТарифныеСтавки.Подразделение
		|			И НачисленияДанныеГрафика.РабочихЧасовФактическийПериодДействия > ТарифныеСтавки.ФактическиОтработанныеЧасы.ОтработанноОт
		|			И НачисленияДанныеГрафика.РабочихЧасовФактическийПериодДействия <= ТарифныеСтавки.ФактическиОтработанныеЧасы.ОтработанноДо
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ВидРасчетаОплатаПоЧасовомуТарифу", ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасам);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса	= Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка			= РезультатЗапроса.Выбрать();
		СтруктураПоиска	= Новый Структура("НомерСтроки", ); 
		
		Для каждого Запись Из НаборЗаписей Цикл
			СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
			Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
				Запись.РабочихЧасов			= Выборка.ЧасовФакт;
				Запись.РезультатВДолларах	= Выборка.ТарифнаяСтавка * Выборка.ЧасовФакт / КурсДоллара;
				Запись.РезультатВЕвро		= Выборка.ТарифнаяСтавка * Выборка.ЧасовФакт / КурсЕвро;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	// сверхурочные
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияБазаНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(НачисленияБазаНачисления.РезультатВДолларахБаза, 0) КАК РезультатВДолларахБаза,
		|	ЕСТЬNULL(НачисленияБазаНачисления.РезультатВЕвроБаза, 0) КАК РезультатВЕвроБаза,
		|	ЕСТЬNULL(НачисленияБазаНачисления.РабочихЧасовБаза, 0) КАК РабочихЧасовБаза,
		|	ЕСТЬNULL(НачисленияДанныеГрафика.СверхурочныхЧасовФактическийПериодДействия, 0) КАК СверхурочныхЧасовФакт
		|ИЗ
		|	РегистрРасчета.Начисления.ДанныеГрафика КАК НачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления.БазаНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Регистратор
		|					И ПериодРегистрации = &ПериодРегистрации
		|					И ВидРасчета = &ВидРасчетаСверхурочные) КАК НачисленияБазаНачисления
		|		ПО НачисленияДанныеГрафика.НомерСтроки = НачисленияБазаНачисления.НомерСтроки";
	
	ИзмеренияРегистра	= Новый Массив(1);
	ИзмеренияРегистра[0]= "Сотрудник";
	
	Запрос.УстановитьПараметр("ВидРасчетаСверхурочные", ПланыВидовРасчета.ОсновныеНачисления.Сверхурочные);
	Запрос.УстановитьПараметр("Измерения", ИзмеренияРегистра);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса	= Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка			= РезультатЗапроса.Выбрать();
		СтруктураПоиска	= Новый Структура("НомерСтроки", ); 
		
		Для каждого Запись Из НаборЗаписей Цикл
			СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
			Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
				Если Выборка.РабочихЧасовБаза = 0 Тогда
					Продолжить;
				КонецЕсли;
				Запись.РезультатВДолларах	= Выборка.РезультатВДолларахБаза * Выборка.СверхурочныхЧасовФакт / Выборка.РабочихЧасовБаза;
				Запись.РезультатВЕвро		= Выборка.РезультатВЕвроБаза	 * Выборка.СверхурочныхЧасовФакт / Выборка.РабочихЧасовБаза;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	НаборЗаписей.Записать(, Истина)

КонецПроцедуры
