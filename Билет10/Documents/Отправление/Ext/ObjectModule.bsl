
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.Записать();	
	
	//----------------- Блокировка -----------------------
	Блокировка				= Новый БлокировкаДанных;
	ЭлБлокировки			= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим		= РежимБлокировкиДанных.Исключительный;	
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, СкладОтправитель);
	ЭлБлокировки.ИсточникДанных	= СписокТоваров;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	//----------------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтправлениеСписокТоваров.Номенклатура КАК Номенклатура,
		|	СУММА(ОтправлениеСписокТоваров.Количество) КАК Количество
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.Отправление.СписокТоваров КАК ОтправлениеСписокТоваров
		|ГДЕ
		|	ОтправлениеСписокТоваров.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтправлениеСписокТоваров.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	ЕСТЬNULL(ЖурналПроводокОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ЖурналПроводокОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&ВидыСубконтоТовары,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Субконто2 = &СкладОтправитель) КАК ЖурналПроводокОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстатки.Субконто1";
	
	ВидыСубконтоТовары		= Новый Массив(2);
	ВидыСубконтоТовары[0]	= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыСубконтоТовары[1]	= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	
	Запрос.УстановитьПараметр("ВидыСубконтоТовары", ВидыСубконтоТовары);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("СкладОтправитель", СкладОтправитель);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2", 
								Выборка.НоменклатураПредставление,СокрЛП(Выборка.Количество - Выборка.КоличествоОстаток));			
			Сообщение.Сообщить();		
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость	= Выборка.СуммаОстаток;
		Иначе
			Себестоимость	= Выборка.СуммаОстаток * Выборка.Количество / Выборка.КоличествоОстаток;
		КонецЕсли;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.ТоварыВПути;
		Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
		Движение.Период = Дата;
		Движение.КоличествоДт = Выборка.Количество;
		Движение.КоличествоКт = Выборка.Количество;
		Движение.Сумма = Себестоимость;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ДокументОтправления] = Ссылка;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = СкладОтправитель;
		
	КонецЦикла;
	
КонецПроцедуры
