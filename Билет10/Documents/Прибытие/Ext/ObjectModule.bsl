
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.Записать();	
	
	//----------------- Блокировка -----------------------
	Блокировка				= Новый БлокировкаДанных;
	ЭлБлокировки			= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим		= РежимБлокировкиДанных.Исключительный;	
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.ТоварыВПути);
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.ДокументОтправления, ДокументОтправления);
	ЭлБлокировки.ИсточникДанных	= СписокТоваров;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	//----------------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрибытиеСписокТоваров.Номенклатура КАК Номенклатура,
		|	СУММА(ПрибытиеСписокТоваров.Количество) КАК Количество
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.Прибытие.СписокТоваров КАК ПрибытиеСписокТоваров
		|ГДЕ
		|	ПрибытиеСписокТоваров.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПрибытиеСписокТоваров.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	ЕСТЬNULL(ЖурналПроводокОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ЖурналПроводокОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТоварыВПути,
		|				&ВидыСубконтоТоварыВПути,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Субконто2 = &ДокументОтправления) КАК ЖурналПроводокОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстатки.Субконто1";
	
	ВидыСубконтоТоварыВПути		= Новый Массив(2);
	ВидыСубконтоТоварыВПути[0]	= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыСубконтоТоварыВПути[1]	= ПланыВидовХарактеристик.ВидыСубконто.ДокументОтправления;
	
	Запрос.УстановитьПараметр("ВидыСубконтоТоварыВПути", ВидыСубконтоТоварыВПути);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("ДокументОтправления", ДокументОтправления);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СчетТоварыВПути", ПланыСчетов.Управленческий.ТоварыВПути);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2", 
								Выборка.НоменклатураПредставление, СокрЛП(Выборка.Количество - Выборка.КоличествоОстаток));			
			Сообщение.Сообщить();		
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость	= Выборка.СуммаОстаток;
		Иначе
			Себестоимость	= Выборка.СуммаОстаток * Выборка.Количество / Выборка.КоличествоОстаток;
		КонецЕсли;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
		Движение.СчетКт = ПланыСчетов.Управленческий.ТоварыВПути;
		Движение.Период = Дата;
		Движение.КоличествоДт = Выборка.Количество;
		Движение.КоличествоКт = Выборка.Количество;
		Движение.Сумма = Себестоимость;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = СкладПолучатель;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ДокументОтправления] = ДокументОтправления;
		
	КонецЦикла;
	
КонецПроцедуры
