
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПериодРегистрации	= НачалоМесяца(Дата);
	
	// регистр НачисленияСотрудникам
	Движения.НачисленияСотрудникам.Записать();
	Движения.НачисленияСотрудникам.Записывать = Истина;
	Для Каждого стр Из ОсновныеНачисления Цикл
		Движение = Движения.НачисленияСотрудникам.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, стр); 
		
		Движение.ПериодРегистрации	= ПериодРегистрации;
		Движение.БазовыйПериодНачало = стр.ПериодДействияНачало;
		Движение.БазовыйПериодКонец = стр.ПериодДействияКонец;
		Движение.Параметр = стр.ТарифнаяСтавка;
	КонецЦикла;

	// сторно
	СторноЗаписи	= Движения.НачисленияСотрудникам.ПолучитьДополнение();
	Для Каждого Сторно Из СторноЗаписи Цикл
		Движение = Движения.НачисленияСотрудникам.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Сторно); 
		
		Движение.Сторно	= Истина;
		Движение.ПериодРегистрации	= Сторно.ПериодРегистрацииСторно;
		Движение.ПериодДействияНачало = Сторно.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Сторно.ПериодДействияКонецСторно;
		Движение.БазовыйПериодНачало = Сторно.ПериодДействияНачалоСторно;
		Движение.БазовыйПериодКонец = Сторно.ПериодДействияКонецСторно;
	КонецЦикла;
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления(Ссылка);
	
	
КонецПроцедуры
