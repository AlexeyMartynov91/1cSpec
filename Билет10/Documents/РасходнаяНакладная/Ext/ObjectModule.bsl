
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Свойство КАК Свойство,
		|	РасходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Набор) КАК Набор
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Набор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставНаборовСрезПоследних.Набор КАК Набор,
		|	МАКСИМУМ(СоставНаборовСрезПоследних.Период) КАК Период
		|ПОМЕСТИТЬ вт_СоставНабора
		|ИЗ
		|	РегистрСведений.СоставНаборов.СрезПоследних(
		|			&Дата,
		|			Набор В
		|				(ВЫБРАТЬ
		|					вт_ДокТЧ.Номенклатура
		|				ИЗ
		|					вт_ДокТЧ КАК вт_ДокТЧ
		|				ГДЕ
		|					вт_ДокТЧ.Набор)) КАК СоставНаборовСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	СоставНаборовСрезПоследних.Набор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Свойство КАК Свойство,
		|	вт_ДокТЧ.Количество КАК Количество
		|ПОМЕСТИТЬ вт_БлюдаИПродукты
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|ГДЕ
		|	НЕ вт_ДокТЧ.Набор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СоставНаборов.Продукт, вт_ДокТЧ.Номенклатура),
		|	ЕСТЬNULL(вт_ДокТЧ.Свойство, ЗНАЧЕНИЕ(Справочник.СвойстваНоменклатуры.ПустаяСсылка)),
		|	вт_ДокТЧ.Количество * ЕСТЬNULL(СоставНаборов.Количество, 1)
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ вт_СоставНабора КАК вт_СоставНабора
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставНаборов КАК СоставНаборов
		|			ПО (СоставНаборов.Набор = вт_СоставНабора.Набор)
		|				И (СоставНаборов.Период = вт_СоставНабора.Период)
		|		ПО (вт_ДокТЧ.Номенклатура = СоставНаборов.Набор)
		|ГДЕ
		|	вт_ДокТЧ.Набор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_БлюдаИПродукты.Номенклатура КАК Номенклатура,
		|	вт_БлюдаИПродукты.Свойство КАК Свойство,
		|	СУММА(вт_БлюдаИПродукты.Количество) КАК Количество
		|ИЗ
		|	вт_БлюдаИПродукты КАК вт_БлюдаИПродукты
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_БлюдаИПродукты.Номенклатура,
		|	вт_БлюдаИПродукты.Свойство";
	
	Запрос.УстановитьПараметр("Дата", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат	= Запрос.Выполнить();
	Выборка	= Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Свойство = Выборка.Свойство;
		Движение.Количество = Выборка.Количество;
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ОстаткиНоменклатурыОстатки.Свойство.Представление КАК СвойствоПредставление,
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Превышение
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремениВключая,
		|			(Номенклатура, Свойство) В
		|				(ВЫБРАТЬ
		|					вт_БлюдаИПродукты.Номенклатура,
		|					вт_БлюдаИПродукты.Свойство
		|				ИЗ
		|					вт_БлюдаИПродукты КАК вт_БлюдаИПродукты)) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремениВключая", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	Результат	= Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Отказ	= Истина;
		
		Выборка	= Результат.Выбрать();
	   	Пока Выборка.Следующий() Цикл
			Сообщение		= Новый СообщениеПользователю;		
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 по свойству %2 в количестве %3.", 
								Выборка.НоменклатураПредставление, Выборка.СвойствоПредставление, СокрЛП(Выборка.Превышение));			
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
