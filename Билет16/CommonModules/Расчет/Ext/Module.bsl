Процедура Рассчитать(Регистратор, ПериодРегистрации, ДопОтбор = Неопределено) Экспорт
	// Оклад
	НаборЗаписей		= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия КАК ЧасовПлан,
		|	ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия КАК ЧасовФакт,
		|	ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия КАК ДнейФакт
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияДанныеГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка		= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки", );
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки		= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= ?(Выборка.ЧасовПлан = 0, 0, Запись.Параметр * Выборка.ЧасовФакт / Выборка.ЧасовПлан);
			Запись.ОтработаноДней	= Выборка.ДнейФакт;	
		КонецЕсли;	
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	
	// Компенсация
	СтавкаКомпенсации	= 0;
	СтавкаКомпенсации	= Константы.СтавкаКомпенсацииЗатратНаМобильный.Получить();
	
	НаборЗаписей		= РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДопНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДопНачисленияБазаОсновныеНачисления.ОтработаноДнейБаза КАК ОтработаноДней
		|ИЗ
		|	РегистрРасчета.ДопНачисления.БазаОсновныеНачисления(
		|			&Измерения,
		|			&Измерения,
		|			,
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И &ДопОтбор) КАК ДопНачисленияБазаОсновныеНачисления
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Измерения		= Новый Массив(2);
	Измерения[0]	= "Сотрудник";
	Измерения[1]	= "Подразделение";
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ДопОтбор", ДопОтбор);
	
	Если ДопОтбор <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &ДопОтбор", "И (Сотрудник, Подразделение) В (&ДопОтбор)");	
	Иначе	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &ДопОтбор", "");	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка		= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки", );
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки		= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= СтавкаКомпенсации * Выборка.ОтработаноДней;
		КонецЕсли;	
	КонецЦикла;
	
	НаборЗаписей.Записать();

КонецПроцедуры
