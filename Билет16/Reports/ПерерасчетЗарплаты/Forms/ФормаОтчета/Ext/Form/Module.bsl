
&НаСервере
Процедура ВыполнитьПересчетНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПерерасчетЗарплаты.ОбъектПерерасчета КАК Регистратор,
		|	ПерерасчетЗарплаты.Сотрудник КАК Сотрудник,
		|	ПерерасчетЗарплаты.Подразделение КАК Подразделение,
		|	НАЧАЛОПЕРИОДА(ПерерасчетЗарплаты.ОбъектПерерасчета.Дата, МЕСЯЦ) КАК ПериодРегистрации
		|ИЗ
		|	РегистрРасчета.ДопНачисления.ПерерасчетЗарплаты КАК ПерерасчетЗарплаты
		|ИТОГИ
		|	МАКСИМУМ(ПериодРегистрации)
		|ПО
		|	Регистратор";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДопОтбор	= Новый ТаблицаЗначений;
	ДопОтбор.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));	
	ДопОтбор.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.Подразделения"));	
	
	ВыборкаРегистратор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРегистратор.Следующий() Цикл
		ДопОтбор.Очистить();
		
		Выборка	= ВыборкаРегистратор.Выбрать();
		Пока Выборка.Следующий() Цикл
			НовСтрОтбора				= ДопОтбор.Добавить();
			НовСтрОтбора.Сотрудник		= Выборка.Сотрудник;			
			НовСтрОтбора.Подразделение	= Выборка.Подразделение;			
		КонецЦикла;
		
		Расчет.Рассчитать(ВыборкаРегистратор.Регистратор, ВыборкаРегистратор.ПериодРегистрации, ДопОтбор);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПересчет(Команда)
	ВыполнитьПересчетНаСервере();
	СкомпоноватьРезультат();	
КонецПроцедуры
