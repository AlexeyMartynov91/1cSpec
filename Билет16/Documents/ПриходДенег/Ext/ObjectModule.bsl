
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр Взаиморасчеты Расход
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();
	
	//----------- Блокировка -----------
	Блокировка				= Новый БлокировкаДанных;
	ЭлБлокировки			= Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлБлокировки.Режим		= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	Блокировка.Заблокировать();
	//----------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.Накладная КАК Накладная,
		|	ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(&МоментВремени, Контрагент = &Контрагент) КАК ВзаиморасчетыОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НакладнаяМоментВремени";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка		= РезультатЗапроса.Выбрать();
	
	СуммаКПогашению	= СуммаОплаты;
	
	Пока Выборка.Следующий() И СуммаКПогашению > 0 Цикл
		Погасить		= Мин(СуммаКПогашению, Выборка.СуммаОстаток);
		
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Выборка.Накладная;
		Движение.Сумма = Погасить;
	
		СуммаКПогашению	= СуммаКПогашению - Погасить;	
	КонецЦикла;
	
	Если СуммаКПогашению > 0 Тогда
		Отказ	= Истина;
		
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= СтрШаблон("Оплата превышает долг контрагента на %1. Авансов и переплат быть не может.", СокрЛП(СуммаКПогашению));		
	    Сообщение.Сообщить();
	КонецЕсли;

КонецПроцедуры
