
Процедура РассчитатьНачисления (Регистратор, НаборЗаписей) Экспорт
	
	// РАСЧЕТ ТАРИФА
	// Формула расчета: [Количество отработанных часов] * [Тарифная ставка]
	
	// Запрос данных для формирования движений, отработанные часы получаем из регистра данных табеля, 
	// а тарифную ставку - из регистра сведений СведенияОСотрудниках
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	ОсновныеНачисления.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ВТ_НачисленияПоТарифу
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
	|ГДЕ
	|	ОсновныеНачисления.Регистратор = &Регистратор
	|	И ОсновныеНачисления.ВидРасчета = &ВидРасчетаТариф
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НачисленияПоТарифу.НомерСтроки КАК НомерСтроки,
	|	ВТ_НачисленияПоТарифу.Сотрудник КАК Сотрудник,
	|	ВТ_НачисленияПоТарифу.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(ДанныеТабеляОбороты.ОтработаноЧасовОборот, 0) КАК ЧасовФакт,
	|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.ТарифнаяСтавка, 0) КАК ТарифнаяСтавка
	|ИЗ
	|	ВТ_НачисленияПоТарифу КАК ВТ_НачисленияПоТарифу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеТабеля.Обороты(
	|				&ПериодРегистрации,
	|				&КонецМесяца,
	|				,
	|				(Сотрудник, Подразделение) В
	|					(ВЫБРАТЬ
	|						ВТ_НачисленияПоТарифу.Сотрудник,
	|						ВТ_НачисленияПоТарифу.Подразделение
	|					ИЗ
	|						ВТ_НачисленияПоТарифу КАК ВТ_НачисленияПоТарифу)) КАК ДанныеТабеляОбороты
	|		ПО ВТ_НачисленияПоТарифу.Сотрудник = ДанныеТабеляОбороты.Сотрудник
	|			И ВТ_НачисленияПоТарифу.Подразделение = ДанныеТабеляОбороты.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
	|				&ПериодРегистрации,
	|				(Сотрудник, Подразделение) В
	|					(ВЫБРАТЬ
	|						ВТ_НачисленияПоТарифу.Сотрудник,
	|						ВТ_НачисленияПоТарифу.Подразделение
	|					ИЗ
	|						ВТ_НачисленияПоТарифу КАК ВТ_НачисленияПоТарифу)) КАК СведенияОСотрудникахСрезПоследних
	|		ПО ВТ_НачисленияПоТарифу.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
	|			И ВТ_НачисленияПоТарифу.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение";
	
	ПериодРегистрации = НачалоМесяца(Регистратор.Дата);
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчетаТариф", ПланыВидовРасчета.ОсновныеНачисления.Тариф);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПериодРегистрации));
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("НомерСтроки", 0);
	Выборка = Запрос.Выполнить().Выбрать();
	Для Каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки = Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат = Выборка.ЧасовФакт * Выборка.ТарифнаяСтавка;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	НаборЗаписей.Записать(); // Необходимо обязательно записать Тариф т.к. он составляет базу для премии
	
		
	// РАСЧЕТ ПРЕМИИ
	// Формула расчета: [Сумма базы (от тарифа)] * [Процент премии]
	
	Запрос.Текст =  
	"ВЫБРАТЬ
	|	ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза КАК База,
	|	ОсновныеНачисленияБазаОсновныеНачисления.Параметр КАК Процент
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
	|			&Измерения,
	|			&Измерения,
	|			,
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &ВидРасчетаПремия) КАК ОсновныеНачисленияБазаОсновныеНачисления";
	
	Измерения = Новый Массив(2);
	Измерения[0] = "Сотрудник";
	Измерения[1] = "Подразделение";
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ВидРасчетаПремия", ПланыВидовРасчета.ОсновныеНачисления.Премия);
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("НомерСтроки", 0);
	Выборка = Запрос.Выполнить().Выбрать();
	Для Каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки = Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат = Выборка.База * Выборка.Процент / 100;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	НаборЗаписей.Записать();

КонецПроцедуры