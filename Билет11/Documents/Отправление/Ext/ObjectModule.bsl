
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ДВИЖЕНИЯ ПО РЕГИСТРУ УПРАВЛЕНЧЕСКИЙ (СТАРАЯ МЕТОДИКА ПРОВЕДЕНИЯ)
	
	Движения.Управленческий.Записывать = Истина;
	Движения.Управленческий.Записать();
	
	// Установка блокировки данных в регистре Управленческий по счету Товары, складу документа и списку номенклатур табличной части
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);	
	Блокировка.Заблокировать();

	// Получение данных для формирования движений
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтправлениеСписокНоменклатуры.Номенклатура КАК Номенклатура,
	|	СУММА(ОтправлениеСписокНоменклатуры.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ТабЧасть
	|ИЗ
	|	Документ.Отправление.СписокНоменклатуры КАК ОтправлениеСписокНоменклатуры
	|ГДЕ
	|	ОтправлениеСписокНоменклатуры.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтправлениеСписокНоменклатуры.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТабЧасть.Номенклатура КАК Номенклатура,
	|	ВТ_ТабЧасть.Количество КАК КоличествоВДокументе,
	|	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ВТ_ТабЧасть.Номенклатура.Представление КАК НоменклатураПредставление
	|ИЗ
	|	ВТ_ТабЧасть КАК ВТ_ТабЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	|				&МоментВремени,
	|				Счет = &СчетТовары,
	|				&Субконто,
	|				Субконто1 В
	|						(ВЫБРАТЬ
	|							ВТ_ТабЧасть.Номенклатура
	|						ИЗ
	|							ВТ_ТабЧасть КАК ВТ_ТабЧасть)
	|					И Субконто2 = &Склад) КАК УправленческийОстатки
	|		ПО ВТ_ТабЧасть.Номенклатура = УправленческийОстатки.Субконто1";
	
	
	Субконто = Новый Массив(2);
	Субконто[0] = ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	Субконто[1] = ПланыВидовХарактеристик.ВидыСубконто.Склады;

	Запрос.УстановитьПараметр("Субконто", Субконто);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	
	// Обход результатов запроса, формирование движений в регистр бухгалтерии
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Превышение = Выборка.КоличествоВДокументе - Выборка.КоличествоОстаток;
			Если Превышение > 0 Тогда 
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2", 
					Выборка.НоменклатураПредставление, Превышение);						
				Сообщение.Сообщить();
				
				Отказ = Истина;				
			КонецЕсли;
			
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			
			// Проводка отражения отправления товара
			Проводка = Движения.Управленческий.Добавить();
			Проводка.Период = Дата;
			Проводка.СчетДт = ПланыСчетов.Управленческий.ТоварыВПути;
			Проводка.СчетКт = ПланыСчетов.Управленческий.Товары;
			
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ДокументОтправления] = Ссылка;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ДатаПрибытия] = ПланируемаяДатаПрибытия;
			
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
			
			Проводка.КоличествоДт = Выборка.КоличествоВДокументе;
			Проводка.КоличествоКт = Выборка.КоличествоВДокументе;
			
			Проводка.Сумма = Выборка.КоличествоВДокументе / Выборка.КоличествоОстаток * Выборка.СуммаОстаток;
			
		КонецЦикла;
		
	КонецЕсли;                 

	
КонецПроцедуры
