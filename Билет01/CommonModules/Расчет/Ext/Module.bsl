Процедура РассчитатьНачисления(Регистратор)	Экспорт
	// Основные начисления
	НаборЗаписей	= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);	
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЗначениеФактическийПериодДействия,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеБазовыйПериод КАК ЗначениеБазовыйПериод
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика КАК ОсновныеНачисленияДанныеГрафика
		|ГДЕ
		|	ОсновныеНачисленияДанныеГрафика.Регистратор = &Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.ЗначениеФактическийПериодДействия / Выборка.ЗначениеБазовыйПериод * Запись.Параметр;
		КонецЕсли; 			
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);
	
	// Доп. начисления
	НаборЗаписей	= РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);	
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДопНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДопНачисленияБазаОсновныеНачисления.РезультатБаза КАК РезультатБаза
		|ИЗ
		|	РегистрРасчета.ДопНачисления.БазаОсновныеНачисления(
		|			&МассивИзмерений,
		|			&МассивИзмеренийБазового,
		|			,
		|			(Регистратор, ПериодРегистрации) В
		|				(ВЫБРАТЬ
		|					&Регистратор,
		|					&ПериодРегистрации)) КАК ДопНачисленияБазаОсновныеНачисления
		|ГДЕ
		|	ДопНачисленияБазаОсновныеНачисления.Регистратор = &Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	МассивИзмерений			= Новый Массив;
	МассивИзмерений.Добавить("Сотрудник");
//	МассивИзмерений.Добавить("Подразделение");	// если в РР добавим измерение Подразделение
	
	МассивИзмеренийБазового	= Новый Массив;
	МассивИзмеренийБазового.Добавить("Сотрудник");
//	МассивИзмеренийБазового.Добавить("Подразделение");	// если в РР добавим измерение Подразделение
	
	Запрос.УстановитьПараметр("МассивИзмерений", МассивИзмерений);
	Запрос.УстановитьПараметр("МассивИзмеренийБазового", МассивИзмеренийБазового);
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.РезультатБаза * Запись.Параметр/100;
		КонецЕсли; 			
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);
	
КонецПроцедуры
