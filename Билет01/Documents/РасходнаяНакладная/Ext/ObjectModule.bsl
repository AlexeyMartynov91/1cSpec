
Процедура ОбработкаПроведения(Отказ, Режим)
	ОбработкаПроведенияОУ(Отказ, Режим);
	ОбработкаПроведенияБУ(Отказ, Режим);
КонецПроцедуры

Процедура ОбработкаПроведенияОУ(Отказ, Режим)
	// не буду из ПараметровСеанса брать, буду каждый раз устанавливать
	//УчетнаяПолитикаСписанияПартий	= ОбщийМодульСервер.УстановитьУчетнуюПолитику();
	
	// лучше не через общий модуль
	УчетнаяПолитикаСписанияПартий = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписанияПоПартиям;
	
	//Сообщение	= Новый СообщениеПользователю;
	//Сообщение.Текст	= "Учетная политика списания партий в параметре сеанса: "+СокрЛП(ПараметрыСеанса.УчетнаяПолитика);
	//Сообщение.Сообщить();
	//Сообщение.Текст	= "Учетная политика списания партий: "+СокрЛП(УчетнаяПолитикаСписанияПартий);
	//Сообщение.Сообщить();
	
	// регистр ОстаткиТоваров Расход 
	Движения.ОстаткиТоваров.Очистить();
	Движения.ОстаткиТоваров.БлокироватьДляИзменения	= Истина;	
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
		|	И НЕ РасходнаяНакладнаяТовары.Номенклатура.Услуга
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение		= Движения.ОстаткиТоваров.ДобавитьРасход();
		Движение.Период	= Дата;
		Движение.Склад	= Склад;
		Движение.Номенклатура	= Выборка.Номенклатура;
		Движение.Количество		= Выборка.Количество;
	КонецЦикла; 
	
//	Движения.ОстаткиТоваров.Записать();	// !! так не сбросится флаг	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Записать();

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	-ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК Нехватает,
		|	ОстаткиТоваровОстатки.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)
		|				И Склад = &Склад) КАК ОстаткиТоваровОстатки
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Отказ	= Истина;
		
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение	= Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2 на складе %3 (оперативный учет, регистр ""Остатки Товаров"")",
				Выборка.НоменклатураПредставление, Выборка.Нехватает, Склад);
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
	    Возврат;
	КонецЕсли; 
	
	//--------- Блокировка --------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрНакопления.СтоимостьТоваров");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.ИсточникДанных	= Товары;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//-----------------------------------

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	СтоимостьТоваровОстатки.Партия КАК Партия,
		|	СтоимостьТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	СтоимостьТоваровОстатки.СебестоимостьОстаток КАК СебестоимостьОстаток
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьТоваров.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК СтоимостьТоваровОстатки
		|		ПО вт_ДокТЧ.Номенклатура = СтоимостьТоваровОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтоимостьТоваровОстатки.Партия.МоментВремени ВОЗР
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";

	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	// если УчетнаяПолитикаСписанияПартий не установлена, то FIFO по умолчанию
	Если УчетнаяПолитикаСписанияПартий = Перечисления.МетодУчета.FIFO Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "МоментВремени ", "МоментВремени УБЫВ");	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// регистр СтоимостьТоваров Расход
	Движения.СтоимостьТоваров.Очистить();
	Движения.СтоимостьТоваров.Записывать = Истина;
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		КоличествоОстаток		= ВыборкаНоменклатура.КоличествоОстаток;
		ОбщееКоличествоСписания	= ВыборкаНоменклатура.Количество;
		
		Выборка	= ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ОбщееКоличествоСписания > 0 Цикл
			КоличествоСписания		= Мин(Выборка.КоличествоОстаток, Выборка.Количество);
			Если Выборка.КоличествоОстаток = КоличествоСписания Тогда
				СебестоимостьСписания	= Выборка.СебестоимостьОстаток;
			Иначе	
				СебестоимостьСписания	= Выборка.Количество/Выборка.КоличествоОстаток * Выборка.СебестоимостьОстаток;
			КонецЕсли;
			
			Движение = Движения.СтоимостьТоваров.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Партия = Выборка.Партия;
			Движение.Количество = КоличествоСписания;
			Движение.Себестоимость = СебестоимостьСписания;
			
			ОбщееКоличествоСписания		= ОбщееКоличествоСписания	- КоличествоСписания;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведенияБУ(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.БлокироватьДляИзменения	= Истина;	
	Движения.ЖурналПроводок.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяТовары.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяТовары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.Товары КАК РасходнаяНакладнаяТовары
		|ГДЕ
		|	РасходнаяНакладнаяТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//---------- Блокировка --------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.ПланСчетовБУ.Товары);
	ЭлБлокировки.ИсточникДанных	= РезультатЗапроса;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Товары, "Номенклатура");
	Блокировка.Заблокировать();
	//------------------------------------
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ вт_Стоимость
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &Счет,
		|				&МассивСубконто,
		|				Субконто1 В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЖурналПроводокОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстатки.Субконто1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Стоимость.Номенклатура КАК Номенклатура,
		|	вт_Стоимость.Количество КАК Количество,
		|	вт_Стоимость.Сумма КАК Сумма,
		|	вт_Стоимость.СуммаОстаток КАК Стоимость,
		|	ЖурналПроводокОстатки.Субконто2 КАК СрокГодности,
		|	ЖурналПроводокОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПРЕДСТАВЛЕНИЕ(вт_Стоимость.Номенклатура) КАК НоменклатураПредставление
		|ИЗ
		|	вт_Стоимость КАК вт_Стоимость
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &Счет,
		|				&МассивСубконто,
		|				Субконто1 В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЖурналПроводокОстатки
		|		ПО вт_Стоимость.Номенклатура = ЖурналПроводокОстатки.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЖурналПроводокОстатки.Субконто2.Дата
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Стоимость),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	МассивСубконто		= Новый Массив(2);
	МассивСубконто[0]	= ПланыВидовХарактеристик.ВидыСубконто.Товары;
	МассивСубконто[1]	= ПланыВидовХарактеристик.ВидыСубконто.СрокиГодности;
	
	Запрос.УстановитьПараметр("МассивСубконто", МассивСубконто);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ПланСчетовБУ.Товары);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаНоменклатура	= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.КоличествоОстаток < ВыборкаНоменклатура.Количество Тогда
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2 (бухгалтерский учет)", 
				ВыборкаНоменклатура.НоменклатураПредставление, СокрЛП(ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток));						                      
			Сообщение.Сообщить();
			Отказ	= Истина;
			Продолжить;
		КонецЕсли; 
		
		Стоимость					= ВыборкаНоменклатура.Стоимость; 
		СписатьПоНоменклатуре		= ВыборкаНоменклатура.Количество;
		
		Выборка		= ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() Цикл
		    Списать					= Мин(Выборка.КоличествоОстаток, СписатьПоНоменклатуре);
			СписатьПоНоменклатуре	= СписатьПоНоменклатуре - Списать;
			
			Если (ВыборкаНоменклатура.КоличествоОстаток = ВыборкаНоменклатура.Количество) 
				И СписатьПоНоменклатуре = 0 Тогда 
				Себестомость		= Стоимость;	
			Иначе
				Себестомость		= Списать / ВыборкаНоменклатура.КоличествоОстаток * ВыборкаНоменклатура.Стоимость;	
			КонецЕсли;
			
			Стоимость				= Стоимость - Себестомость; 

			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.Период = Дата;
			Движение.СчетДт = ПланыСчетов.ПланСчетовБУ.ПрибылиИУбытки;
			Движение.СчетКт = ПланыСчетов.ПланСчетовБУ.Товары;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Товары] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СрокиГодности] = Выборка.СрокГодности;
			Движение.КоличествоКт	= Списать;
			Движение.Сумма			= Себестомость;
		КонецЦикла;
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	Движение = Движения.ЖурналПроводок.Добавить();
	Движение.СчетДт = ПланыСчетов.ПланСчетовБУ.Покупатели;
	Движение.СчетКт = ПланыСчетов.ПланСчетовБУ.ПрибылиИУбытки;
	Движение.Период = Дата;
	Движение.Сумма = Товары.Итог("Сумма");

КонецПроцедуры
