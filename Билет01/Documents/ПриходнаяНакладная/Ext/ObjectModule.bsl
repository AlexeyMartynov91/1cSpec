
Процедура ОбработкаПроведения(Отказ, Режим)
	ОбработкаПроведенияОУ(Отказ, Режим);
	ОбработкаПроведенияБУ(Отказ, Режим);
КонецПроцедуры

Процедура ОбработкаПроведенияОУ(Отказ, Режим)
	// регистр ОстаткиТоваров Приход и регистр СтоимостьТоваров Приход
	Движения.ОстаткиТоваров.Очистить();
	Движения.СтоимостьТоваров.Очистить();
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.СтоимостьТоваров.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокТоваровИУслуг.Номенклатура КАК Номенклатура,
		|	СУММА(ПриходнаяНакладнаяСписокТоваровИУслуг.Количество) КАК Количество,
		|	СУММА(ПриходнаяНакладнаяСписокТоваровИУслуг.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокТоваровИУслуг КАК ПриходнаяНакладнаяСписокТоваровИУслуг
		|ГДЕ
		|	НЕ ПриходнаяНакладнаяСписокТоваровИУслуг.Номенклатура.Услуга
		|	И ПриходнаяНакладнаяСписокТоваровИУслуг.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПриходнаяНакладнаяСписокТоваровИУслуг.Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Склад = Склад;
		Движение.Количество = Выборка.Количество;
		
		Движение = Движения.СтоимостьТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Партия = Ссылка;
		Движение.Количество = Выборка.Количество;
		Движение.Себестоимость = Выборка.Сумма;
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаПроведенияБУ(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.Записывать = Истина;
	Для Каждого стр Из СписокТоваровИУслуг Цикл
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.ПланСчетовБУ.Товары;
		Движение.СчетКт = ПланыСчетов.ПланСчетовБУ.Поставщики;
		Движение.Период = Дата;
		Движение.КоличествоДт = стр.Количество;
		Движение.Сумма = стр.Сумма;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Товары] = стр.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.СрокиГодности] = стр.СрокГодности;
	КонецЦикла;

КонецПроцедуры
