
Процедура ОбработкаПроведения(Отказ, Режим) 
	ЗаписатьДанные();
КонецПроцедуры

Процедура ЗаписатьДанные() Экспорт
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Очистить();
	Движения.ОсновныеНачисления.Записывать = Истина;
	
	Для Каждого стр Из Начисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, стр);
		Движение.ПериодРегистрации = Дата;
	КонецЦикла;

	// регистр ДопНачисления
	Движения.ДопНачисления.Очистить();
	Движения.ДопНачисления.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыДопНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыДопНачисления.ВидРасчета КАК ВидРасчета,
		|	НачислениеЗарплатыДопНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	НачислениеЗарплатыДопНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	НачислениеЗарплатыДопНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	НачислениеЗарплатыДопНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
		|	РАЗНОСТЬДАТ(НачислениеЗарплатыДопНачисления.Сотрудник.НачалоСтажа, &Дата, ДЕНЬ) КАК Стаж,
		|	НачислениеЗарплатыДопНачисления.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ вт_ДопНачисления
		|ИЗ
		|	Документ.НачислениеЗарплаты.ДопНачисления КАК НачислениеЗарплатыДопНачисления
		|ГДЕ
		|	НачислениеЗарплатыДопНачисления.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДопНачисления.Сотрудник КАК Сотрудник,
		|	вт_ДопНачисления.ВидРасчета КАК ВидРасчета,
		|	вт_ДопНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	вт_ДопНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	вт_ДопНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	вт_ДопНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
		|	вт_ДопНачисления.Стаж КАК Стаж,
		|	ПроцентПремииСрезПоследних.ПроцентПремии КАК ПроцентПремии,
		|	вт_ДопНачисления.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	вт_ДопНачисления КАК вт_ДопНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентПремии.СрезПоследних(&Дата, ) КАК ПроцентПремииСрезПоследних
		|		ПО вт_ДопНачисления.Стаж > ПроцентПремииСрезПоследних.ТрудовойСтаж.СтажОт
		|			И вт_ДопНачисления.Стаж <= ПроцентПремииСрезПоследних.ТрудовойСтаж.СтажДо";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ДопНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.ПериодРегистрации = Дата;
		Движение.Параметр = Выборка.ПроцентПремии;
	КонецЦикла;

	Движения.Записать();
	
	Расчет.РассчитатьНачисления(Ссылка);
	
КонецПроцедуры




