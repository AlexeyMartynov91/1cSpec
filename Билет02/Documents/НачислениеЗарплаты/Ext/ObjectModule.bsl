
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр НачисленияСотрдникам
	Движения.НачисленияСотрдникам.Очистить();
	Движения.НачисленияСотрдникам.Записывать = Истина;
	
	// Оклад
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыНачисления.НомерСтроки КАК НомерСтроки,
		|	НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
		|	НачислениеЗарплатыНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	НачислениеЗарплатыНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	НачислениеЗарплатыНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	НачислениеЗарплатыНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
		|	НачислениеЗарплатыНачисления.Параметр КАК Параметр
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.НачислениеЗарплаты.Начисления КАК НачислениеЗарплатыНачисления
		|ГДЕ
		|	НачислениеЗарплатыНачисления.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Сотрудник КАК Сотрудник,
		|	вт_ДокТЧ.Подразделение КАК Подразделение,
		|	МАКСИМУМ(СведеньяОСотрдниках.Период) КАК Период
		|ПОМЕСТИТЬ вт_СрезНачОкладов
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведеньяОСотрдниках КАК СведеньяОСотрдниках
		|		ПО вт_ДокТЧ.Сотрудник = СведеньяОСотрдниках.Сотрудник
		|			И вт_ДокТЧ.Подразделение = СведеньяОСотрдниках.Подразделение
		|			И вт_ДокТЧ.ПериодДействияНачало > СведеньяОСотрдниках.Период
		|ГДЕ
		|	вт_ДокТЧ.ВидРасчета = &ВидРасчетаОклад
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_ДокТЧ.Сотрудник,
		|	вт_ДокТЧ.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.НомерСтроки КАК НомерСтроки,
		|	вт_ДокТЧ.Сотрудник КАК Сотрудник,
		|	вт_ДокТЧ.ВидРасчета КАК ВидРасчета,
		|	вт_ДокТЧ.Подразделение КАК Подразделение,
		|	вт_ДокТЧ.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	вт_ДокТЧ.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	вт_ДокТЧ.ПериодДействияНачало КАК ПериодДействияНачало,
		|	вт_ДокТЧ.ПериодДействияКонец КАК ПериодДействияКонец,
		|	СведеньяОСотрдниках.Оклад КАК Параметр,
		|	СведеньяОСотрдниках.Период КАК Период,
		|	вт_ДокТЧ.Параметр КАК ПроцентПремии
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведеньяОСотрдниках КАК СведеньяОСотрдниках
		|		ПО вт_ДокТЧ.Сотрудник = СведеньяОСотрдниках.Сотрудник
		|			И вт_ДокТЧ.Подразделение = СведеньяОСотрдниках.Подразделение
		|			И вт_ДокТЧ.ПериодДействияНачало <= СведеньяОСотрдниках.Период
		|			И вт_ДокТЧ.ПериодДействияКонец >= СведеньяОСотрдниках.Период
		|ГДЕ
		|	вт_ДокТЧ.ВидРасчета = &ВидРасчетаОклад
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вт_ДокТЧ.НомерСтроки,
		|	вт_ДокТЧ.Сотрудник,
		|	вт_ДокТЧ.ВидРасчета,
		|	вт_ДокТЧ.Подразделение,
		|	вт_ДокТЧ.БазовыйПериодНачало,
		|	вт_ДокТЧ.БазовыйПериодКонец,
		|	вт_ДокТЧ.ПериодДействияНачало,
		|	вт_ДокТЧ.ПериодДействияКонец,
		|	СведеньяОСотрдниках.Оклад,
		|	СведеньяОСотрдниках.Период,
		|	вт_ДокТЧ.Параметр
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведеньяОСотрдниках КАК СведеньяОСотрдниках
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_СрезНачОкладов КАК вт_СрезНачОкладов
		|			ПО СведеньяОСотрдниках.Сотрудник = вт_СрезНачОкладов.Сотрудник
		|				И СведеньяОСотрдниках.Подразделение = вт_СрезНачОкладов.Подразделение
		|				И СведеньяОСотрдниках.Период = вт_СрезНачОкладов.Период
		|		ПО вт_ДокТЧ.Сотрудник = СведеньяОСотрдниках.Сотрудник
		|			И вт_ДокТЧ.Подразделение = СведеньяОСотрдниках.Подразделение
		|ГДЕ
		|	вт_ДокТЧ.ВидРасчета = &ВидРасчетаОклад
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки,
		|	Период УБЫВ
		|ИТОГИ ПО
		|	Сотрудник";
//		|	ВидРасчета";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.Начисления.Оклад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСотрудник = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСотрудник.Следующий() Цикл
//		ВыборкаВидРасчета = ВыборкаСотрудник.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
//		Пока ВыборкаВидРасчета.Следующий() Цикл
		    ПредПериодОклада	= Неопределено;
//			Выборка = ВыборкаВидРасчета.Выбрать();
			Выборка = ВыборкаСотрудник.Выбрать();
			Пока Выборка.Следующий() Цикл
				Движение = Движения.НачисленияСотрдникам.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Выборка);
				//Если Выборка.ВидРасчета = ПланыВидовРасчета.Начисления.Премия Тогда
				//	Движение.Параметр	= Выборка.ПроцентПремии; 	
				//КонецЕсли;
				Движение.ПериодРегистрации	= НачалоМесяца(Дата);
				Если Выборка.ПериодДействияНачало	< Выборка.Период Тогда
					Движение.ПериодДействияНачало	= Выборка.Период;
				КонецЕсли; 
				Если ПредПериодОклада <> Неопределено Тогда
					Движение.ПериодДействияКонец	= ПредПериодОклада - 24*60*60;
				КонецЕсли;
				ПредПериодОклада	= Выборка.Период;
				Если Движение.ПериодДействияКонец < Движение.ПериодДействияНачало Тогда
					Движения.НачисленияСотрдникам.Удалить(Движение);	
				КонецЕсли;
			КонецЦикла;
//		КонецЦикла;
	КонецЦикла;

	// Премия
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_ДокТЧ.НомерСтроки КАК НомерСтроки,
		|	вт_ДокТЧ.Сотрудник КАК Сотрудник,
		|	вт_ДокТЧ.ВидРасчета КАК ВидРасчета,
		|	вт_ДокТЧ.Подразделение КАК Подразделение,
		|	вт_ДокТЧ.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	вт_ДокТЧ.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	вт_ДокТЧ.ПериодДействияНачало КАК ПериодДействияНачало,
		|	вт_ДокТЧ.ПериодДействияКонец КАК ПериодДействияКонец,
		|	вт_ДокТЧ.Параметр КАК Параметр
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|ГДЕ
		|	(вт_ДокТЧ.ВидРасчета = &ВидРасчетаПремия
		|			ИЛИ вт_ДокТЧ.ВидРасчета = &ВидРасчетаФикСумма)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ВидРасчетаПремия", ПланыВидовРасчета.Начисления.Премия);
	Запрос.УстановитьПараметр("ВидРасчетаФикСумма", ПланыВидовРасчета.Начисления.ФиксированнаяСумма);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.НачисленияСотрдникам.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.ПериодРегистрации	= НачалоМесяца(Дата);
	КонецЦикла;
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления(Ссылка);
	
	Движения.НачисленияСотрдникам.Прочитать();
	
	// регистр РасчетыССотрудниками Приход
	Движения.РасчетыССотрудниками.Очистить();
	Движения.РасчетыССотрудниками.Записывать = Истина;
	Для Каждого стр Из Движения.НачисленияСотрдникам Цикл
		Движение = Движения.РасчетыССотрудниками.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Сотрудник = стр.Сотрудник;
		Движение.Подразделение = стр.Подразделение;
		Движение.ВидРасчета = стр.ВидРасчета;
		Движение.Сумма	= стр.Результат;
	КонецЦикла;
	

КонецПроцедуры
