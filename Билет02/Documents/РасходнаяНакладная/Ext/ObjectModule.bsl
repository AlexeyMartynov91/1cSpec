
Процедура ОбработкаПроведения(Отказ, Режим)
	Если ОУ Тогда
		ОбработкаПроведенияОУ(Отказ, Режим);		
	КонецЕсли; 
	Если БУ Тогда
		ОбработкаПроведенияБУ(Отказ, Режим);	
	КонецЕсли; 
КонецПроцедуры

Процедура ОбработкаПроведенияОУ(Отказ, Режим)
	// регистр ОстаткиТовара Расход
	Движения.ОстаткиТовара.Очистить();    
	Движения.ОстаткиТовара.БлокироватьДляИзменения	= Истина;
	Движения.ОстаткиТовара.Записывать = Истина;
	
	// регистр Продажи 
	Движения.Продажи.Очистить();
	Движения.Продажи.БлокироватьДляИзменения	= Истина;
	Движения.Продажи.Записывать = Истина;
	
	//------------ Блокировка ----------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрНакопления.Продажи");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.ИсточникДанных	= ТоварыУслуги;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрНакопления.ОстаткиТовара");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.ИсточникДанных	= ТоварыУслуги;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//----------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТоварыУслуги.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяТоварыУслуги.Партия КАК Партия,
		|	СУММА(РасходнаяНакладнаяТоварыУслуги.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяТоварыУслуги.Сумма) КАК Сумма,
		|	РасходнаяНакладнаяТоварыУслуги.Номенклатура.Услуга КАК Услуга
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.ТоварыУслуги КАК РасходнаяНакладнаяТоварыУслуги
		|ГДЕ
		|	РасходнаяНакладнаяТоварыУслуги.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТоварыУслуги.Номенклатура,
		|	РасходнаяНакладнаяТоварыУслуги.Партия,
		|	РасходнаяНакладнаяТоварыУслуги.Номенклатура.Услуга
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Партия КАК Партия,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ОстаткиТовараОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ОстаткиТовараОстатки.СуммаОстаток КАК СуммаОстаток,
		|	вт_ДокТЧ.Услуга КАК Услуга
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТовара.Остатки(
		|				&МоментВремени,
		|				(Номенклатура, Партия) В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура,
		|						вт_ДокТЧ.Партия
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ОстаткиТовараОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ОстаткиТовараОстатки.Номенклатура
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток),
		|	МАКСИМУМ(Услуга)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		ОбщаяСебестоимость	= 0;
		Если НЕ Выборка.Услуга Тогда
			ВыборкаПартия = Выборка.Выбрать();
			Пока ВыборкаПартия.Следующий() Цикл
				Движение = Движения.ОстаткиТовара.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаПартия.Номенклатура;
				Движение.Партия = ВыборкаПартия.Партия;
				Движение.Количество = ВыборкаПартия.Количество;
				
				Если ВыборкаПартия.Услуга Тогда
					Себестоимость	= 0;
				ИначеЕсли ВыборкаПартия.Количество = ВыборкаПартия.КоличествоОстаток Тогда
					Себестоимость	= ВыборкаПартия.СуммаОстаток;
				Иначе
					Себестоимость	= ВыборкаПартия.Количество / ВыборкаПартия.КоличествоОстаток * ВыборкаПартия.СуммаОстаток
				КонецЕсли; 
				
				Движение.Сумма = Себестоимость;
				ОбщаяСебестоимость	= ОбщаяСебестоимость + Себестоимость;
			КонецЦикла;
		КонецЕсли; 
		
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;
		Движение.Себестоимость = ОбщаяСебестоимость;
		Движение.Выручка = Выборка.Сумма;
	КонецЦикла;

	Движения.Записать();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ОстаткиТовараОстатки.Партия.Представление КАК ПартияПредставление,
		|	-ОстаткиТовараОстатки.КоличествоОстаток КАК Превышение
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТовара.Остатки(
		|				&ГраницаДок,
		|				(Номенклатура, Партия) В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура,
		|						вт_ДокТЧ.Партия
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ОстаткиТовараОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ОстаткиТовараОстатки.Номенклатура
		|ГДЕ
		|	ОстаткиТовараОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("ГраницаДок", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Отказ	= Истина;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 по партии %2 в количестве %3", 
								Выборка.НоменклатураПредставление, Выборка.ПартияПредставление, Выборка.Превышение); 
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;

	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроведенияБУ(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.БлокироватьДляИзменения	= Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТоварыУслуги.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяТоварыУслуги.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяТоварыУслуги.Сумма) КАК Сумма,
		|	&Склад КАК Склад
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.ТоварыУслуги КАК РасходнаяНакладнаяТоварыУслуги
		|ГДЕ
		|	РасходнаяНакладнаяТоварыУслуги.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТоварыУслуги.Номенклатура,
		|	РасходнаяНакладнаяТоварыУслуги.Ссылка.Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Склад КАК Склад
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//----------- Блокировка -----------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.ПланСчетовБУ.Товары); 
	ЭлБлокировки.ИсточникДанных	= РезультатЗапроса;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Товары, "Номенклатура"); 
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Склады, "Склад"); 
	Блокировка.Заблокировать();
	//----------------------------------------
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЖурналПроводокОстатки.Субконто1 КАК Товар,
		|	ЖурналПроводокОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ вт_Стоимость
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|			&МоментВремени,
		|			Счет = &СчетТовары,
		|			&МассивСубконто,
		|			Субконто1 В
		|				(ВЫБРАТЬ
		|					вт_ДокТЧ.Номенклатура
		|				ИЗ
		|					вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЖурналПроводокОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЖурналПроводокОстатки.Субконто1 КАК Товар,
		|	ЖурналПроводокОстатки.Субконто2 КАК Склад,
		|	ЖурналПроводокОстатки.КоличествоОстаток КАК КоличествоОстатокПоСкладу,
		|	вт_Стоимость.КоличествоОстаток КАК КоличествоОстаток,
		|	вт_Стоимость.СуммаОстаток КАК СуммаОстаток,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ЖурналПроводокОстатки.Субконто1.Представление КАК ТоварПредставление
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&МассивСубконто,
		|				(Субконто1, Субконто2) В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура,
		|						вт_ДокТЧ.Склад
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЖурналПроводокОстатки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_Стоимость КАК вт_Стоимость
		|			ПО ЖурналПроводокОстатки.Субконто1 = вт_Стоимость.Товар
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстатки.Субконто1
		|			И вт_ДокТЧ.Склад = ЖурналПроводокОстатки.Субконто2
		|ИТОГИ
		|	МАКСИМУМ(КоличествоОстаток),
		|	МАКСИМУМ(СуммаОстаток),
		|	СУММА(Количество),
		|	СУММА(Сумма)
		|ПО
		|	Товар";
	
	МассивСубконто		= Новый Массив(2);
	МассивСубконто[0]	= ПланыВидовХарактеристик.ВидыСубконто.Товары;
	МассивСубконто[1]	= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	
	Запрос.УстановитьПараметр("МассивСубконто", МассивСубконто);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.ПланСчетовБУ.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаТовар	= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаТовар.Следующий() Цикл
		Стоимость	= ВыборкаТовар.СуммаОстаток; 
		ОбщОстаток	= ВыборкаТовар.КоличествоОстаток; 
		
		Выборка	= ВыборкаТовар.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.КоличествоОстаток < Выборка.Количество Тогда
				Отказ	= Истина;
				Сообщение		= Новый СообщениеПользователю;
				Сообщение.Текст	= СтрШаблон("Не хватает товара %1 в количестве %2 на складе %3",
									Выборка.ТоварПредставление, СокрЛП(Выборка.Количество - Выборка.КоличествоОстаток), Выборка.Склад);
				Сообщение.Сообщить();
			КонецЕсли;
			
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.ПланСчетовБУ.ПрибылиИУбытки;
			Движение.СчетКт = ПланыСчетов.ПланСчетовБУ.Товары;
			Движение.Период = Дата;
			Движение.КоличествоКт = Выборка.Количество;
			
			Если ОбщОстаток = Выборка.Количество Тогда
				Себестоимость	= Стоимость;
			Иначе	
				Себестоимость	= Выборка.Количество/ВыборкаТовар.КоличествоОстаток * ВыборкаТовар.СуммаОстаток;
			КонецЕсли;
			
			Движение.Сумма = Себестоимость;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Товары] = Выборка.Товар;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Проекты] = Проект;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Товары] = Выборка.Товар;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
			
			ОбщОстаток	= ОбщОстаток- Выборка.Количество;
			Стоимость	= Стоимость	- Себестоимость;
		КонецЦикла;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.ПланСчетовБУ.Покупатели;
		Движение.СчетКт = ПланыСчетов.ПланСчетовБУ.ПрибылиИУбытки;
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаТовар.Сумма;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Товары] = ВыборкаТовар.Товар;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Проекты] = Проект;
	КонецЦикла;

КонецПроцедуры
