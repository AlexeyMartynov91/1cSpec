
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.БлокироватьДляИзменения	= Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗатратыЗатратыПоПроектам.Проект КАК Проект,
		|	СУММА(ЗатратыЗатратыПоПроектам.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.Затраты.ЗатратыПоПроектам КАК ЗатратыЗатратыПоПроектам
		|ГДЕ
		|	ЗатратыЗатратыПоПроектам.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗатратыЗатратыПоПроектам.Проект
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Проект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Проект КАК Проект,
		|	вт_ДокТЧ.Сумма КАК Сумма
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//-------- Блокировка ПрибылиИУбытки по Товарам --------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.ПланСчетовБУ.ПрибылиИУбытки);
	ЭлБлокировки.ИсточникДанных	= РезультатЗапроса;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Проекты, "Проект");
	Блокировка.Заблокировать();
	//-----------------------------------------
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_ДокТЧ.Проект КАК Проект,
		|	вт_ДокТЧ.Сумма КАК СуммаЗатрат,
		|	ЖурналПроводокОбороты.Субконто1 КАК Товар,
		|	ЖурналПроводокОбороты.СуммаОборот КАК СуммаОборот
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Обороты(
		|				&ДатаН,
		|				&ДатаК,
		|				,
		|				Счет = &Счет,
		|				&ВидыСубконто,
		|				Субконто2 В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Проект
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ),
		|				КорСчет = &КорСчет,
		|				) КАК ЖурналПроводокОбороты
		|		ПО вт_ДокТЧ.Проект = ЖурналПроводокОбороты.Субконто2
		|ИТОГИ
		|	МАКСИМУМ(СуммаЗатрат),
		|	СУММА(СуммаОборот)
		|ПО
		|	Проект";
	
	ВидыСубконто		= Новый Массив(2);
	ВидыСубконто[0]		= ПланыВидовХарактеристик.ВидыСубконто.Товары;
	ВидыСубконто[1]		= ПланыВидовХарактеристик.ВидыСубконто.Проекты;
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("ДатаН", НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаК", КонецДня(Дата));
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ПланСчетовБУ.ПрибылиИУбытки);
	Запрос.УстановитьПараметр("КорСчет", ПланыСчетов.ПланСчетовБУ.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПроект	= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПроект.Следующий() Цикл
	   	ЗатратыНаПроект	= ВыборкаПроект.СуммаЗатрат;
		
		Выборка		= ВыборкаПроект.Выбрать();
		Счетчик		= 0;
		Пока Выборка.Следующий() Цикл
			Счетчик		= Счетчик + 1;
			Если Счетчик = Выборка.Количество() Тогда
				СписатьЗатраты	= ЗатратыНаПроект;		
			Иначе	
				СписатьЗатраты	= Выборка.СуммаОборот/ВыборкаПроект.СуммаОборот * ВыборкаПроект.СуммаЗатрат;		
			КонецЕсли; 
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.ПланСчетовБУ.ПрибылиИУбытки;
			Движение.СчетКт = ПланыСчетов.ПланСчетовБУ.ОбщехозяйственныеЗатраты;
			Движение.Период = Дата;
			Движение.Сумма = СписатьЗатраты;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Товары] = Выборка.Товар;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Проекты] = Выборка.Проект;
			
			ЗатратыНаПроект	= ЗатратыНаПроект - СписатьЗатраты;
		КонецЦикла;
	КонецЦикла;
	

КонецПроцедуры
