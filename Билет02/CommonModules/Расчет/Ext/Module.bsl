Процедура РассчитатьНачисления(Регистратор)	Экспорт
	НаборЗаписей		= РегистрыРасчета.НачисленияСотрдникам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	// Оклад
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрдникамДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	НачисленияСотрдникамДанныеГрафика.ЧасовФактическийПериодДействия КАК Факт,
		|	НачисленияСотрдникамДанныеГрафика.ЧасовБазовыйПериод КАК Норма
		|ИЗ
		|	РегистрРасчета.НачисленияСотрдникам.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &Оклад) КАК НачисленияСотрдникамДанныеГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.Начисления.Оклад);	// !!
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();

	СтруктураПоиска		= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Выборка.НайтиСледующий(СтруктураПоиска);
		Если Запись.ВидРасчета = ПланыВидовРасчета.Начисления.Оклад Тогда
			Запись.Результат	= ?(Выборка.Норма=0, 0, Выборка.Факт / Выборка.Норма * Запись.Параметр);
		КонецЕсли;
	КонецЦикла; 
	
	НаборЗаписей.Записать();	
	
	// Премия
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрдникамБазаНачисленияСотрдникам.НомерСтроки КАК НомерСтроки,
		|	НачисленияСотрдникамБазаНачисленияСотрдникам.РезультатБаза КАК База
		|ИЗ
		|	РегистрРасчета.НачисленияСотрдникам.БазаНачисленияСотрдникам(
		|			&ИзмеренияРегистра,
		|			&ИзмеренияРегистра,
		|			,
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &Премия) КАК НачисленияСотрдникамБазаНачисленияСотрдникам
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	ИзмеренияРегистра		= Новый Массив(1);
	ИзмеренияРегистра[0]	= "Сотрудник";
	
	Запрос.УстановитьПараметр("ИзмеренияРегистра", ИзмеренияРегистра);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Премия", ПланыВидовРасчета.Начисления.Премия);	// !!
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();

	СтруктураПоиска		= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Выборка.НайтиСледующий(СтруктураПоиска);
		Если Запись.ВидРасчета = ПланыВидовРасчета.Начисления.Премия Тогда
			Запись.Результат	= Выборка.База * Запись.Параметр / 100;
		КонецЕсли;
	КонецЦикла; 
	
	// Фиксированная сумма
	Для каждого Запись Из НаборЗаписей Цикл
		Если Запись.ВидРасчета	= ПланыВидовРасчета.Начисления.ФиксированнаяСумма Тогда
			Запись.Результат	= Запись.Параметр;
		КонецЕсли;
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);	// !!	

КонецПроцедуры
