Процедура Рассчитать(Регистратор, ПериодРегистрации) Экспорт
	НаборЗаписей		= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия КАК ЧасовПлан,
		|	ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия КАК ЧасовФакт,
		|	ОсновныеНачисленияДанныеГрафика.ДнейПериодДействия КАК ДнейПлан,
		|	ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия КАК ДнейФакт,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(ОсновныеНачисленияБазаДопНачисления.РезультатБаза, 0) КАК База,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РабочихДнейБаза, 0) КАК РабочихДней
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Регистратор
		|					И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаДопНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Регистратор
		|					И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияБазаДопНачисления
		|		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаДопНачисления.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Измерения		= Новый Массив(1);
	Измерения[0]	= "Сотрудник";
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки", );
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Если Запись.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
				Если Запись.Сторно Тогда
					Запись.Результат	= -Запись.Параметр * Выборка.ЧасовФакт / Выборка.ЧасовПлан;
					Запись.РабочихДней	= 0;
				Иначе	
					Запись.Результат	= Запись.Параметр * Выборка.ЧасовФакт / Выборка.ЧасовПлан;
					Запись.РабочихДней	= Выборка.ДнейПлан;
				КонецЕсли;
			ИначеЕсли Запись.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда
				Запись.Результат	= ?(Выборка.РабочихДней = 0, 0, Выборка.База * Выборка.ДнейФакт / Выборка.РабочихДней);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	

КонецПроцедуры
