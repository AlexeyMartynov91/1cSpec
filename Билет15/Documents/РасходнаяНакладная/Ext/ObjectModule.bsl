
Процедура ОбработкаПроведения(Отказ, Режим)
	МетодСписания	= РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(МоментВремени()).МетодСписанияПартий;
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ПустаяСсылка() Тогда
		Отказ	= Истина;	
		
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= "Не установлен метод списания партий в учетной политике.";
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	// ОУ
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	// регистр Продажи 
	Движения.Продажи.Записывать = Истина;
	Движения.Продажи.Записать();
	
	//----------- Блокировка -----------
	Блокировка					= Новый БлокировкаДанных;
	ЭлБлокировки				= Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.ИсточникДанных	= СписокНоменклатуры;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//----------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ЭтоУслуга КАК ЭтоУслуга
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ДокТЧ.ЭтоУслуга КАК ЭтоУслуга,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени КАК ПартияМоментВремени
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ОстаткиНоменклатурыОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартияМоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(ЭтоУслуга),
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст	= СтрЗаменить(Запрос.Текст, "ПартияМоментВремени УБЫВ", "ПартияМоментВремени ВОЗР");
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Стоимость	= 0;
		Если НЕ ВыборкаНоменклатура.ЭтоУслуга Тогда
			Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
				Отказ	= Истина;	
				
				Сообщение		= Новый СообщениеПользователю;
				Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2 по ОУ", 
									ВыборкаНоменклатура.НоменклатураПредставление, СокрЛП(ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток));
				Сообщение.Сообщить();
			КонецЕсли;
			
			Если Отказ Тогда
			    Продолжить;
			КонецЕсли;
			
		    ВсегоСписать	= ВыборкаНоменклатура.Количество;
			Выборка			= ВыборкаНоменклатура.Выбрать();
			Пока Выборка.Следующий() И ВсегоСписать > 0 Цикл
				Списать		= Мин(ВсегоСписать, Выборка.КоличествоОстаток);
				
				Если Списать = Выборка.КоличествоОстаток Тогда
					Себестоимость	= Выборка.СуммаОстаток;
				Иначе	
					Себестоимость	= Выборка.СуммаОстаток * Списать / Выборка.КоличествоОстаток;
				КонецЕсли;
				
				Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Номенклатура = Выборка.Номенклатура;
				Движение.Партия = Выборка.Партия;
				Движение.Количество = Списать;
				Движение.Сумма = Себестоимость;
				
				ВсегоСписать	= ВсегоСписать - Списать;	
				Стоимость		= Стоимость + Себестоимость;
			КонецЦикла;
		КонецЕсли;
		
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
		Движение.Количество = ВыборкаНоменклатура.Количество;
		Движение.Себестоимость = Стоимость;
		Движение.Продажа = ВыборкаНоменклатура.Сумма;
	КонецЦикла;
	
	// БУ
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.Записать();
	
	//----------- Блокировка -----------
	Блокировка					= Новый БлокировкаДанных;
	ЭлБлокировки				= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
	ЭлБлокировки.ИсточникДанных	= СписокНоменклатуры;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	//----------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|	И НЕ РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ЭтоУслуга
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ЖурналПроводокОстатки.Субконто3 КАК Партия,
		|	ЕСТЬNULL(ЖурналПроводокОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ЖурналПроводокОстатки.Субконто3.МоментВремени КАК ПартияМоментВремени
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&ВидыСубконто,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Субконто2 = &Склад) КАК ЖурналПроводокОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстатки.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартияМоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток)
		|ПО
		|	Номенклатура";
	
	ВидыСубконто		= Новый Массив(3);
	ВидыСубконто[0]		= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыСубконто[1]		= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	ВидыСубконто[2]		= ПланыВидовХарактеристик.ВидыСубконто.Партии;
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст	= СтрЗаменить(Запрос.Текст, "ПартияМоментВремени УБЫВ", "ПартияМоментВремени ВОЗР");
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Отказ	= Истина;	
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2 по БУ", 
								ВыборкаНоменклатура.НоменклатураПредставление, СокрЛП(ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток));
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ВсегоСписать	= ВыборкаНоменклатура.Количество;
		Выборка			= ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ВсегоСписать > 0 Цикл
			Списать		= Мин(ВсегоСписать, Выборка.КоличествоОстаток);
			
			Если Списать > Выборка.КоличествоОстаток Тогда
				Себестоимость	= Выборка.СуммаОстаток;
			Иначе
				Себестоимость	= Выборка.СуммаОстаток * Списать / Выборка.КоличествоОстаток;
			КонецЕсли;
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
			Движение.Период = Дата;
			Движение.КоличествоКт = Списать;
			Движение.Сумма = Себестоимость;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Выборка.Партия;
			
			ВсегоСписать	= ВсегоСписать - Списать;	
		КонецЦикла;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
		Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаНоменклатура.Сумма;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;
		
	КонецЦикла;
	
КонецПроцедуры
