
Процедура ОбработкаПроведения(Отказ, Режим)
	НачМес	= НачалоМесяца(Дата);
	
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Движения.ОсновныеНачисления.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыОсновныеНачисления.График КАК График,
		|	НачислениеЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ПериодДействияНачало,
		|	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания КАК ПериодДействияКонец
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
		|ГДЕ
		|	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Сотрудник КАК Сотрудник,
		|	вт_ДокТЧ.Подразделение КАК Подразделение,
		|	вт_ДокТЧ.График КАК График,
		|	вт_ДокТЧ.ВидРасчета КАК ВидРасчета,
		|	вт_ДокТЧ.ПериодДействияНачало КАК ПериодДействияНачало,
		|	вт_ДокТЧ.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Параметр
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&НачМес,
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Сотрудник
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО вт_ДокТЧ.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("НачМес", НачМес);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.ПериодРегистрации = НачМес;
		
		Если Выборка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда
			Движение.БазовыйПериодНачало = ДобавитьМесяц(НачМес, -1);
			Движение.БазовыйПериодКонец = НачМес - 1;
			Движение.График = Справочники.Графики.Пятидневка;
		КонецЕсли;
	КонецЦикла;
	
	СторноЗаписи	= Движения.ОсновныеНачисления.ПолучитьДополнение();
	Для каждого Сторно Из СторноЗаписи Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Сторно);
		Движение.Сторно	= Истина;
		Движение.ПериодРегистрации		= Сторно.ПериодРегистрацииСторно;
		Движение.ПериодДействияНачало	= Сторно.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец	= Сторно.ПериодДействияКонецСторно;
	КонецЦикла;

	// регистр ДопНачисления
	Движения.ДопНачисления.Записывать = Истина;
	Движения.ДопНачисления.Записать();
	Для Каждого стр Из ДопНачисления Цикл
		Движение = Движения.ДопНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, стр);
		Движение.ПериодРегистрации = НачМес;
		Движение.Результат = стр.Размер;
	КонецЦикла;

	Движения.Записать();
	
	Расчет.Рассчитать(Ссылка, НачМес);
КонецПроцедуры
