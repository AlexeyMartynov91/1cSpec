Процедура РассчитатьНачисления(Регистратор) Экспорт 
	// оклад по часам
	НаборЗаписей	= РегистрыРасчета.НачисленияСотрудникам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);	
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудникамДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(НачисленияСотрудникамДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ЧасовФакт,
		|	ЕСТЬNULL(НачисленияСотрудникамДанныеГрафика.ДнейПериодДействия, 0) КАК ДнейПлан
		|ИЗ
		|	РегистрРасчета.НачисленияСотрудникам.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &ВидРасчетаОклад) КАК НачисленияСотрудникамДанныеГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ОсновныеНачисления.ОкладПоЧасам);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Если Запись.Сторно Тогда
				Запись.Результат	= - Запись.ЧасоваяСтавка * Выборка.ЧасовФакт;
				Запись.РабочихДней	= 0; // сторно не увеличивает кол-во плановых дней
			Иначе	
				Запись.Результат	= Запись.ЧасоваяСтавка * Выборка.ЧасовФакт;
				Запись.РабочихДней	= Выборка.ДнейПлан;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);
	
	// больничный
	НаборЗаписей	= РегистрыРасчета.НачисленияСотрудникам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);	
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудникамБазаНачисленияСотрудникам.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(НачисленияСотрудникамБазаНачисленияСотрудникам.РезультатБаза, 0) КАК База,
		|	ЕСТЬNULL(НачисленияСотрудникамБазаНачисленияСотрудникам.РабочихДнейБаза, 0) КАК РабочихДнейБаза,
		|	ЕСТЬNULL(НачисленияСотрудникамДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ДнейБолезни,
		|	НачисленияСотрудникамДанныеГрафика.ДнейПериодДействия КАК ДнейПериодДействия,
		|	НачисленияСотрудникамДанныеГрафика.ДнейБазовыйПериод КАК ДнейБазовыйПериод,
		|	НачисленияСотрудникамДанныеГрафика.ДнейПериодРегистрации КАК ДнейПериодРегистрации
		|ИЗ
		|	РегистрРасчета.НачисленияСотрудникам.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &ВидРасчетаБольничный) КАК НачисленияСотрудникамДанныеГрафика
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрРасчета.НачисленияСотрудникам.БазаНачисленияСотрудникам(
		|				&ИзмеренияРегистра,
		|				&ИзмеренияРегистра,
		|				,
		|				Регистратор = &Регистратор
		|					И ПериодРегистрации = &ПериодРегистрации
		|					И ВидРасчета = &ВидРасчетаБольничный) КАК НачисленияСотрудникамБазаНачисленияСотрудникам
		|		ПО НачисленияСотрудникамДанныеГрафика.НомерСтроки = НачисленияСотрудникамБазаНачисленияСотрудникам.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	ИзмеренияРегистра		= Новый Массив(2);
	ИзмеренияРегистра[0]	= "Сотрудник";
	ИзмеренияРегистра[1]	= "Подразделение";
	
	Запрос.УстановитьПараметр("ИзмеренияРегистра", ИзмеренияРегистра);
	Запрос.УстановитьПараметр("ВидРасчетаБольничный", ПланыВидовРасчета.ОсновныеНачисления.Больничный);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= ?(Выборка.РабочихДнейБаза=0, 0, Выборка.База * Выборка.ДнейБолезни / Выборка.РабочихДнейБаза);
			Запись.ДнейБолезни	= Выборка.ДнейБолезни;
		КонецЕсли;
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);
	
КонецПроцедуры
