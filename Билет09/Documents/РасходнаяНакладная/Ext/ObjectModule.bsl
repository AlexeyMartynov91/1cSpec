
Процедура ОбработкаПроведения(Отказ, Режим)
	//--------------- ОУ -----------------
	
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина; 
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	//------------- Блокировка ---------------------
	Блокировка			= Новый БлокировкаДанных;	
	ЭлБлокировки		= Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.ИсточникДанных	= СписокНоменклатуры;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("СвойствоНоменклатуры", "Свойство");
	Блокировка.Заблокировать();
	//----------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Свойство КАК Свойство,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Свойство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Свойство КАК Свойство,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ДокТЧ.Свойство.Представление КАК СвойствоПредставление
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				(Номенклатура, СвойствоНоменклатуры) В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура,
		|						вт_ДокТЧ.Свойство
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ОстаткиНоменклатурыОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|			И вт_ДокТЧ.Свойство = ОстаткиНоменклатурыОстатки.СвойствоНоменклатуры";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат	= Запрос.Выполнить();
	Выборка		= Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 по свойству %2 в количестве %3", 
								Выборка.НоменклатураПредставление, Выборка.СвойствоПредставление, СокрЛП(Выборка.Количество - Выборка.КоличествоОстаток));			
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли; 
		
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость	= Выборка.СуммаОстаток;
		Иначе	
			Себестоимость	= Выборка.СуммаОстаток * Выборка.Количество / Выборка.КоличествоОстаток;
		КонецЕсли; 
		
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.СвойствоНоменклатуры = Выборка.Свойство;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Себестоимость;
		
	КонецЦикла;
	
	//--------------- БУ -----------------
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.БлокироватьДляИзменения	= Истина;
	Движения.ЖурналПроводок.Записывать = Истина;
	
	//---------------- Блокировка -------------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");	
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары); 
	ЭлБлокировки.ИсточникДанных	= СписокНоменклатуры;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
	Блокировка.Заблокировать();
	//-----------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ЕСТЬNULL(ЖурналПроводокОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЖурналПроводокОстатки.СуммаВРубОстаток КАК СуммаВРубОстаток,
		|	ЖурналПроводокОстатки.СуммаВВалОстаток КАК СуммаВВалОстаток,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&ВидыСубконто,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Субконто2 = &Склад) КАК ЖурналПроводокОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстатки.Субконто1";
	
	ВидыСубконто		= Новый Массив(2);
	ВидыСубконто[0]		= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыСубконто[1]		= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ОбщаяСуммаСписания	= 0;
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
				Отказ	= Истина;
				
				Сообщение		= Новый СообщениеПользователю;
				Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2", 
											Выборка.НоменклатураПредставление, СокрЛП(Выборка.Количество - Выборка.КоличествоОстаток));			
				Сообщение.Сообщить();
			КонецЕсли;			
			Если Отказ Тогда
			    Продолжить;
			КонецЕсли;
			
			Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
				Себестоимость	= Выборка.СуммаВРубОстаток;
			Иначе
				Себестоимость	= Выборка.СуммаВРубОстаток *  Выборка.Количество / Выборка.КоличествоОстаток;
			КонецЕсли;
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
			Движение.Период = Дата;
			Движение.КоличествоКт = Выборка.Количество;
			Движение.СуммаВРуб = Себестоимость;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
			
			ОбщаяСуммаСписания	= ОбщаяСуммаСписания + Себестоимость;
		КонецЦикла;
	КонецЕсли;
	
	Движение = Движения.ЖурналПроводок.Добавить();
	Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
	Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
	Движение.Период = Дата;
	Движение.СуммаВРуб = СписокНоменклатуры.Итог("Сумма");

	Если Агент <> Справочники.Агенты.ПустаяСсылка() Тогда
		Курс	= РегистрыСведений.КурсыВалют.ПолучитьПоследнее(МоментВремени(), Новый Структура("Валюта", Агент.Валюта)).Курс;
		Если Курс = 0 Тогда
			Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= "Не задан курс для валюты агента";			
			Сообщение.Сообщить();
		Иначе	
			Вознаграждение	= 0.2 * (СписокНоменклатуры.Итог("Сумма") - ОбщаяСуммаСписания);
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт = ПланыСчетов.Управленческий.Агенты;
			Движение.Период = Дата;
			Движение.СуммаВРуб		=  Вознаграждение;
			Движение.СуммаВВалКт	=  Вознаграждение / Курс;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Агенты] = Агент;
		КонецЕсли; 		
	КонецЕсли; 
	
КонецПроцедуры
