
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр НачисленияСотрудникам
	Движения.НачисленияСотрудникам.Очистить();
	Движения.НачисленияСотрудникам.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыОсновныеНачисления.График КАК График,
		|	НачислениеЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	НачислениеЗарплатыОсновныеНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	НачислениеЗарплатыОсновныеНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.ЧасоваяСтавка, 0) КАК ЧасоваяСтавка,
		|	НачислениеЗарплатыОсновныеНачисления.Сотрудник.Представление КАК СотрудникПредставление,
		|	РАЗНОСТЬДАТ(НачислениеЗарплатыОсновныеНачисления.ПериодДействияНачало, НачислениеЗарплатыОсновныеНачисления.ПериодДействияКонец, МЕСЯЦ) КАК КоличествоМесяцев
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(&ДатаНачалаРасчетногоПериода, ) КАК СведенияОСотрудникахСрезПоследних
		|		ПО НачислениеЗарплатыОсновныеНачисления.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
		|			И НачислениеЗарплатыОсновныеНачисления.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение
		|ГДЕ
		|	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ДатаНачалаРасчетногоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЧасоваяСтавка = 0 Тогда
			Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("У сотрудника %1 не установлена часовая ставка", Выборка.СотрудникПредставление);			
		    Сообщение.Сообщить();
		КонецЕсли;
		Если Отказ Тогда
		    Продолжить;
		КонецЕсли;
		
		Если Выборка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда
			НачалоПериода						= Выборка.ПериодДействияНачало;  
			Для мес=0 По Выборка.КоличествоМесяцев Цикл
				Движение = Движения.НачисленияСотрудникам.Добавить();
				ЗаполнитьЗначенияСвойств(Движение, Выборка);
				
				Движение.ПериодРегистрации 		= НачалоМесяца(Дата);
				Движение.ПериодДействияНачало	= НачалоПериода;
				Движение.ПериодДействияКонец	= Мин(Выборка.ПериодДействияКонец, КонецМесяца(НачалоПериода));
				Движение.БазовыйПериодНачало 	= НачалоМесяца(ДобавитьМесяц(НачалоПериода, -1));
				Движение.БазовыйПериодКонец 	= КонецМесяца(ДобавитьМесяц(НачалоПериода, -1));
				
				НачалоПериода					= КонецМесяца(НачалоПериода)+1;
			КонецЦикла;
		Иначе
			Движение = Движения.НачисленияСотрудникам.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.ПериодРегистрации			= НачалоМесяца(Дата);
			Движение.БазовыйПериодНачало		= Выборка.ПериодДействияНачало;
			Движение.БазовыйПериодКонец			= Выборка.ПериодДействияКонец;
		КонецЕсли;
	КонецЦикла;
	
	// Данные могут вводиться задним числом, значит необходимо сформировать сторно записи
	СторноЗаписи = Движения.НачисленияСотрудникам.ПолучитьДополнение();
	Для Каждого Сторно Из СторноЗаписи Цикл
		Движение = Движения.НачисленияСотрудникам.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Сторно);
		Движение.Сторно = Истина;
		Движение.ПериодРегистрации = Сторно.ПериодРегистрацииСторно;
		Движение.ПериодДействияНачало = Сторно.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Сторно.ПериодДействияКонецСторно;
	КонецЦикла;
	//-----------------------------------------------------------------------------------
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления(Ссылка);
	
КонецПроцедуры
