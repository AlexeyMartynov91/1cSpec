Процедура РассчитатьНачисления(Регистратор) Экспорт

	// рассчитать основные начисления
	НаборЗаписей	= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК ЧасовПлан,
		|	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
		|	ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало КАК ПериодДействияНачало,
		|	ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец КАК ПериодДействияКонец
		|ПОМЕСТИТЬ вт_ДанныеГрафика
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодДействия = &НачалоМес
		|				И ВидРасчета = &ВидРасчетаОклад) КАК ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	вт_ДанныеГрафика.ЧасовПлан КАК ЧасовПлан,
		|	СУММА(ЕСТЬNULL(ТабельОбороты.ЧасовОтработаноОборот, 0)) КАК ЧасовФакт,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад
		|ИЗ
		|	вт_ДанныеГрафика КАК вт_ДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Табель.Обороты(
		|				&НачалоМес,
		|				&КонецМес,
		|				День,
		|				(Сотрудник, Подразделение) В
		|					(ВЫБРАТЬ
		|						вт_ДанныеГрафика.Сотрудник,
		|						вт_ДанныеГрафика.Подразделение
		|					ИЗ
		|						вт_ДанныеГрафика КАК вт_ДанныеГрафика)) КАК ТабельОбороты
		|		ПО вт_ДанныеГрафика.Сотрудник = ТабельОбороты.Сотрудник
		|			И вт_ДанныеГрафика.Подразделение = ТабельОбороты.Подразделение
		|			И вт_ДанныеГрафика.ПериодДействияНачало <= ТабельОбороты.Период
		|			И вт_ДанныеГрафика.ПериодДействияКонец >= ТабельОбороты.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&НачалоМес,
		|				(Сотрудник, Подразделение) В
		|					(ВЫБРАТЬ
		|						вт_ДанныеГрафика.Сотрудник,
		|						вт_ДанныеГрафика.Подразделение
		|					ИЗ
		|						вт_ДанныеГрафика КАК вт_ДанныеГрафика)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО вт_ДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
		|			И вт_ДанныеГрафика.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_ДанныеГрафика.НомерСтроки,
		|	вт_ДанныеГрафика.ЧасовПлан,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("КонецМес", КонецДня(КонецМесяца(Регистратор.Дата)));
	Запрос.УстановитьПараметр("НачалоМес", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса	= Запрос.Выполнить();
	Выборка				= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска		= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда	// не делаем Выборка.Сбросить() т.к. упорядочили по НомерСтроки
			Запись.ОтработанноЧасов	= Выборка.ЧасовФакт;
			Запись.Результат		= ?(Выборка.ЧасовПлан=0,0,Выборка.Оклад * Выборка.ЧасовФакт / Выборка.ЧасовПлан);
		КонецЕсли; 
	
	КонецЦикла;
		
	НаборЗаписей.Записать(, Истина);
	
	// рассчитать доп. начисления
	НаборЗаписей	= РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ДопНачисленияБазаОсновныеНачисления.Сотрудник, ДопНачисленияБазаДопНачисления.Сотрудник) КАК Сотрудник,
		|	ЕСТЬNULL(ДопНачисленияБазаОсновныеНачисления.Подразделение, ДопНачисленияБазаДопНачисления.Подразделение) КАК Подразделение,
		|	ЕСТЬNULL(ДопНачисленияБазаОсновныеНачисления.НомерСтроки, ДопНачисленияБазаДопНачисления.НомерСтроки) КАК НомерСтроки,
		|	ЕСТЬNULL(ДопНачисленияБазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(ДопНачисленияБазаДопНачисления.РезультатБаза, 0) КАК База,
		|	ЕСТЬNULL(ДопНачисленияБазаОсновныеНачисления.ОтработанноЧасовБаза, 0) КАК ОтработанноЧасовБаза
		|ПОМЕСТИТЬ ВТ_ДанныеБазы
		|ИЗ
		|	РегистрРасчета.ДопНачисления.БазаОсновныеНачисления(
		|			&Измерения,
		|			&Измерения,
		|			,
		|			Регистратор = &Регистратор
		|				И ВидРасчета = &ВидРасчетаКомандировка) КАК ДопНачисленияБазаОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДопНачисления.БазаДопНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Регистратор
		|					И ВидРасчета = &ВидРасчетаКомандировка) КАК ДопНачисленияБазаДопНачисления
		|		ПО ДопНачисленияБазаОсновныеНачисления.НомерСтроки = ДопНачисленияБазаДопНачисления.НомерСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеБазы.НомерСтроки КАК НомерСтроки,
		|	ВТ_ДанныеБазы.База КАК База,
		|	ВТ_ДанныеБазы.ОтработанноЧасовБаза КАК ЧасовБаза,
		|	СУММА(ЕСТЬNULL(ТабельОбороты.ЧасовКомандировкаОборот, 0)) КАК ЧасовФакт
		|ИЗ
		|	ВТ_ДанныеБазы КАК ВТ_ДанныеБазы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Табель.Обороты(
		|				&НачалоМес,
		|				&КонецМес,
		|				,
		|				(Сотрудник, Подразделение) В
		|					(ВЫБРАТЬ
		|						вт_ДанныеБазы.Сотрудник,
		|						вт_ДанныеБазы.Подразделение
		|					ИЗ
		|						вт_ДанныеБазы КАК вт_ДанныеБазы)) КАК ТабельОбороты
		|		ПО ВТ_ДанныеБазы.Сотрудник = ТабельОбороты.Сотрудник
		|			И ВТ_ДанныеБазы.Подразделение = ТабельОбороты.Подразделение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДанныеБазы.НомерСтроки,
		|	ВТ_ДанныеБазы.База,
		|	ВТ_ДанныеБазы.ОтработанноЧасовБаза";
	
	Измерения	= Новый Массив(2);
	Измерения[0]= "Сотрудник";
	Измерения[1]= "Подразделение";
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ВидРасчетаКомандировка", ПланыВидовРасчета.ДополнительныеНачисления.Командировка);
	Запрос.УстановитьПараметр("КонецМес", КонецМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("НачалоМес", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса	= Запрос.Выполнить();
	Выборка				= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска		= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда	// не делаем Выборка.Сбросить() т.к. упорядочили по НомерСтроки
			Запись.Результат		= ?(Выборка.ЧасовБаза=0,0,Выборка.База * Выборка.ЧасовФакт / Выборка.ЧасовБаза);
		КонецЕсли; 
	
	КонецЦикла;
	
	НаборЗаписей.Записать();

КонецПроцедуры
