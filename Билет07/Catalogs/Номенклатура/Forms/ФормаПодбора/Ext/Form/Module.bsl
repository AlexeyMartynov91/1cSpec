
&НаКлиенте
Процедура ОК(Команда)
	
	Адрес	= АдресВХранилище();	
	ОповеститьОВыборе(Адрес);
	
КонецПроцедуры

&НаСервере
Функция АдресВХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(ОтобранныеТовары.Выгрузить(),УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка	= Ложь;
	
	ДобавитьНоменклатуруВОтобранныеТовары(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНоменклатуруВОтобранныеТовары(Номенклатура)

	СтруктураПоиска	= Новый Структура("Номенклатура",Номенклатура);
	НайденныеСтроки	= ОтобранныеТовары.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() <> 0 Тогда
		ИзменяемаяСтрока	= НайденныеСтроки[0];
		ИзменяемаяСтрока.Количество	= ИзменяемаяСтрока.Количество + 1;
	Иначе
		НовСтрока	= ОтобранныеТовары.Добавить();
		НовСтрока.Номенклатура	= Номенклатура;
		НовСтрока.Количество	= 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобранныеТоварыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Для каждого Номенклатура Из ПараметрыПеретаскивания.Значение Цикл
		 ДобавитьНоменклатуруВОтобранныеТовары(Номенклатура);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтобранныеТовары.Загрузить(ПолучитьИзВременногоХранилища(Параметры.Адрес));
	
КонецПроцедуры


