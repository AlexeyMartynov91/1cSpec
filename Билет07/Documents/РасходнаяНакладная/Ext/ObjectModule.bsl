
Процедура ОбработкаПроведения(Отказ, Режим)
	// ОУ
	// определим метод списания партий
	
	МетодСписания	= РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписанияПартий;
	
	Если НЕ ЗначениеЗаполнено(МетодСписания) Тогда
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= "Не задан метод списания партий";	
		Сообщение.Сообщить();
		
	    Отказ		= Истина;
		Возврат;
	КонецЕсли;
	
	// регистр ОстаткиНоменклатуры 
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения	= Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	// регистр Продажи 
	Движения.Продажи.Очистить();
	Движения.Продажи.БлокироватьДляИзменения	= Истина;
	Движения.Продажи.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Услуга КАК Услуга
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Услуга
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//---------------- Блокировка -----------------------------
	Блокировка					= Новый БлокировкаДанных;
	ЭлБлокировки				= Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.ИсточникДанных	= РезультатЗапроса;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//---------------------------------------------------------
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.СуммаРубОстаток КАК СуммаРубОстаток,
		|	ОстаткиНоменклатурыОстатки.СуммаВалОстаток КАК СуммаВалОстаток,
		|	ПРЕДСТАВЛЕНИЕ(вт_ДокТЧ.Номенклатура) КАК НоменклатураПредставление,
		|	вт_ДокТЧ.Услуга КАК Услуга
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК ДокТЧ)) КАК ОстаткиНоменклатурыОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	Партия УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток),
		|	МАКСИМУМ(Услуга)
		|ПО
		|	Номенклатура";
	
	Если МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		Запрос.Текст				= СтрЗаменить(Запрос.Текст, "Партия УБЫВ", "Партия ВОЗР");
	КонецЕсли; 
	
	РезультатЗапроса 				= Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
		
			СтоимостьВРуб			= 0;
			СтоимостьВВал			= 0;
			
			Если НЕ ВыборкаНоменклатура.Услуга Тогда
				Если ВыборкаНоменклатура.КоличествоОстаток < ВыборкаНоменклатура.Количество Тогда
					Сообщение		= Новый СообщениеПользователю; 
					Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2",
										ВыборкаНоменклатура.НоменклатураПредставление, ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток); 		
					Сообщение.Сообщить();
					Отказ			= Истина;
				КонецЕсли;
				
				Если Отказ Тогда
					Продолжить;
				КонецЕсли; 
				
				ОсталосьСписать		= ВыборкаНоменклатура.Количество;
				Выборка				= ВыборкаНоменклатура.Выбрать();
				
				Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
					Списываем		= Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
					
					// проблема копеек
					Если Списываем = Выборка.КоличествоОстаток Тогда
						СебестоимостьРуб	= Выборка.СуммаРубОстаток;
						СебестоимостьВал	= Выборка.СуммаВалОстаток;
					Иначе	
						СебестоимостьРуб	= Выборка.СуммаРубОстаток * Списываем / Выборка.КоличествоОстаток;
						СебестоимостьВал	= Выборка.СуммаВалОстаток * Списываем / Выборка.КоличествоОстаток;
					КонецЕсли;
					
					Движение				= Движения.ОстаткиНоменклатуры.ДобавитьРасход();
					Движение.Период			= Дата;
					Движение.Номенклатура	= Выборка.Номенклатура;
					Движение.Партия			= Выборка.Партия;
					Движение.Количество		= Списываем;
					Движение.СуммаРуб		= СебестоимостьРуб;
					Движение.СуммаВал		= СебестоимостьВал;
					
					СтоимостьВРуб			= СтоимостьВРуб + СебестоимостьРуб;
					СтоимостьВВал			= СтоимостьВВал + СебестоимостьВал;
						
					ОсталосьСписать			= ОсталосьСписать - Списываем;
				КонецЦикла;
		
			КонецЕсли;
		
			Движение = Движения.Продажи.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаНоменклатура.Номенклатура;
			Движение.Количество = ВыборкаНоменклатура.Количество;
			Движение.СебестоимостьРуб = СтоимостьВРуб;
			Движение.СебестоимостьВал = СтоимостьВВал;
			Движение.СуммаПродажиРуб = ВыборкаНоменклатура.Сумма;
			Движение.СуммаПродажиВал = ?(КурсДоллара=0, 0, ВыборкаНоменклатура.Сумма / КурсДоллара);
			
		КонецЦикла;
	
	КонецЕсли;
	
	// БУ
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.БлокироватьДляИзменения	= Истина;
	Движения.ЖурналПроводок.Записывать = Истина;
	
	Движение = Движения.ЖурналПроводок.Добавить();
	Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
	Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
	Движение.Период = Дата;
	Движение.Сумма = СписокНоменклатуры.Итог("Сумма");
	Движение.СуммаВалДт = ?(КурсЕвро=0, 0 , СписокНоменклатуры.Итог("Сумма") / КурсЕвро);
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Договор;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ДокументыОтгрузки] = Ссылка;

	
КонецПроцедуры
