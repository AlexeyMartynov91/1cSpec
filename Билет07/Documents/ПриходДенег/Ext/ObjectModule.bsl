
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.Записать();
	Движения.ЖурналПроводок.БлокироватьДляИзменения	= Истина;
	Движения.ЖурналПроводок.Записывать = Истина;
	
	//-------------- Блокировка ----------------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");		
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Покупатели);
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Контрагенты, Контрагент);
	Блокировка.Заблокировать();
	//------------------------------------------------

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ЖурналПроводокОстатки.Субконто2 КАК Справочник.Договоры) КАК Договор,
		|	ЖурналПроводокОстатки.Субконто3 КАК Документ,
		|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ЖурналПроводокОстатки.СуммаВалОстаток КАК СуммаВалОстаток,
		|	ВЫРАЗИТЬ(ЖурналПроводокОстатки.Субконто2 КАК Справочник.Договоры).ДатаОкончанияДействия КАК ДатаОкончанияДействия,
		|	ЖурналПроводокОстатки.Субконто3.Дата КАК ДатаОтгрузки
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводок.Остатки(&МоментВремени, Счет = &Счет, &ВидыСубконто, Субконто1 = &Контрагент) КАК ЖурналПроводокОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаОкончанияДействия,
		|	ДатаОтгрузки
		|ИТОГИ
		|	СУММА(СуммаОстаток)
		|ПО
		|	ОБЩИЕ";
	
	ВидыСубконто		= Новый Массив(3);
	ВидыСубконто[0]		= ПланыВидовХарактеристик.ВидыСубконто.Контрагенты;
	ВидыСубконто[1]		= ПланыВидовХарактеристик.ВидыСубконто.Договоры;
	ВидыСубконто[2]		= ПланыВидовХарактеристик.ВидыСубконто.ДокументыОтгрузки;
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Управленческий.Покупатели);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщийИтог.Следующий();		// Общий итог
	
	Если СуммаОплаты > ВыборкаОбщийИтог.СуммаОстаток Тогда
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= СтрШаблон("Сумма оплаты превышает общую сумма задолженности на %1 руб.", СокрЛП(СуммаОплаты - ВыборкаОбщийИтог.СуммаОстаток));
		Сообщение.Сообщить();
		
		Отказ	= Истина;
		Возврат;
	КонецЕсли;
	
	ОсталосьСписать		= СуммаОплаты;
	
	Выборка	= ВыборкаОбщийИтог.Выбрать();
	Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
		Списать			= Мин(ОсталосьСписать, Выборка.СуммаОстаток);
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Касса;
		Движение.СчетКт = ПланыСчетов.Управленческий.Покупатели;
		Движение.Период = Дата;
		Движение.Сумма = Списать;
		Движение.СуммаВалКт = ?(КурсЕвро = 0, 0, Списать / КурсЕвро);
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Выборка.Договор;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.ДокументыОтгрузки] = Выборка.Документ;
			
		ОсталосьСписать	= ОсталосьСписать - Списать;	
	КонецЦикла;

	
КонецПроцедуры
