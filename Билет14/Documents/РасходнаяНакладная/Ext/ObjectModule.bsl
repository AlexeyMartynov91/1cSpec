
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Курс	= ?(ВалютаВзаиморасчетов = Справочники.Валюты.РоссийскийРубль, 1, 
				РегистрыСведений.КурсыВалют.ПолучитьПоследнее(МоментВремени(), Новый Структура("Валюта", ВалютаВзаиморасчетов)).Курс);
	Если Курс = 0 Тогда
	    Отказ	= Истина;
		
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= СтрШаблон("Не задан курс для валюты");
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	НакладнаяАванс	= Документы.РасходнаяНакладная.ПустаяСсылка();
	Аванс			= 0;
	
	// регистр Взаиморасчеты Приход/Расход
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();
	
	Движение = Движения.Взаиморасчеты.ДобавитьПриход();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Накладная = Ссылка;
	Движение.Сумма = СуммаВВалюте;
	
	//----------- Блокировка ------------
	Блокировка				= Новый БлокировкаДанных;
	ЭлБлокировки			= Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлБлокировки.Режим		= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлБлокировки.УстановитьЗначение("Накладная", НакладнаяАванс);
	Блокировка.Заблокировать();
	//-----------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Накладная = &Аванс) КАК ВзаиморасчетыОстатки";
	
	Запрос.УстановитьПараметр("Аванс", НакладнаяАванс);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка	= РезультатЗапроса.Выбрать();
	    Выборка.Следующий();
		Аванс	= Выборка.Сумма;
	КонецЕсли;
	
	Если Аванс > 0 Тогда
		СуммаВРуб			= СуммаВВалюте * Курс;
		СуммаПогашенияРуб	= Мин(Аванс, СуммаВРуб);	
		
		Если СуммаПогашенияРуб = Аванс Тогда
			СуммаПогашенияВал	= СуммаПогашенияРуб / Курс;
		Иначе	
			СуммаПогашенияВал	= СуммаВВалюте;
		КонецЕсли;
		
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Ссылка;
		Движение.Сумма = СуммаПогашенияВал;
		
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = НакладнаяАванс;
		Движение.Сумма = СуммаПогашенияРуб;
		
КонецЕсли;

КонецПроцедуры
