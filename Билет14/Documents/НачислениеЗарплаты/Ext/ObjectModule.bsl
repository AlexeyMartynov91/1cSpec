
Процедура ОбработкаПроведения(Отказ, Режим)
	НачМес	= НачалоМесяца(Дата);
	
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Движения.ОсновныеНачисления.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыОсновныеНачисления.График КАК График,
		|	НачислениеЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
		|ГДЕ
		|	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник,
		|	Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Сотрудник КАК Сотрудник,
		|	вт_ДокТЧ.Подразделение КАК Подразделение,
		|	вт_ДокТЧ.График КАК График,
		|	вт_ДокТЧ.ВидРасчета КАК ВидРасчета,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Параметр
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&НачМес,
		|				(Сотрудник, Подразделение) В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Сотрудник,
		|						вт_ДокТЧ.Подразделение
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО вт_ДокТЧ.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
		|			И вт_ДокТЧ.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение";
	
	Запрос.УстановитьПараметр("НачМес", НачМес);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка		= РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.ПериодДействияНачало = НачМес;
		Движение.ПериодДействияКонец = КонецМесяца(НачМес);
		Движение.ПериодРегистрации = НачМес;
	КонецЦикла;
	
	// регистр ДопНачисления
	Движения.ДопНачисления.Записывать = Истина;
	Движения.ДопНачисления.Записать();
	Для Каждого стр Из ДопНачисления Цикл
		Движение = Движения.ДопНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, стр);
		Движение.ПериодРегистрации = НачМес;
		Движение.БазовыйПериодНачало = НачМес;
		Движение.БазовыйПериодКонец = КонецМесяца(НачМес);
	КонецЦикла;
	
	Движения.Записать();
	
	Расчет.Рассчитать(Ссылка, НачМес);

КонецПроцедуры
