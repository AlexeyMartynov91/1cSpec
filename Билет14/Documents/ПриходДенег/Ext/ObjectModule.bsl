
Процедура ОбработкаПроведения(Отказ, Режим)
	НакладнаяАванс	= Документы.РасходнаяНакладная.ПустаяСсылка();
	
	// регистр Взаиморасчеты Расход/Приход
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Взаиморасчеты.Записать();
	
	//----------- Блокировка ------------
	Блокировка				= Новый БлокировкаДанных;
	ЭлБлокировки			= Блокировка.Добавить("РегистрНакопления.Взаиморасчеты");
	ЭлБлокировки.Режим		= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	Блокировка.Заблокировать();
	//-----------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВзаиморасчетыОстатки.Накладная КАК Накладная,
		|	ВзаиморасчетыОстатки.Накладная.МоментВремени КАК НакладнаяМоментВремени,
		|	ВзаиморасчетыОстатки.Накладная.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ вт_Остатки
		|ИЗ
		|	РегистрНакопления.Взаиморасчеты.Остатки(
		|			&МоментВремени,
		|			Контрагент = &Контрагент
		|				И Накладная <> &НакладнаяАванс) КАК ВзаиморасчетыОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Остатки.Накладная КАК Накладная,
		|	вт_Остатки.НакладнаяМоментВремени КАК НакладнаяМоментВремени,
		|	ПРЕДСТАВЛЕНИЕ(вт_Остатки.ВалютаВзаиморасчетов) КАК ВалютаПРЕДСТАВЛЕНИЕ,
		|	вт_Остатки.СуммаОстаток КАК СуммаОстаток,
		|	ВЫБОР
		|		КОГДА вт_Остатки.ВалютаВзаиморасчетов = &ВалютаРубль
		|			ТОГДА 1
		|		ИНАЧЕ ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0)
		|	КОНЕЦ КАК Курс
		|ИЗ
		|	вт_Остатки КАК вт_Остатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&МоментВремени,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						вт_Остатки.ВалютаВзаиморасчетов
		|					ИЗ
		|						вт_Остатки КАК вт_Остатки)) КАК КурсыВалютСрезПоследних
		|		ПО вт_Остатки.ВалютаВзаиморасчетов = КурсыВалютСрезПоследних.Валюта";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("НакладнаяАванс", НакладнаяАванс);
	Запрос.УстановитьПараметр("ВалютаРубль", Справочники.Валюты.РоссийскийРубль);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	СуммаОплаты		= Сумма;
	
	Пока Выборка.Следующий() И СуммаОплаты > 0 Цикл
		Если Выборка.Курс = 0 Тогда
		    Отказ	= Истина;
		
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Не задан курс для валюты %1", Выборка.ВалютаПРЕДСТАВЛЕНИЕ);
			Сообщение.Сообщить();
		
		КонецЕсли;
		
		Если Отказ Тогда
		    Продолжить;
		КонецЕсли;
		
		СуммаПогашения	= Мин(СуммаОплаты, Выборка.СуммаОстаток * Выборка.Курс);
		
		Если СуммаПогашения = СуммаОплаты Тогда
			СуммаПогашенияВал	= СуммаПогашения / Выборка.Курс;
		Иначе			
			СуммаПогашенияВал	= Выборка.СуммаОстаток;
		КонецЕсли;
		
		Движение = Движения.Взаиморасчеты.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = Выборка.Накладная;
		Движение.Сумма = СуммаПогашенияВал;
		
		СуммаОплаты	= СуммаОплаты - СуммаПогашения;
	КонецЦикла;
	
	Если СуммаОплаты > 0 Тогда
		Движение = Движения.Взаиморасчеты.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Накладная = НакладнаяАванс;
		Движение.Сумма = СуммаОплаты;
	КонецЕсли;

КонецПроцедуры
