
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.Записать();
	
	//----------- Блокировка ------------
	Блокировка					= Новый БлокировкаДанных;
	ЭлБлокировки				= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Материалы);
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
	ЭлБлокировки.ИсточникДанных	= СоставКомплекта;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Деталь");
	Блокировка.Заблокировать();
	//-----------------------------------

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СборкаСоставКомплекта.Деталь КАК Деталь,
		|	СУММА(СборкаСоставКомплекта.Количество) КАК Количество
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.Сборка.СоставКомплекта КАК СборкаСоставКомплекта
		|ГДЕ
		|	СборкаСоставКомплекта.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СборкаСоставКомплекта.Деталь
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Деталь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(вт_ДокТЧ.Деталь, СоставКомплектов.Деталь)) КАК КолДеталей,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(вт_ДокТЧ.Количество, 0) = ЕСТЬNULL(СоставКомплектов.Количество, 0)
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ) КАК НеСтандарт
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.СоставКомплектов КАК СоставКомплектов
		|		ПО вт_ДокТЧ.Деталь = СоставКомплектов.Деталь
		|ГДЕ
		|	СоставКомплектов.Комплект = &Комплект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Деталь КАК Деталь,
		|	вт_ДокТЧ.Деталь.Представление КАК ДетальПредставление,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	ЕСТЬNULL(ЖурналПроводокОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетМатериалы,
		|				&СубконтоМатериалы,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Деталь
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Субконто2 = &Склад) КАК ЖурналПроводокОстатки
		|		ПО вт_ДокТЧ.Деталь = ЖурналПроводокОстатки.Субконто1";
	
	СубконтоМатериалы		= Новый Массив(2);
	СубконтоМатериалы[0]	= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	СубконтоМатериалы[1]	= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	
	Запрос.УстановитьПараметр("Комплект", Комплект);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СубконтоМатериалы", СубконтоМатериалы);
	Запрос.УстановитьПараметр("СчетМатериалы", ПланыСчетов.Управленческий.Материалы);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаТип	= РезультатЗапроса[1].Выбрать();
	ВыборкаТип.Следующий();
	Если ВыборкаТип.НеСтандарт Тогда
		ТипСборки	= Перечисления.ТипСборки.НеСтандарт;
	Иначе	
		ТипСборки	= Перечисления.ТипСборки.Стандарт;
	КонецЕсли;
	
	Стоимость	= 0;
	Выборка		= РезультатЗапроса[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
		    Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2", 
								Выборка.ДетальПредставление, СокрЛП(Выборка.Количество - Выборка.КоличествоОстаток));
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
		    Продолжить;
		КонецЕсли;
		
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость		= Выборка.СуммаОстаток; 
		Иначе	
			Себестоимость		= Выборка.СуммаОстаток * Выборка.Количество / Выборка.КоличествоОстаток; 
		КонецЕсли;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.ОсновноеПроизводство;
		Движение.СчетКт = ПланыСчетов.Управленческий.Материалы;
		Движение.Период = Дата;
		Движение.КоличествоКт = Выборка.Количество;
		Движение.Сумма = Себестоимость;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Деталь;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
		
		Стоимость	= Стоимость + Себестоимость;
	КонецЦикла;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Движение = Движения.ЖурналПроводок.Добавить();
	Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
	Движение.СчетКт = ПланыСчетов.Управленческий.ОсновноеПроизводство;
	Движение.Период = Дата;
	Движение.КоличествоДт = 1;
	Движение.Сумма = Стоимость;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Комплект;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.ТипыСборки] = ТипСборки;

КонецПроцедуры
