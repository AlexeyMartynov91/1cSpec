
&НаКлиенте
Процедура МенеджерПриИзменении(Элемент)
	ОбновитьДопРеквизиты(Объект.Менеджер);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьДопРеквизиты(Объект.Менеджер);
КонецПроцедуры

&НаСервере
Процедура ОбновитьДопРеквизиты(Менеджер)
	
	ДобавляемыеРеквизиты	= Новый Массив;
	УдаляемыеРеквизиты		= Новый Массив;
	
	Префикс	= "ДопРеквизит_";
	ТекущиеРеквизиты		= ПолучитьРеквизиты();
	
	Для каждого РеквФормы Из ТекущиеРеквизиты Цикл
		Если СтрНайти(РеквФормы.Имя, Префикс) > 0 Тогда
			УдаляемыеРеквизиты.Добавить(РеквФормы.Имя); 
			УдаляемыйЭлемент	= Элементы.Найти(РеквФормы.Имя);
			Если УдаляемыйЭлемент <> Неопределено Тогда
				Элементы.Удалить(УдаляемыйЭлемент);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	""ДопРеквизит_"" + ФизическиеЛицаДопРеквизиты.Свойство.Код КАК Имя,
		|	ФизическиеЛицаДопРеквизиты.Свойство.Наименование КАК Заголовок,
		|	ФизическиеЛицаДопРеквизиты.Свойство.ТипЗначения КАК Тип,
		|	ЕСТЬNULL(КонтрагентыДопСведенья.Значение, НЕОПРЕДЕЛЕНО) КАК Значение,
		|	ФизическиеЛицаДопРеквизиты.Свойство КАК Свойство
		|ИЗ
		|	Справочник.ФизическиеЛица.ДопРеквизиты КАК ФизическиеЛицаДопРеквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты.ДопСведенья КАК КонтрагентыДопСведенья
		|		ПО ФизическиеЛицаДопРеквизиты.Ссылка = КонтрагентыДопСведенья.Ссылка.Менеджер
		|			И ФизическиеЛицаДопРеквизиты.Свойство = КонтрагентыДопСведенья.Свойство
		|ГДЕ
		|	ФизическиеЛицаДопРеквизиты.Ссылка = &Менеджер";
	
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НовРеквизит	= Новый РеквизитФормы(Выборка.Имя, Выборка.Тип, , Выборка.Заголовок);
	    ДобавляемыеРеквизиты.Добавить(НовРеквизит);
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		НовЭлемент				= Элементы.Добавить(Выборка.Имя, Тип("ПолеФормы"), ЭтаФорма); 
		НовЭлемент.Вид			= ВидПоляФормы.ПолеВвода;
		НовЭлемент.ПутьКДанным	= Выборка.Имя;		
		
		МассивПараметров		= Новый Массив;
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Выборка.Свойство));
		НовЭлемент.ПараметрыВыбора	= Новый ФиксированныйМассив(МассивПараметров);
		
		ЭтотОбъект[Выборка.Имя]	= Выборка.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДопСведенья.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизическиеЛицаДопРеквизиты.Свойство КАК Свойство,
		|	""ДопРеквизит_"" + ФизическиеЛицаДопРеквизиты.Свойство.Код КАК Имя
		|ИЗ
		|	Справочник.ФизическиеЛица.ДопРеквизиты КАК ФизическиеЛицаДопРеквизиты
		|ГДЕ
		|	ФизическиеЛицаДопРеквизиты.Ссылка = &Менеджер";
	
	Запрос.УстановитьПараметр("Менеджер", ТекущийОбъект.Менеджер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗначениеРевизита	= ЭтотОбъект[Выборка.Имя];
		Если ЗначениеЗаполнено(ЗначениеРевизита) Тогда
			НовДопРекв	= ТекущийОбъект.ДопСведенья.Добавить();
			НовДопРекв.Свойство	= Выборка.Свойство;
			НовДопРекв.Значение	= ЗначениеРевизита;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

