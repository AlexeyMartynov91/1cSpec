Процедура Рассчитать(Регистратор, ПериодРегистрации) Экспорт
	// Оклад
	НаборЗаписей		= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
		|	ОсновныеНачисленияДанныеГрафика.ДнейПериодДействия КАК ДнейПлан
		|ПОМЕСТИТЬ вт_ДанныеГрафика
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник,
		|	Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	вт_ДанныеГрафика.ДнейПлан КАК ДнейПлан,
		|	ЕСТЬNULL(ДанныеТабеляОбороты.ОтработаноДнейОборот, 0) КАК ДнейФакт
		|ИЗ
		|	вт_ДанныеГрафика КАК вт_ДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеТабеля.Обороты(
		|				&ПериодРегистрации,
		|				КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ),
		|				,
		|				(Сотрудник, Подразделение) В
		|					(ВЫБРАТЬ
		|						вт_ДанныеГрафика.Сотрудник,
		|						вт_ДанныеГрафика.Подразделение
		|					ИЗ
		|						вт_ДанныеГрафика КАК вт_ДанныеГрафика)) КАК ДанныеТабеляОбороты
		|		ПО вт_ДанныеГрафика.Сотрудник = ДанныеТабеляОбороты.Сотрудник
		|			И вт_ДанныеГрафика.Подразделение = ДанныеТабеляОбороты.Подразделение
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	СтруктураПоиска		= Новый Структура("НомерСтроки", );
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки		= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Запись.Параметр * Выборка.ДнейФакт / Выборка.ДнейПлан;		
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	
	// Премия
	НаборЗаписей		= РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДопНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДопНачисленияБазаОсновныеНачисления.РезультатБаза КАК База
		|ИЗ
		|	РегистрРасчета.ДопНачисления.БазаОсновныеНачисления(
		|			&Измерения,
		|			&Измерения,
		|			,
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ДопНачисленияБазаОсновныеНачисления
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Измерения		= Новый Массив(1);
	Измерения[0]	= "Подразделение";
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	СтруктураПоиска		= Новый Структура("НомерСтроки", );
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки		= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.База * Запись.Параметр / 100;		
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	

КонецПроцедуры
