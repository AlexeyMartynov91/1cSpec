Процедура Рассчитать(Регистратор, ПериодРегистрации) Экспорт
	// Оплата по часовому тарифу
	НаборЗаписей	= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	// регистр НаезженныеЧасы
	НаборНаезжЧасы	= РегистрыНакопления.НаезженныеЧасы.СоздатьНаборЗаписей();
	НаборНаезжЧасы.Отбор.Регистратор.Установить(Регистратор);
	НаборНаезжЧасы.Прочитать();
	НаборНаезжЧасы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия КАК ЧасовФакт,
		|	ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия КАК ДнейФакт
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияДанныеГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки", );
	
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Если Запись.ВидРасчета	= ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоТарифу Тогда
				Запись.Результат	= Запись.Параметр * Выборка.ЧасовФакт;		
				
				НовЗаписьЧасы		= НаборНаезжЧасы.ДобавитьПриход();
				НовЗаписьЧасы.Период = ПериодРегистрации;
				НовЗаписьЧасы.Сотрудник = Запись.Сотрудник;
				НовЗаписьЧасы.Часы = Выборка.ЧасовФакт;
			Иначе	
				Запись.Результат	= -5000 * Выборка.ДнейФакт;		
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	НаборНаезжЧасы.Записать();
	
	// Компенсация за ремонт
	НаборЗаписей	= РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДопНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДопНачисленияБазаОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ДопНачисленияБазаОсновныеНачисления.РезультатБаза КАК База
		|ПОМЕСТИТЬ вт_База
		|ИЗ
		|	РегистрРасчета.ДопНачисления.БазаОсновныеНачисления(
		|			&Измерения,
		|			&Измерения,
		|			,
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ДопНачисленияБазаОсновныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_База.НомерСтроки КАК НомерСтроки,
		|	вт_База.База КАК База,
		|	СУММА(ЕСТЬNULL(НаезженныеЧасыОстатки.ЧасыОстаток, 0)) КАК НаезженныеЧасы
		|ИЗ
		|	вт_База КАК вт_База
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НаезженныеЧасы.Остатки(
		|				КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ),
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						вт_База.Сотрудник
		|					ИЗ
		|						вт_База КАК вт_База)) КАК НаезженныеЧасыОстатки
		|		ПО вт_База.Сотрудник = НаезженныеЧасыОстатки.Сотрудник
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_База.НомерСтроки,
		|	вт_База.База
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЕСТЬNULL(НаезженныеЧасыОстатки.ЧасыОстаток, 0)) > 1000
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Измерения		= Новый Массив(1);
	Измерения[0]	= "Сотрудник";
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки", );
	
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.База * Запись.Параметр /100;		
			Запись.ОтработаноЧасов	= Выборка.НаезженныеЧасы;
			
			НовЗаписьЧасы		= НаборНаезжЧасы.ДобавитьРасход();
			НовЗаписьЧасы.Период = ПериодРегистрации;
			НовЗаписьЧасы.Сотрудник = Запись.Сотрудник;
			НовЗаписьЧасы.Часы = Выборка.НаезженныеЧасы;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	НаборНаезжЧасы.Записать();
	

КонецПроцедуры

	