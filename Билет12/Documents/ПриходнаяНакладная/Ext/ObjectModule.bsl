
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр Заказы Расход
	Движения.Заказы.Записывать = Истина;
	Движения.Заказы.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Заказ КАК Заказ,
		|	ПриходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ПриходнаяНакладнаяСписокНоменклатуры.Количество КАК Количество,
		|	ПриходнаяНакладнаяСписокНоменклатуры.Сумма КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.ПриходнаяНакладная.СписокНоменклатуры КАК ПриходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	ПриходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Заказ КАК Заказ,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.Заказы.ДобавитьРасход();
		Движение.Период = Дата;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Заказ КАК Заказ,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ДокТЧ.Заказ.Представление КАК ЗаказПредставление,
		|	-ЗаказыОстатки.КоличествоОстаток КАК Превышение
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Заказы.Остатки(
		|				&МоментВремениВкл,
		|				(Номенклатура, Заказ) В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура,
		|						вт_ДокТЧ.Заказ
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЗаказыОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ЗаказыОстатки.Номенклатура
		|			И вт_ДокТЧ.Заказ = ЗаказыОстатки.Заказ
		|ГДЕ
		|	ЗаказыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремениВкл", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Отказ	= Истина;	
		
		ВыборкаОтрицОстатки	= РезультатЗапроса.Выбрать();
		Пока ВыборкаОтрицОстатки.Следующий() Цикл
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 по заказу %2 в количестве %3", 
								ВыборкаОтрицОстатки.НоменклатураПредставление, ВыборкаОтрицОстатки.ЗаказПредставление, ВыборкаОтрицОстатки.Превышение);			
		    Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
	    Возврат;
	КонецЕсли;
	
	// регистр ОстаткиНоменклатуры Приход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	Выборка.Сбросить();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Период = Дата;
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказыПокупателя") Тогда
		Для Каждого ТекСтрокаСписокНоменклатуры Из ДанныеЗаполнения.СписокНоменклатуры Цикл
			НоваяСтрока = СписокНоменклатуры.Добавить();
			НоваяСтрока.Количество = ТекСтрокаСписокНоменклатуры.Количество;
			НоваяСтрока.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
			НоваяСтрока.Заказ	= ДанныеЗаполнения.Ссылка;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
