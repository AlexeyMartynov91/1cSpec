
Процедура ОбработкаПроведения(Отказ, Режим)
	// ОУ
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	//------------ Блокировка -------------
	Блокировка					= Новый БлокировкаДанных;
	ЭлБлокировки				= Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Заказ", Заказ);
	ЭлБлокировки.ИсточникДанных	= СписокНоменклатуры;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//-------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Заказ = &Заказ) КАК ОстаткиНоменклатурыОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|			И (&Заказ = ОстаткиНоменклатурыОстатки.Заказ)";
	
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
			Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2 по ОУ", 
								Выборка.НоменклатураПредставление, СокрЛП(Выборка.Количество - Выборка.КоличествоОстаток));			
		    Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
		    Продолжить;
		КонецЕсли;
		
		Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
			Себестоимость	= Выборка.СуммаОстаток;
		Иначе
			Себестоимость	= Выборка.СуммаОстаток * Выборка.Количество / Выборка.КоличествоОстаток;
		КонецЕсли;	
		
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Заказ = Заказ;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Себестоимость;
	КонецЦикла;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// БУ
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры КАК НоменклатураВидНоменклатуры,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК ГП,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	NULL КАК Комплектующее
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|ГДЕ
		|	вт_ДокТЧ.НоменклатураВидНоменклатуры = &ВидНомГП
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	NULL,
		|	вт_ДокТЧ.Количество,
		|	вт_ДокТЧ.Сумма,
		|	вт_ДокТЧ.Номенклатура
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|ГДЕ
		|	вт_ДокТЧ.НоменклатураВидНоменклатуры = &ВидНомКомплектующее";
	
	Запрос.УстановитьПараметр("ВидНомКомплектующее",	Перечисления.ВидыНоменклатуры.Комплектующее);
	Запрос.УстановитьПараметр("ВидНомГП", 				Перечисления.ВидыНоменклатуры.ГотоваяПродукция);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//------------ Блокировка -------------
	Блокировка					= Новый БлокировкаДанных;
	
	ЭлБлокировки				= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, СкладГП);
	ЭлБлокировки.ИсточникДанных	= РезультатЗапроса;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "ГП");
	
	ЭлБлокировки				= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Материалы);
	ЭлБлокировки.ИсточникДанных	= РезультатЗапроса;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Комплектующее");
	
	Блокировка.Заблокировать();
	//-------------------------------------
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	ВЫБОР
		|		КОГДА вт_ДокТЧ.НоменклатураВидНоменклатуры = &ВидНомГП
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ГП,
		|	ВЫБОР
		|		КОГДА вт_ДокТЧ.НоменклатураВидНоменклатуры = &ВидНомКомпл
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Комплектующее,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ЕСТЬNULL(ЖурналПроводокОстаткиМатериалы.КоличествоОстаток, 0) КАК КоличествоОстатокМатериалы,
		|	ЖурналПроводокОстаткиМатериалы.СуммаОстаток КАК СуммаОстатокМатериалы,
		|	ЕСТЬNULL(ЖурналПроводокОстаткиТовары.КоличествоОстаток, 0) КАК КоличествоОстатокТовары,
		|	ЖурналПроводокОстаткиТовары.СуммаОстаток КАК СуммаОстатокТовары
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&СубконтоНомСкл,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Субконто2 = &Склад) КАК ЖурналПроводокОстаткиТовары
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстаткиТовары.Субконто1
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетМатериалы,
		|				&СубконтоНом,
		|				Субконто1 В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЖурналПроводокОстаткиМатериалы
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстаткиМатериалы.Субконто1";
	
	СубконтоНомСкл		= Новый Массив(2);
	СубконтоНомСкл[0]	= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	СубконтоНомСкл[1]	= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	
	СубконтоНом		= Новый Массив(1);
	СубконтоНом[0]	= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	
	Запрос.УстановитьПараметр("СубконтоНом",	СубконтоНом);
	Запрос.УстановитьПараметр("СубконтоНомСкл", СубконтоНомСкл);
	Запрос.УстановитьПараметр("ВидНомКомпл",	Перечисления.ВидыНоменклатуры.Комплектующее);
	Запрос.УстановитьПараметр("ВидНомГП", 		Перечисления.ВидыНоменклатуры.ГотоваяПродукция);
	Запрос.УстановитьПараметр("МоментВремени",	МоментВремени());
	Запрос.УстановитьПараметр("СчетТовары",		ПланыСчетов.Управленческий.Товары);
	Запрос.УстановитьПараметр("СчетМатериалы",	ПланыСчетов.Управленческий.Материалы);
	Запрос.УстановитьПараметр("Склад",			СкладГП);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка		= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ГП Тогда
			Если Выборка.Количество > Выборка.КоличествоОстатокТовары Тогда
				Отказ	= Истина;
				
				Сообщение		= Новый СообщениеПользователю;
				Сообщение.Текст	= СтрШаблон("Превышение остатка по товару %1 на складе %2 в количестве %3 по БУ", 
									Выборка.НоменклатураПредставление, СокрЛП(СкладГП), СокрЛП(Выборка.Количество - Выборка.КоличествоОстатокТовары));			
				Сообщение.Сообщить();
			КонецЕсли;
			
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			
			Если Выборка.Количество = Выборка.КоличествоОстатокТовары Тогда
				Себестоимость	= Выборка.СуммаОстатокТовары;
			Иначе
				Себестоимость	= Выборка.СуммаОстатокТовары * Выборка.Количество / Выборка.КоличествоОстатокТовары;
			КонецЕсли;	
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
			Движение.Период = Дата;
			Движение.КоличествоКт = Выборка.Количество;
			Движение.Сумма = Себестоимость;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = СкладГП;
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
			Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.Период = Дата;
			Движение.Сумма = Выборка.Сумма;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		ИначеЕсли Выборка.Комплектующее Тогда	
			Если Выборка.Количество > Выборка.КоличествоОстатокМатериалы Тогда
				Отказ	= Истина;
				
				Сообщение		= Новый СообщениеПользователю;
				Сообщение.Текст	= СтрШаблон("Превышение остатка по материалу %1 в количестве %2 по БУ", 
									Выборка.НоменклатураПредставление, СокрЛП(Выборка.Количество - Выборка.КоличествоОстатокМатериалы));			
				Сообщение.Сообщить();
			КонецЕсли;
			
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			
			Если Выборка.Количество = Выборка.КоличествоОстатокМатериалы Тогда
				Себестоимость	= Выборка.СуммаОстатокМатериалы;
			Иначе
				Себестоимость	= Выборка.СуммаОстатокМатериалы * Выборка.Количество / Выборка.КоличествоОстатокМатериалы;
			КонецЕсли;	
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт = ПланыСчетов.Управленческий.Материалы;
			Движение.Период = Дата;
			Движение.КоличествоКт = Выборка.Количество;
			Движение.Сумма = Себестоимость;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
			Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.Период = Дата;
			Движение.Сумма = Выборка.Сумма;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
		Иначе
			Отказ			= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Номенклатура %1 не является ни материалом ни товаром (БУ).", 
								Выборка.НоменклатураПредставление);			
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
