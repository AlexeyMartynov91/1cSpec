
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр УчетнаяПолитика
	Движения.УчетнаяПолитика.Очистить();
	Движения.УчетнаяПолитика.Записывать = Истина;
	Движение = Движения.УчетнаяПолитика.Добавить();
	Движение.Период = Дата;
	Движение.МетодСписанияТоваров = МетодСписания;
	
	Если МетодСписания = Перечисления.МетодСписания.ПоСредней Тогда
		// заблокироуем РН Остатки, получим их и спишем все партии и оприходуем с партией этот документ
		Движения.ОстаткиТоваров.Очистить();
		Движения.ОстаткиТоваров.БлокироватьДляИзменения	= Истина;
		Движения.ОстаткиТоваров.Записывать	= Истина;
		
		//----------- Блокировка --------------------
		Блокировка			= Новый БлокировкаДанных;
		ЭлБлокировки		= Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
		ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		//-------------------------------------------
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровОстатки.Партия КАК Партия,
		|	ОстаткиТоваровОстатки.КоличествоОстаток КАК Количество,
		|	ОстаткиТоваровОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(&МоментВремени, ) КАК ОстаткиТоваровОстатки";
		
		Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка	= РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Движение		= Движения.ОстаткиТоваров.ДобавитьРасход();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.Период	= Дата;

			Движение		= Движения.ОстаткиТоваров.ДобавитьПриход();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.Партия	= Ссылка;
			Движение.Период	= Дата;
		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Дата	= НачалоМесяца(Дата);
КонецПроцедуры
