
Процедура ОбработкаПроведенияОУ(Отказ, Режим)
	// регистр ОстаткиТоваров Расход
	Движения.ОстаткиТоваров.Очистить();
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	//----------- Блокировка ------------------
	Блокировка		= Новый БлокировкаДанных;
	ЭлБлокировки	= Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");	
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.ИсточникДанных	= ТоварыИУслуги;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//-----------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяТоварыИУслуги.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяТоварыИУслуги.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяТоварыИУслуги.Сумма) КАК Сумма,
		|	РасходнаяНакладнаяТоварыИУслуги.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.ТоварыИУслуги КАК РасходнаяНакладнаяТоварыИУслуги
		|ГДЕ
		|	РасходнаяНакладнаяТоварыИУслуги.Ссылка = &Ссылка
		|	И НЕ РасходнаяНакладнаяТоварыИУслуги.Номенклатура.Услуга
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяТоварыИУслуги.Номенклатура,
		|	РасходнаяНакладнаяТоварыИУслуги.НомерСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	ОстаткиТоваровОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ОстаткиТоваровОстатки.СуммаОстаток КАК СуммаОстаток,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ДокТЧ.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)) КАК ОстаткиТоваровОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ОстаткиТоваровОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиТоваровОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток),
		|	МИНИМУМ(НомерСтроки)
		|ПО
		|	Номенклатура,
		|	НоменклатураПредставление";
// !! УПОРЯДОЧИТЬ ПО  МоментВремени
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// не нужно ещё одну вложенность ВыборкаНоменклатураПредст, т.к. представление итак формируется при группировк по полю Номенклатура
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
	ВыборкаНоменклатураПредст = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатураПредст.Следующий() Цикл
		Если ВыборкаНоменклатураПредст.Количество > ВыборкаНоменклатураПредст.КоличествоОстаток Тогда
			Отказ	= Истина;
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Не хватает %1 товара %2", СокрЛП(ВыборкаНоменклатураПредст.Количество - ВыборкаНоменклатураПредст.КоличествоОстаток), ВыборкаНоменклатураПредст.НоменклатураПредставление);
			// Привязка выводимого сообщения к полю формы (требование задачи по управляемым формам)	- двойной клик мышкой на сообщении	
			// !! именно Объект. нужен
			Сообщение.Поле = "Объект.ТоварыИУслуги["+СокрЛП(ВыборкаНоменклатураПредст.НомерСтроки - 1)+"].Количество";
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
		    Продолжить;
		КонецЕсли; 
		
		ОсталосьСписать	= ВыборкаНоменклатураПредст.Количество;
		
		Выборка	= ВыборкаНоменклатураПредст.Выбрать();
		Пока Выборка.Следующий() и ОсталосьСписать > 0 Цикл
			Списать		= Мин(Выборка.Количество, Выборка.КоличествоОстаток);
			Если Списать = Выборка.КоличествоОстаток Тогда
				Себестоимость	= Выборка.СуммаОстаток;			
			Иначе		
				Себестоимость	= Выборка.Количество / Выборка.КоличествоОстаток * Выборка.СуммаОстаток;			
			КонецЕсли; 
			
			Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Партия = Выборка.Партия;
			Движение.Количество = Списать;
			Движение.Сумма = Себестоимость;
			
			ОсталосьСписать	= ОсталосьСписать - Списать;
		КонецЦикла;
	КонецЦикла;
	КонецЦикла;
	

КонецПроцедуры

Процедура ОбработкаПроведенияБУ(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.Записывать = Истина;
	
	Курс	= РегистрыСведений.КурсыВалют.ПолучитьПоследнее(МоментВремени(), Новый Структура("Валюта", Договор.ВалютаВзаиморасчетов)).Курс;
	Если НЕ ЗначениеЗаполнено(Курс) Тогда
		Курс	= 1;	
	КонецЕсли; 
	
	Движение = Движения.ЖурналПроводок.Добавить();
	Движение.СчетДт = ПланыСчетов.ПланСчетовБУ.Покупатели;
	Движение.СчетКт = ПланыСчетов.ПланСчетовБУ.ПрибыльИУбытки;
	Движение.Период = Дата;
	Движение.Сумма = СуммаВВалютеВзаиморасчетов * Курс;
	Движение.СуммаВВалДт = СуммаВВалютеВзаиморасчетов;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
	Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Договор;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если ОУ Тогда ОбработкаПроведенияОУ(Отказ, Режим);
	КонецЕсли; 	
	
	Если БУ Тогда ОбработкаПроведенияБУ(Отказ, Режим);
	КонецЕсли; 	
КонецПроцедуры
