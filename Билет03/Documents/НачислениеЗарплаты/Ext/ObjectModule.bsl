
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр НачисленияСотрудникам
	Движения.НачисленияСотрудникам.Очистить();
	Движения.НачисленияСотрудникам.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыНачисления.НомерСтроки КАК НомерСтроки,
		|	НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
		|	НачислениеЗарплатыНачисления.График КАК График,
		|	НачислениеЗарплатыНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
		|	НачислениеЗарплатыНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
		|	НачислениеЗарплатыНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	НачислениеЗарплатыНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
		|	СведеньяОСотрудникахСрезПоследних.Оклад КАК Параметр
		|ИЗ
		|	Документ.НачислениеЗарплаты.Начисления КАК НачислениеЗарплатыНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведеньяОСотрудниках.СрезПоследних(&Дата, ) КАК СведеньяОСотрудникахСрезПоследних
		|		ПО НачислениеЗарплатыНачисления.Сотрудник = СведеньяОСотрудникахСрезПоследних.Сотрудник
		|ГДЕ
		|	НачислениеЗарплатыНачисления.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.НачисленияСотрудникам.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.ПериодРегистрации	= Дата;
	КонецЦикла;
	
	// !! Обработка сторно записей (командировка может вводится задним числом)
	СторноЗаписи = Движения.НачисленияСотрудникам.ПолучитьДополнение();
	Для Каждого Запись из СторноЗаписи Цикл
		Движение = Движения.НачисленияСотрудникам.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Запись);
		Движение.ПериодРегистрации = Запись.ПериодРегистрацииСторно;
		Движение.ПериодДействияНачало = Запись.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Запись.ПериодДействияКонецСторно;
		Движение.Сторно = Истина;
	КонецЦикла;
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления(Ссылка);
	
КонецПроцедуры
