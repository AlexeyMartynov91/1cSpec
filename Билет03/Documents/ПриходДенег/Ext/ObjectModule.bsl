
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.БлокироватьДляИзменения = Истина;
	Движения.ЖурналПроводок.Записывать = Истина;
	
	//---------- Блокировка ----------------
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.ПланСчетовБУ.Покупатели);
	ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Контрагенты, Контрагент);
	Блокировка.Заблокировать();
	//--------------------------------------
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЖурналПроводокОстатки.Субконто2 КАК Договор,
		|	ЖурналПроводокОстатки.СуммаВВалОстаток КАК СуммаВВалОстаток,
		|	ЖурналПроводокОстатки.СуммаВВалОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК СуммаРуб,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводок.Остатки(&МоментВремени, Счет = &Счет, &МассивСубконто, Субконто1 = &Контрагент) КАК ЖурналПроводокОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСрезПоследних
		|		ПО ЖурналПроводокОстатки.Субконто2.ВалютаВзаиморасчетов = КурсыВалютСрезПоследних.Валюта
		|
		|УПОРЯДОЧИТЬ ПО
		|	СуммаРуб УБЫВ
		|ИТОГИ
		|	СУММА(СуммаРуб)
		|ПО
		|	ОБЩИЕ";
	
	МассивСубконто		= Новый Массив(2);
	МассивСубконто[0]	= ПланыВидовХарактеристик.ВидыСубконто.Контрагенты;
	МассивСубконто[1]	= ПланыВидовХарактеристик.ВидыСубконто.Договоры;
	
	Запрос.УстановитьПараметр("МассивСубконто", МассивСубконто);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.ПланСчетовБУ.Покупатели);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщение	= Новый СообщениеПользователю;
		Сообщение.Текст	= СтрШаблон("У контрагента %1 нет задолжности", Контрагент);
		Сообщение.Сообщить();
		Отказ = Истина;
		Возврат;	
	КонецЕсли; 
	
	ВыборкаИтоги	= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаИтоги.Следующий();
	
	Если Сумма > ВыборкаИтоги.СуммаРуб Тогда
		Сообщение	= Новый СообщениеПользователю;
		Сообщение.Текст	= "Сумма оплаты превышает имеющийся долг контрагента на "+ СокрЛП(Сумма - ВыборкаИтоги.СуммаРуб);
		Сообщение.Сообщить();
		Отказ	= Истина;
		Возврат;
	КонецЕсли; 
		
	Списываем	= Сумма;
	СообщениеОКурсеНезаданном	= 0;	// не обязательно
	Выборка	= ВыборкаИтоги.Выбрать();
	Пока Выборка.Следующий() И Списываем > 0 Цикл
		Если Выборка.СуммаРуб > Списываем Тогда
			Списать		= Списываем;
			СписатьВал	= Списываем / Выборка.Курс;
		Иначе
			Списать		= Выборка.СуммаРуб;
			СписатьВал	= Выборка.СуммаВВалОстаток; 
		КонецЕсли;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.ПланСчетовБУ.Касса;
		Движение.СчетКт = ПланыСчетов.ПланСчетовБУ.Покупатели;
		Движение.Период = Дата;
		Движение.Сумма = Списать;
		Движение.СуммаВВалКт = СписатьВал;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Контрагент;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договоры] = Выборка.Договор;
		
		Списываем	= Списываем - Списать;
	КонецЦикла;
	
КонецПроцедуры
