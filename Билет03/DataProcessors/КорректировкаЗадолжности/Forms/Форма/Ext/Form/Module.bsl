
&НаСервере
Процедура КорректировкаНаСервере()
	
	ДатаКор	= КонецДня(ДатаКорректировки);
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	Попытка
		
		//------------ Блокировка ----------------
		Блокировка			= Новый БлокировкаДанных;
		ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");	
		ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
		ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.ПланСчетовБУ.Покупатели);
		Блокировка.Заблокировать();
		//----------------------------------------
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЖурналПроводокОстатки.Субконто1 КАК Контрагент,
			|	ЖурналПроводокОстатки.Субконто2 КАК Договор,
			|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстаток,
			|	ЖурналПроводокОстатки.СуммаВВалОстаток КАК СуммаВВалОстаток,
			|	ЖурналПроводокОстатки.СуммаВВалОстаток * КурсыВалютСрезПоследних.Курс - ЖурналПроводокОстатки.СуммаОстаток КАК Увеличение
			|ИЗ
			|	РегистрБухгалтерии.ЖурналПроводок.Остатки(&Дата, Счет = &Счет, &ВидыСубконто, ) КАК ЖурналПроводокОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалютСрезПоследних
			|		ПО ЖурналПроводокОстатки.Субконто2.ВалютаВзаиморасчетов = КурсыВалютСрезПоследних.Валюта
			|ГДЕ
			|	ЖурналПроводокОстатки.СуммаВВалОстаток * КурсыВалютСрезПоследних.Курс - ЖурналПроводокОстатки.СуммаОстаток <> 0";
		
		ВидыСубконто		= Новый Массив(2);
		ВидыСубконто[0]		= ПланыВидовХарактеристик.ВидыСубконто.Контрагенты;	
		ВидыСубконто[1]		= ПланыВидовХарактеристик.ВидыСубконто.Договоры;	
		
		Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
		Запрос.УстановитьПараметр("Дата", ДатаКор);
		Запрос.УстановитьПараметр("Счет", ПланыСчетов.ПланСчетовБУ.Покупатели);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;	
		КонецЕсли; 
		
		ДокОперация			= Документы.Операция.СоздатьДокумент();
		ДокОперация.Дата	= ДатаКор;
		ДокОперация.Записать();
		
		НаборЗаписей		= РегистрыБухгалтерии.ЖурналПроводок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокОперация.Ссылка);
		НаборЗаписей.Очистить();
		
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Запись			= НаборЗаписей.Добавить();
			Запись.Период	= ДатаКор;
			
			Если Выборка.Увеличение > 0 Тогда
				Запись.СчетДт	= ПланыСчетов.ПланСчетовБУ.Покупатели; 
				Запись.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты]	= Выборка.Контрагент;
				Запись.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договоры]	= Выборка.Договор;
				Запись.СчетКт	= ПланыСчетов.ПланСчетовБУ.ПрибыльИУбытки;
				Запись.Сумма	= Выборка.Увеличение;
			Иначе	
				Запись.СчетДт	= ПланыСчетов.ПланСчетовБУ.ПрибыльИУбытки;
				Запись.СчетКт	= ПланыСчетов.ПланСчетовБУ.Покупатели; 
				Запись.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты]	= Выборка.Контрагент;
				Запись.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договоры]	= Выборка.Договор;
				Запись.Сумма	= -Выборка.Увеличение;
			КонецЕсли; 
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		Сообщение	= Новый СообщениеПользователю;
		Сообщение.Текст	= СтрШаблон("Документ успешно сформирован: %1", ДокОперация); 
		Сообщение.КлючДанных	= ДокОперация.Ссылка;	// Делаем сообщение кликабельным - при нажатии будет открываться новый документ - ДокОперация
 
		Сообщение.Сообщить();

		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		Сообщение	= Новый СообщениеПользователю;
		Сообщение.Текст	= СтрШаблон("Ошибка: %1", КраткоеПредставлениеОшибки(ИнформацияОбОшибке())); 
		Сообщение.Сообщить();
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура Корректировка(Команда)
	Если ПроверитьЗаполнение() Тогда	// Проверка заполнения обязательных для заполнения реквизитов, в данном случае даты актуальности
		КорректировкаНаСервере();
	КонецЕсли; 
КонецПроцедуры
