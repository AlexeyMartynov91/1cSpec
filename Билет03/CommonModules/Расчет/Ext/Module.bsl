Процедура РассчитатьНачисления(Регистратор) Экспорт
	
	НаборЗаписей		= РегистрыРасчета.НачисленияСотрудникам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	// Оклад
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудникамДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(НачисленияСотрудникамДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ФактДней,
		|	ЕСТЬNULL(НачисленияСотрудникамДанныеГрафика.ДнейПериодДействия, 0) КАК НормаДней,
		|	НачисленияСотрудникамДанныеГрафика.ЧасовФактическийПериодДействия КАК ФактЧасов,
		|	ЕСТЬNULL(НачисленияСотрудникамДанныеГрафика.ВечерниеЧасыФактическийПериодДействия, 0) КАК ФактЧасовВечер,
		|	НачисленияСотрудникамДанныеГрафика.ЧасовПериодДействия КАК НормаЧасов
		|ИЗ
		|	РегистрРасчета.НачисленияСотрудникам.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &ВидРасчета) КАК НачисленияСотрудникамДанныеГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка		= РезультатЗапроса.Выбрать();
		СтруктураПоиска		= Новый Структура("НомерСтроки",);
		Для каждого Запись Из НаборЗаписей Цикл
			СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
			Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
				Сторно = ?(Запись.Сторно, -1, 1);
				// !! Вечер - удаляемое условие (ресурсы в РС ГрафикиРабот и РР НачисленияСотрудникам)
				Вечер  = 1.5 * Выборка.ФактЧасовВечер / Выборка.НормаЧасов * Запись.Параметр ; 
				Запись.Результат		= Сторно * (Выборка.ФактДней / Выборка.НормаДней * Запись.Параметр + Вечер);
				Запись.ОтработанноЧасов	= Выборка.ФактЧасов;
				Запись.ВечерниеЧасы	= Выборка.ФактЧасовВечер;
			КонецЕсли;		
		КонецЦикла; 
		
		НаборЗаписей.Записать(,Истина);
	КонецЕсли;
	
	// Командировка
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудникамДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(НачисленияСотрудникамДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ФактЧасов,
		|	ЕСТЬNULL(НачисленияСотрудникамБазаНачисленияСотрудникам.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(НачисленияСотрудникамБазаНачисленияСотрудникам.ОтработанноЧасовБаза, 0) КАК ОтработанноЧасовБаза
		|ИЗ
		|	РегистрРасчета.НачисленияСотрудникам.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &ВидРасчета) КАК НачисленияСотрудникамДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.НачисленияСотрудникам.БазаНачисленияСотрудникам(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Регистратор
		|					И ПериодРегистрации = &ПериодРегистрации
		|					И ВидРасчета = &ВидРасчета) КАК НачисленияСотрудникамБазаНачисленияСотрудникам
		|		ПО НачисленияСотрудникамДанныеГрафика.Регистратор = НачисленияСотрудникамБазаНачисленияСотрудникам.Регистратор
		|			И НачисленияСотрудникамДанныеГрафика.НомерСтроки = НачисленияСотрудникамБазаНачисленияСотрудникам.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Измерения		= Новый Массив(2);
	Измерения[0]	= "Сотрудник";
	Измерения[1]	= "Подразделение";
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ОсновныеНачисления.Командировка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;	
	КонецЕсли;
	
	Выборка		= РезультатЗапроса.Выбрать();
	СтруктураПоиска		= Новый Структура("НомерСтроки",);
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.РезультатБаза / Выборка.ОтработанноЧасовБаза * Выборка.ФактЧасов;
		КонецЕсли;;
	КонецЦикла; 
	
	НаборЗаписей.Записать(,Истина);

КонецПроцедуры
