
Процедура ПередЗаписью(Отказ, Замещение)
	// !!!	
	// Запрос для формирования записей в таблицу перерасчетов
	// Во временной таблице ВТ_НаборЗаписей передаем параметром данные текущего набора записей регистра
	// Во временной таблице ВТ_ДанныеДляПерарасчета получаем данные из регистра расчета, которые требуется перерасчитать
	// В последней таблице запроса исключаем из данных таблицы ВТ_ДанныеДляПерерасчета те записи, которые уже есть в таблице перерасчетов
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаборЗаписей.Сотрудник КАК Сотрудник,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(НаборЗаписей.Период, МЕСЯЦ), МЕСЯЦ, 1) КАК Период
	|ПОМЕСТИТЬ ВТ_НаборЗаписей
	|ИЗ
	|	&НаборЗаписей КАК НаборЗаписей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеНачисления.Сотрудник КАК Сотрудник,
	|	ДополнительныеНачисления.Регистратор КАК Регистратор,
	|	ДополнительныеНачисления.ВидРасчета КАК ВидРасчета
	|ПОМЕСТИТЬ ВТ_ДанныеДляПерерасчета
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НаборЗаписей КАК ВТ_НаборЗаписей
	|		ПО ДополнительныеНачисления.Сотрудник = ВТ_НаборЗаписей.Сотрудник
	|			И ДополнительныеНачисления.ПериодРегистрации = ВТ_НаборЗаписей.Период
	|ГДЕ
	|	ДополнительныеНачисления.ВидРасчета = &ВидРасчетаНадбавка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДляПерерасчета.Сотрудник КАК Сотрудник,
	|	ВТ_ДанныеДляПерерасчета.Регистратор КАК Регистратор,
	|	ВТ_ДанныеДляПерерасчета.ВидРасчета КАК ВидРасчета
	|ИЗ
	|	ВТ_ДанныеДляПерерасчета КАК ВТ_ДанныеДляПерерасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.Перерасчет1 КАК ПерерасчетДополнительныеНачисления
	|		ПО ВТ_ДанныеДляПерерасчета.Сотрудник = ПерерасчетДополнительныеНачисления.Сотрудник
	|			И ВТ_ДанныеДляПерерасчета.Регистратор = ПерерасчетДополнительныеНачисления.ОбъектПерерасчета
	|			И ВТ_ДанныеДляПерерасчета.ВидРасчета = ПерерасчетДополнительныеНачисления.ВидРасчета
	|ГДЕ
	|	ПерерасчетДополнительныеНачисления.ОбъектПерерасчета ЕСТЬ NULL
	|ИТОГИ ПО
	|	Регистратор";
	
	Запрос.УстановитьПараметр("НаборЗаписей", Выгрузить()); // Передаем параметром запроса текущий набор записей, преобразованный в таблицу значений  
	Запрос.УстановитьПараметр("ВидРасчетаНадбавка", ПланыВидовРасчета.ДополнительныеНачисления.Надбавка);
	
	// Обход результатов запроса, заполнение таблицы перерасчетов
	ВыборкаДокумент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДокумент.Следующий() Цикл
		НаборЗаписейПерерасчета = РегистрыРасчета.ДополнительныеНачисления.Перерасчеты.Перерасчет1.СоздатьНаборЗаписей();
		НаборЗаписейПерерасчета.Отбор.ОбъектПерерасчета.Установить (ВыборкаДокумент.Регистратор);
		
		Выборка = ВыборкаДокумент.Выбрать();
		Пока Выборка.Следующий() Цикл
			Запись = НаборЗаписейПерерасчета.Добавить();
			Запись.ВидРасчета = Выборка.ВидРасчета;
			Запись.Сотрудник = Выборка.Сотрудник;
			Запись.ОбъектПерерасчета = Выборка.Регистратор;
		КонецЦикла;
		
		НаборЗаписейПерерасчета.Записать(Ложь);
		
	КонецЦикла;

КонецПроцедуры
