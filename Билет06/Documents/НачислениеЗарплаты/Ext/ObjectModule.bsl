
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПоказателиДляДопНачислений	= РегистрыСведений.ПоказателиДляНачислений.ПолучитьПоследнее(Дата);
	
	// регистр ДополнительныеНачисления
	Движения.ДополнительныеНачисления.Очистить();
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Для Каждого стр Из ДопНачисления Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.ВидРасчета = стр.ВидРасчета;
		Движение.ПериодРегистрации = стр.ДатаНачала;
		Движение.ПериодДействияНачало = стр.ДатаНачала;
		Движение.ПериодДействияКонец = стр.ДатаОкончания;
		Движение.Автомобиль	= стр.Автомобиль; 
		Движение.Сотрудник = стр.Сотрудник;
		Если стр.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.ФиксированнаяСумма Тогда
			Движение.Параметр	= ПоказателиДляДопНачислений.ФиксированнаяСумма;
			Движение.Результат	= ПоказателиДляДопНачислений.ФиксированнаяСумма; 
		ИначеЕсли стр.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.Надбавка Тогда
			Движение.Параметр	= ПоказателиДляДопНачислений.ПроцентНадбавки;
		КонецЕсли;
	КонецЦикла;

	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Очистить();
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого стр Из ОсновныеНачисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.ВидРасчета = стр.ВидРасчета;
		Движение.ПериодДействияНачало = стр.ДатаНачала;
		Движение.ПериодДействияКонец = стр.ДатаОкончания;
		Движение.ПериодРегистрации = Дата;
		Движение.БазовыйПериодНачало = ДобавитьМесяц(стр.ДатаНачала, -3);
		Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(стр.ДатаОкончания, -1));
		Движение.Сотрудник = стр.Сотрудник;
		Движение.График = Справочники.Графики.Пятидневка;
	КонецЦикла;
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления(Ссылка);

КонецПроцедуры
