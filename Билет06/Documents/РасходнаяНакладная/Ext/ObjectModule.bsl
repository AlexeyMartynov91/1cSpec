
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Очистить();
	Движения.ОстаткиНоменклатуры.БлокироватьДляИзменения = Истина;
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
	// Новая методика проведения
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(Комплекты.Деталь, РасходнаяНакладнаяСписокНоменклатуры.Номенклатура) КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество * ЕСТЬNULL(Комплекты.КоличествоДеталей, 1)) КАК Количество
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Комплекты КАК Комплекты
		|		ПО РасходнаяНакладнаяСписокНоменклатуры.Номенклатура = Комплекты.Стеллаж
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(Комплекты.Деталь, РасходнаяНакладнаяСписокНоменклатуры.Номенклатура)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Склад = Склад;
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	Движения.Записать();

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	-ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Превышение,
		|	ОстаткиНоменклатурыОстатки.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремениВкл,
		|			Номенклатура В
		|					(ВЫБРАТЬ
		|						вт_ДокТЧ.Номенклатура
		|					ИЗ
		|						вт_ДокТЧ КАК вт_ДокТЧ)
		|				И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
		|ГДЕ
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("МоментВремениВкл", Новый Граница(МоментВремени(), ВидГраницы.Включая));

	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2", Выборка.НоменклатураПредставление, Выборка.Превышение); 
			Сообщение.Сообщить();
		КонецЦикла;
		
		Отказ	= Истина;
		Возврат;
	КонецЕсли; 
	
	
	// БухгалтерскийУчет
	МетодСписания	= РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(МоментВремени()).МетодСписанияПартий;
	
	Если Не ЗначениеЗаполнено(МетодСписания) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнена учетная политика";
		Сообщение.Сообщить();
		
		Отказ = Истина;		
		Возврат;
	КонецЕсли;

	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.БлокироватьДляИзменения	= Истина;  
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.Записать();	
	
	//-------------- Блокировка ----------------
	Блокировка			= Новый БлокировкаДанных;
	ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
//	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);	// не нужно склад, т.к. себестоимость по партиям по всем складам	
	ЭлБлокировки.ИсточникДанных	= СписокНоменклатуры;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Товары, "Номенклатура");
	Блокировка.Заблокировать();
	//------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЖурналПроводокОстатки.Субконто2 КАК Партия,
		|	ЖурналПроводокОстатки.КоличествоОстаток КАК КоличествоОстатокПартии,
		|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстатокПартии,
		|	ЖурналПроводокОстатки.Субконто1 КАК Номенклатура
		|ПОМЕСТИТЬ вт_ОстаткиПоПартиям
		|ИЗ
		|	РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|			&МоментВремени,
		|			Счет = &Счет,
		|			&ВидыСубконтоПартии,
		|			Субконто1 В
		|				(ВЫБРАТЬ
		|					вт_ДокТЧ.Номенклатура
		|				ИЗ
		|					вт_ДокТЧ КАК вт_ДокТЧ)) КАК ЖурналПроводокОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ЖурналПроводокОстатки.Субконто3 КАК Партия,
		|	ЖурналПроводокОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ОстаткиПоПартиям.СуммаОстатокПартии КАК СуммаОстатокПартии,
		|	вт_ОстаткиПоПартиям.КоличествоОстатокПартии КАК КоличествоОстатокПартии
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &Счет,
		|				&ВидыСубконто,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Субконто2 = &Склад) КАК ЖурналПроводокОстатки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ вт_ОстаткиПоПартиям КАК вт_ОстаткиПоПартиям
		|			ПО ЖурналПроводокОстатки.Субконто3 = вт_ОстаткиПоПартиям.Партия
		|				И ЖурналПроводокОстатки.Субконто1 = вт_ОстаткиПоПартиям.Номенклатура
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстатки.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЖурналПроводокОстатки.Субконто3.МоментВремени УБЫВ
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Если МетодСписания <> Перечисления.УчетнаяПолитика.ЛИФО Тогда
		Запрос.Текст	= СтрЗаменить(Запрос.Текст, "ЖурналПроводокОстатки.Субконто3.МоментВремени УБЫВ", "ЖурналПроводокОстатки.Субконто3.МоментВремени ВОЗР");
	КонецЕсли; 
	
	ВидыСубконтоПартии		= Новый Массив(2);
	ВидыСубконтоПартии[0]	= ПланыВидовХарактеристик.ВидыСубконто.Товары;
	ВидыСубконтоПартии[1]	= ПланыВидовХарактеристик.ВидыСубконто.Партии;
	
	ВидыСубконто		= Новый Массив(3);
	ВидыСубконто[0]		= ПланыВидовХарактеристик.ВидыСубконто.Товары;
	ВидыСубконто[1]		= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	ВидыСубконто[2]		= ПланыВидовХарактеристик.ВидыСубконто.Партии;
	
	Запрос.УстановитьПараметр("ВидыСубконтоПартии", ВидыСубконтоПартии);
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Управленческий.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по БУ по номенклатуре %1 в количестве %2 на складе %3", 
								ВыборкаНоменклатура.НоменклатураПредставление, (ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток), Склад); 			
			Сообщение.Сообщить();
			Отказ	= Истина;
		КонецЕсли;
		
		Если Отказ Тогда
		    Продолжить;
		КонецЕсли; 
		
		Списываем	= ВыборкаНоменклатура.Количество;
		
		Выборка	= ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И Списываем > 0 Цикл
			Списать	= Мин(Списываем, Выборка.КоличествоОстаток);
			
			Если Списать = Выборка.КоличествоОстатокПартии Тогда
				Себестоимость	= Выборка.СуммаОстатокПартии ;
			Иначе	
				Себестоимость	= Выборка.СуммаОстатокПартии * Списать / Выборка.КоличествоОстатокПартии;
			КонецЕсли;
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
			Движение.Период = Дата;
			Движение.КоличествоКт = Списать;
			Движение.Сумма = Себестоимость;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Товары] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Выборка.Партия;
			
			Списываем	= Списываем - Списать;
		КонецЦикла;
	КонецЦикла;
	
	Если Отказ Тогда
	    Возврат;
	КонецЕсли; 

	Движение = Движения.ЖурналПроводок.Добавить();
	Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
	Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
	Движение.Период = Дата;
	Движение.Сумма = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры
