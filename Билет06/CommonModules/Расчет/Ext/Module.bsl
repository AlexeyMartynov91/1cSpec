Процедура РассчитатьНачисления(Регистратор) Экспорт
	// Надбавка и Фиксированная сумма
	НаборЗаписей	= РегистрыРасчета.ДополнительныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ДополнительныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ФактДней
		|ПОМЕСТИТЬ вт_График
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ДополнительныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_График.НомерСтроки КАК НомерСтроки,
		|	вт_График.ФактДней КАК ФактДней,
		|	ОплатыПассажировОбороты.ОплатаОборот КАК Оплата
		|ИЗ
		|	вт_График КАК вт_График
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОплатыПассажиров.Обороты(&ДатаН, &ДатаК, , ) КАК ОплатыПассажировОбороты
		|		ПО вт_График.Сотрудник = ОплатыПассажировОбороты.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ДатаН", НачалоМесяца(ДобавитьМесяц(Регистратор.Дата, -1)));
	Запрос.УстановитьПараметр("ДатаК", КонецМесяца(ДобавитьМесяц(Регистратор.Дата, -1)));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		СтруктураПоиска	= Новый Структура("НомерСтроки",);
		
		Для каждого Запись Из НаборЗаписей Цикл
			СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
			Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
				Если Запись.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.Надбавка Тогда
					Запись.Результат	= Выборка.Оплата * Запись.Параметр / 100;	
				КонецЕсли;
				Запись.ОтработанноДней	= Выборка.ФактДней;	
			КонецЕсли;
			//Выборка.Сбросить();	// не нужно сбрасывать, т.к. упорядочили по НомерСтроки
		
		КонецЦикла; 
	КонецЕсли;
	
	НаборЗаписей.Записать(, Истина);

	// Отпуск
	НаборЗаписей	= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.РезультатБаза, 0) + ЕСТЬNULL(ОсновныеНачисленияБазаДополнительныеНачисления.РезультатБаза, 0) КАК База,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаДополнительныеНачисления.ОтработанноДнейБаза, 0) КАК Норма
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
		|				&ИзмеренияРегистров,
		|				&ИзмеренияРегистров,
		|				,
		|				Регистратор = &Регистратор
		|					И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаДополнительныеНачисления(
		|				&ИзмеренияРегистров,
		|				&ИзмеренияРегистров,
		|				,
		|				Регистратор = &Регистратор
		|					И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияБазаДополнительныеНачисления
		|		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаДополнительныеНачисления.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	ИзмеренияРегистров		= Новый Массив(1);
	ИзмеренияРегистров[0]	= "Сотрудник";
	
	Запрос.УстановитьПараметр("ИзмеренияРегистров", ИзмеренияРегистров);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		СтруктураПоиска	= Новый Структура("НомерСтроки",);
		
		Для каждого Запись Из НаборЗаписей Цикл
			СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
			Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
				Если Выборка.Норма<>0 Тогда
					Запись.Результат	= Выборка.База * Выборка.Факт / Выборка.Норма;	
				Иначе				
					Запись.Результат	= 0;
				КонецЕсли;
			КонецЕсли;
			//Выборка.Сбросить();
		КонецЦикла; 
	КонецЕсли;
	
	НаборЗаписей.Записать(, Истина);
	
КонецПроцедуры
