
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр Эксплуатация Расход
	Движения.Эксплуатация.Записывать = Истина;
	Движения.Эксплуатация.Записать();
	
	//----------- Блокировка -----------
	Блокировка					= Новый БлокировкаДанных;
	
	ЭлБлокировки				= Блокировка.Добавить("РегистрНакопления.Эксплуатация");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(, НачалоДня(Дата)));
	
	ЭлБлокировки				= Блокировка.Добавить("РегистрНакопления.Эксплуатация");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("СрокЭксплуатации", Новый Диапазон(, НачалоДня(Дата)));
	
	Блокировка.Заблокировать();
	//----------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭксплуатацияОстатки.Номенклатура КАК Номенклатура,
		|	ЭксплуатацияОстатки.СрокГодности КАК СрокГодности,
		|	ЭксплуатацияОстатки.СрокЭксплуатации КАК СрокЭксплуатации,
		|	ЭксплуатацияОстатки.КоличествоОстаток КАК Количество,
		|	ЭксплуатацияОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.Эксплуатация.Остатки(
		|			&Дата,
		|			СрокГодности < &Дата
		|				ИЛИ СрокЭксплуатации < &Дата) КАК ЭксплуатацияОстатки";
	
	Запрос.УстановитьПараметр("Дата", НачалоДня(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.Эксплуатация.ДобавитьРасход();
		Движение.Период = НачалоДня(Дата);
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	//----------- Блокировка -----------
	Блокировка					= Новый БлокировкаДанных;
	ЭлБлокировки				= Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("СрокГодности", Новый Диапазон(, НачалоДня(Дата)));
	Блокировка.Заблокировать();
	//----------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&Дата, СрокГодности < &Дата) КАК ОстаткиНоменклатурыОстатки";
	
	Запрос.УстановитьПараметр("Дата", НачалоДня(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
		Движение.Период = НачалоДня(Дата);
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
	КонецЦикла;
	
КонецПроцедуры
