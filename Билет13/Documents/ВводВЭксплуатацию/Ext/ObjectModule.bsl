
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.ОстаткиНоменклатуры.Записать();
	
	// регистр Эксплуатация Приход
	Движения.Эксплуатация.Записывать = Истина;
	Движения.Эксплуатация.Записать();

	//----------- Блокировка -----------
	Блокировка					= Новый БлокировкаДанных;
	ЭлБлокировки				= Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.ИсточникДанных	= СписокНоменклатуры;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	//----------------------------------
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ВводВЭксплуатациюСписокНоменклатуры.Количество) КАК Количество,
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура.СрокЭксплуатации КАК СрокЭксплуатацииВДнях
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.ВводВЭксплуатацию.СписокНоменклатуры КАК ВводВЭксплуатациюСписокНоменклатуры
		|ГДЕ
		|	ВводВЭксплуатациюСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВводВЭксплуатациюСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	ОстаткиНоменклатурыОстатки.СрокГодности КАК СрокГодности,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление,
		|	вт_ДокТЧ.СрокЭксплуатацииВДнях КАК СрокЭксплуатацииВДнях
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И СрокГодности >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)) КАК ОстаткиНоменклатурыОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СрокГодности
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 с не истекшим сроком годности в количестве %2", 
								ВыборкаНоменклатура.НоменклатураПредставление, СокрЛП(ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток)); 			
			Сообщение.Сообщить();
		КонецЕсли;
		
		Если Отказ Тогда
		    Продолжить;
		КонецЕсли;
	
		ВсегоСписать	= ВыборкаНоменклатура.Количество; 
		Стоимость		= ВыборкаНоменклатура.СуммаОстаток;
		
		Выборка		= ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ВсегоСписать > 0 Цикл
			Списать		= Мин(ВсегоСписать, Выборка.КоличествоОстаток);
			
			Если ВыборкаНоменклатура.Количество = ВыборкаНоменклатура.КоличествоОстаток 
				И Списать = ВсегоСписать Тогда
			    Себестоимость	= Стоимость;
			Иначе	
			    Себестоимость	= ВыборкаНоменклатура.СуммаОстаток * Списать / ВыборкаНоменклатура.КоличествоОстаток;
			КонецЕсли;
			
			Движение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Номенклатура	= Выборка.Номенклатура;
			Движение.СрокГодности	= Выборка.СрокГодности;
			Движение.Количество		= Списать;
			Движение.Сумма			= Себестоимость;
			
			Движение = Движения.Эксплуатация.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.СрокГодности = Выборка.СрокГодности;
			Движение.СрокЭксплуатации = Дата + Выборка.СрокЭксплуатацииВДнях * 86400;
			Движение.Количество = Списать;
			Движение.Сумма = Себестоимость;
		
			ВсегоСписать	= ВсегоСписать - Списать;	
			Стоимость		= Стоимость - Себестоимость;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
