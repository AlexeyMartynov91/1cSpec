
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.Записать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставКомплектов.Деталь КАК Деталь,
		|	ВЫБОР
		|		КОГДА СоставКомплектов.Деталь ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НетСоставаКомплекта,
		|	ЕСТЬNULL(СоставКомплектов.Количество, 0) * вт_ДокТЧ.Количество КАК Количество,
		|	ЕСТЬNULL(СоставКомплектов.Деталь.Представление, вт_ДокТЧ.Номенклатура.Представление) КАК НоменклатураПредставление,
		|	вт_ДокТЧ.Номенклатура КАК Комплект,
		|	вт_ДокТЧ.Сумма КАК Сумма
		|ПОМЕСТИТЬ вт_Детали
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставКомплектов КАК СоставКомплектов
		|		ПО вт_ДокТЧ.Номенклатура = СоставКомплектов.Комплект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Детали.Деталь КАК Деталь
		|ИЗ
		|	вт_Детали КАК вт_Детали
		|ГДЕ
		|	НЕ вт_Детали.НетСоставаКомплекта
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_Детали.Деталь";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//-------------- Блокировка ----------------
	Блокировка					= Новый БлокировкаДанных;
	ЭлБлокировки				= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");	
	ЭлБлокировки.Режим			= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, Склад);
	ЭлБлокировки.ИсточникДанных	= РезультатЗапроса;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Деталь");
	Блокировка.Заблокировать();
	//------------------------------------------
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	вт_Детали.Комплект КАК Комплект,
		|	вт_Детали.Деталь КАК Деталь,
		|	вт_Детали.НетСоставаКомплекта КАК НетСоставаКомплекта,
		|	вт_Детали.Количество КАК Количество,
		|	вт_Детали.Сумма КАК Сумма,
		|	вт_Детали.НоменклатураПредставление КАК НоменклатураПредставление,
		|	ЕСТЬNULL(ЖурналПроводокОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	вт_Детали КАК вт_Детали
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &СчетТовары,
		|				&ВидыСубконто,
		|				Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_Детали.Деталь
		|						ИЗ
		|							вт_Детали КАК вт_Детали)
		|					И Субконто2 = &Склад) КАК ЖурналПроводокОстатки
		|		ПО вт_Детали.Деталь = ЖурналПроводокОстатки.Субконто1
		|ИТОГИ
		|	МАКСИМУМ(НетСоставаКомплекта),
		|	МАКСИМУМ(Сумма)
		|ПО
		|	Комплект";
	
	ВидыСубконто		= Новый Массив(2);
	ВидыСубконто[0]		= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыСубконто[1]		= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	
	Запрос.УстановитьПараметр("ВидыСубконто",	ВидыСубконто);
	Запрос.УстановитьПараметр("МоментВремени",	МоментВремени());
	Запрос.УстановитьПараметр("СчетТовары",		ПланыСчетов.Управленческий.Товары);
	Запрос.УстановитьПараметр("Склад",			Склад);
	
	РезультатЗапроса 		= Запрос.Выполнить();
	ВыборкаКомплект			= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКомплект.Следующий() Цикл
		Если ВыборкаКомплект.НетСоставаКомплекта Тогда
			Отказ	= Истина;
			
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Для комплекта %1 не описан состав.", ВыборкаКомплект.НоменклатураПредставление);			
			Сообщение.Сообщить();		
		КонецЕсли;		
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка			= ВыборкаКомплект.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
				Отказ	= Истина;
				
				Сообщение		= Новый СообщениеПользователю;
				Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2", 
									Выборка.НоменклатураПредставление, СокрЛП(Выборка.Количество - Выборка.КоличествоОстаток));			
				Сообщение.Сообщить();		
			КонецЕсли;		
			
			Если Отказ Тогда
			    Продолжить;
			КонецЕсли;
			
			Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
				Себестоимость	= Выборка.СуммаОстаток;	
			Иначе
				Себестоимость	= Выборка.СуммаОстаток * Выборка.Количество / Выборка.КоличествоОстаток;	
			КонецЕсли;	
			
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
			Движение.Период = Дата;
			Движение.КоличествоКт = Выборка.Количество;
			Движение.Сумма = Себестоимость;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Комплект;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Бригады] = Бригада;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Деталь;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = Склад;
		КонецЦикла;
		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
		Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		Движение.Период = Дата;
		Движение.Сумма = ВыборкаКомплект.Сумма;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаКомплект.Комплект;
		Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Бригады] = Бригада;
		
	КонецЦикла;
	
КонецПроцедуры
