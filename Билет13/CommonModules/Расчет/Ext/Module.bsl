Процедура Рассчитать(Регистратор, ПериодРегистрации) Экспорт
	// оплата по часовому тарифу
	НаборЗаписей		= РегистрыРасчета.ОсновныеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЧасовФакт
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК ОсновныеНачисленияДанныеГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка		= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки", );	
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки		= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Запись.Параметр * Выборка.ЧасовФакт;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	
	// надбавка
	НаборЗаписей		= РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДопНачисления.НомерСтроки КАК НомерСтроки,
		|	ДопНачисления.Бригада КАК Бригада
		|ПОМЕСТИТЬ вт_ДопНач
		|ИЗ
		|	РегистрРасчета.ДопНачисления КАК ДопНачисления
		|ГДЕ
		|	ДопНачисления.Регистратор = &Регистратор
		|	И ДопНачисления.ПериодРегистрации = &ПериодРегистрации
		|	И ДопНачисления.ВидРасчета = &ВидРасчетаНадбавка
		|	И ДопНачисления.Активность
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Бригада
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДопНач.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ЖурналПроводокОбороты.СуммаОборотКт, 0) КАК СуммаПродаж
		|ИЗ
		|	вт_ДопНач КАК вт_ДопНач
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Обороты(&НачПредКвартала, &КонПредКвартала, , Счет = &СчетПрУб, &СубконтоБригада, , , ) КАК ЖурналПроводокОбороты
		|		ПО вт_ДопНач.Бригада = ЖурналПроводокОбороты.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	СубконтоБригада		= Новый Массив(1);
	СубконтоБригада[0]	= ПланыВидовХарактеристик.ВидыСубконто.Бригады;
	
	Запрос.УстановитьПараметр("ВидРасчетаНадбавка", ПланыВидовРасчета.ДополнительныеНачисления.Надбавка);
	Запрос.УстановитьПараметр("НачПредКвартала", ДобавитьМесяц(НачалоКвартала(ПериодРегистрации), -3));
	Запрос.УстановитьПараметр("КонПредКвартала", ДобавитьМесяц(КонецКвартала(ПериодРегистрации), -3));
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("СубконтоБригада", СубконтоБригада);
	Запрос.УстановитьПараметр("СчетПрУб", ПланыСчетов.Управленческий.ПрибылиУбытки);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка		= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки", );	
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки		= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.СуммаПродаж * Запись.Параметр / 100;
			Запись.СуммаПродаж	= Выборка.СуммаПродаж; 
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	
	// премия бригадиру
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДопНачисленияБазаДопНачисления.НомерСтроки КАК НомерСтроки,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДопНачисленияБазаДопНачисления.СотрудникРазрез) КАК СотрудникРазрез,
		|	МАКСИМУМ(ДопНачисленияБазаДопНачисления.РезультатБаза) КАК База
		|ИЗ
		|	РегистрРасчета.ДопНачисления.БазаДопНачисления(
		|			&Измерения,
		|			&Измерения,
		|			&Разрезы,
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &ВидРасчетаПремияБригадиру) КАК ДопНачисленияБазаДопНачисления
		|ГДЕ
		|	ДопНачисленияБазаДопНачисления.СотрудникРазрез <> ДопНачисленияБазаДопНачисления.Сотрудник
		|
		|СГРУППИРОВАТЬ ПО
		|	ДопНачисленияБазаДопНачисления.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Измерения		= Новый Массив(1);
	Измерения[0]	= "Бригада";             
	
	Разрезы		= Новый Массив(1);
	Разрезы[0]	= "Сотрудник";
	
	Запрос.УстановитьПараметр("ВидРасчетаПремияБригадиру", ПланыВидовРасчета.ДополнительныеНачисления.ПремияБригадиру);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Разрезы", Разрезы);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка		= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки", );	
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки		= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.База * Запись.Параметр / 100;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(, Истина);
	
КонецПроцедуры
