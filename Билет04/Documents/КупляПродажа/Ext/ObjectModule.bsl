
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ЖурналПроводок 
	Движения.ЖурналПроводок.Очистить();
	Движения.ЖурналПроводок.Записывать = Истина;
	Движения.ЖурналПроводок.БлокироватьДляИзменения	= Истина;
	
	//-------------- Блокировка -----------------
	Блокировка			= Новый БлокировкаДанных;	
	ЭлБлокировки		= Блокировка.Добавить("РегистрБухгалтерии.ЖурналПроводок");	
	ЭлБлокировки.Режим	= РежимБлокировкиДанных.Исключительный;
	ЭлБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Товары);	
	ЭлБлокировки.УстановитьЗначение("Организация", ОрганизацияПоставщик);	
	ЭлБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Склады, СкладОрганизацииПоставщика);	
	ЭлБлокировки.ИсточникДанных	= Товары;
	ЭлБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура, "Номенклатура");
	Блокировка.Заблокировать();
	//-------------------------------------------
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КупляПродажаТовары.Номенклатура КАК Номенклатура,
		|	СУММА(КупляПродажаТовары.Количество) КАК Количество,
		|	СУММА(КупляПродажаТовары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ вт_ДокТЧ
		|ИЗ
		|	Документ.КупляПродажа.Товары КАК КупляПродажаТовары
		|ГДЕ
		|	КупляПродажаТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	КупляПродажаТовары.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ДокТЧ.Номенклатура КАК Номенклатура,
		|	вт_ДокТЧ.Количество КАК Количество,
		|	вт_ДокТЧ.Сумма КАК Сумма,
		|	ЖурналПроводокОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ЖурналПроводокОстатки.СуммаОстаток КАК СуммаОстаток,
		|	ЖурналПроводокОстатки.Субконто2 КАК Партия,
		|	вт_ДокТЧ.Номенклатура.Представление КАК НоменклатураПредставление
		|ИЗ
		|	вт_ДокТЧ КАК вт_ДокТЧ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.ЖурналПроводок.Остатки(
		|				&МоментВремени,
		|				Счет = &Счет,
		|				&ВидыСубконто,
		|				Организация = &ОрганизацияПоставщик
		|					И Субконто1 В
		|						(ВЫБРАТЬ
		|							вт_ДокТЧ.Номенклатура
		|						ИЗ
		|							вт_ДокТЧ КАК вт_ДокТЧ)
		|					И Субконто3 = &СкладПоставщика) КАК ЖурналПроводокОстатки
		|		ПО вт_ДокТЧ.Номенклатура = ЖурналПроводокОстатки.Субконто1
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЖурналПроводокОстатки.Субконто2.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток)
		|ПО
		|	Номенклатура";
	
	ВидыСубконто	= Новый Массив(3);
	ВидыСубконто[0]	= ПланыВидовХарактеристик.ВидыСубконто.Номенклатура;
	ВидыСубконто[1]	= ПланыВидовХарактеристик.ВидыСубконто.Партии;
	ВидыСубконто[2]	= ПланыВидовХарактеристик.ВидыСубконто.Склады;
	
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("ОрганизацияПоставщик", ОрганизацияПоставщик);
	Запрос.УстановитьПараметр("СкладПоставщика", СкладОрганизацииПоставщика);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Управленческий.Товары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура	= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.КоличествоОстаток < ВыборкаНоменклатура.Количество Тогда
			Отказ	= Истина;
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= СтрШаблон("Превышение остатка по номенклатуре %1 в количестве %2", 
								ВыборкаНоменклатура.НоменклатураПредставление, СокрЛП(ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток));			
			Сообщение.Сообщить();
		КонецЕсли;   	
		
		Если Отказ Тогда
		    Продолжить;
		КонецЕсли;
		
		ОсталосьСписать		= ВыборкаНоменклатура.Количество;
		Выборка	= ВыборкаНоменклатура.Выбрать();
		Пока Выборка.Следующий() И ОсталосьСписать > 0 Цикл
			Списать		= Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
			
			Если Списать <> Выборка.КоличествоОстаток Тогда
				Себестоимость	= Выборка.СуммаОстаток * Списать / Выборка.КоличествоОстаток;  	
			Иначе	
				Себестоимость	= Выборка.СуммаОстаток;  	
			КонецЕсли;
			
			// поставщик
			Движение = Движения.ЖурналПроводок.Добавить();
			Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
			Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
			Движение.Период = Дата;
			Движение.Организация = ОрганизацияПоставщик;
			Движение.Количество = Списать;
			Движение.Сумма = Себестоимость;
			Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Покупатели] = ОрганизацияПокупатель;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Выборка.Партия;
			Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = СкладОрганизацииПоставщика;
			
			ОсталосьСписать		= ОсталосьСписать - Списать;
		КонецЦикла;

		// покупатель		
		Движение = Движения.ЖурналПроводок.Добавить();
		Движение.СчетДт = ПланыСчетов.Управленческий.Товары;
		Движение.СчетКт = ПланыСчетов.Управленческий.Поставщики;
		Движение.Период = Дата;
		Движение.Организация = ОрганизацияПокупатель;
		Движение.Количество = ВыборкаНоменклатура.Количество;
		Движение.Сумма = ВыборкаНоменклатура.Сумма;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = ВыборкаНоменклатура.Номенклатура;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Партии] = Ссылка;
		Движение.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Склады] = СкладОрганизацииПокупателя;
	КонецЦикла;
	
	// поставщик
	Движение = Движения.ЖурналПроводок.Добавить();
	Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
	Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
	Движение.Период = Дата;
	Движение.Организация = ОрганизацияПоставщик;
	Движение.Сумма = Товары.Итог("Сумма");;
	Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Покупатели] = ОрганизацияПокупатель;
	
	
КонецПроцедуры
