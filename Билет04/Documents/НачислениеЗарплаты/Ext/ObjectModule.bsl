
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр НачисленияСотрудникам
	Движения.НачисленияСотрудникам.Очистить();
	Движения.НачисленияСотрудникам.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачислениеЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	НачислениеЗарплатыОсновныеНачисления.Подразделение КАК Подразделение,
		|	НачислениеЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	НачислениеЗарплатыОсновныеНачисления.График КАК График,
		|	НачислениеЗарплатыОсновныеНачисления.ДатаНачала КАК ПериодДействияНачало,
		|	НачислениеЗарплатыОсновныеНачисления.ДатаОкончания КАК ПериодДействияКонец,
		|	НачислениеЗарплатыОсновныеНачисления.Параметр КАК Параметр
		|ПОМЕСТИТЬ вт_ОснНачисления
		|ИЗ
		|	Документ.НачислениеЗарплаты.ОсновныеНачисления КАК НачислениеЗарплатыОсновныеНачисления
		|ГДЕ
		|	НачислениеЗарплатыОсновныеНачисления.Ссылка = &Ссылка
		|	И НачислениеЗарплатыОсновныеНачисления.ВидРасчета.АлгоритмРасчета В(&АлгоритмыРасчетаОтработанногоВремени)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ОснНачисления.Сотрудник КАК Сотрудник,
		|	вт_ОснНачисления.Подразделение КАК Подразделение,
		|	вт_ОснНачисления.ВидРасчета КАК ВидРасчета,
		|	вт_ОснНачисления.График КАК График,
		|	вт_ОснНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	вт_ОснНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
		|	МАКСИМУМ(ТарифныеСтавки.Период) КАК Период
		|ПОМЕСТИТЬ вт_ДатыСрезаПоТарифу
		|ИЗ
		|	вт_ОснНачисления КАК вт_ОснНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки КАК ТарифныеСтавки
		|		ПО вт_ОснНачисления.График = ТарифныеСтавки.Смена
		|			И вт_ОснНачисления.ПериодДействияНачало > ТарифныеСтавки.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	вт_ОснНачисления.Подразделение,
		|	вт_ОснНачисления.График,
		|	вт_ОснНачисления.ПериодДействияКонец,
		|	вт_ОснНачисления.ВидРасчета,
		|	вт_ОснНачисления.Сотрудник,
		|	вт_ОснНачисления.ПериодДействияНачало
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_ОснНачисления.Сотрудник КАК Сотрудник,
		|	вт_ОснНачисления.Подразделение КАК Подразделение,
		|	вт_ОснНачисления.ВидРасчета КАК ВидРасчета,
		|	вт_ОснНачисления.График КАК График,
		|	вт_ОснНачисления.ПериодДействияНачало КАК ПериодДействияНачало,
		|	вт_ОснНачисления.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ТарифныеСтавки.Тариф КАК Тариф,
		|	ТарифныеСтавки.Период КАК Период,
		|	вт_ОснНачисления.Параметр КАК Параметр
		|ИЗ
		|	вт_ОснНачисления КАК вт_ОснНачисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки КАК ТарифныеСтавки
		|		ПО вт_ОснНачисления.График = ТарифныеСтавки.Смена
		|			И вт_ОснНачисления.ПериодДействияНачало <= ТарифныеСтавки.Период
		|			И вт_ОснНачисления.ПериодДействияКонец >= ТарифныеСтавки.Период
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	вт_ДатыСрезаПоТарифу.Сотрудник,
		|	вт_ДатыСрезаПоТарифу.Подразделение,
		|	вт_ДатыСрезаПоТарифу.ВидРасчета,
		|	вт_ДатыСрезаПоТарифу.График,
		|	вт_ДатыСрезаПоТарифу.ПериодДействияНачало,
		|	вт_ДатыСрезаПоТарифу.ПериодДействияКонец,
		|	ТарифныеСтавки.Тариф,
		|	вт_ДатыСрезаПоТарифу.Период,
		|	NULL
		|ИЗ
		|	вт_ДатыСрезаПоТарифу КАК вт_ДатыСрезаПоТарифу
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки КАК ТарифныеСтавки
		|		ПО вт_ДатыСрезаПоТарифу.График = ТарифныеСтавки.Смена
		|			И вт_ДатыСрезаПоТарифу.Период = ТарифныеСтавки.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ
		|ИТОГИ
		|	МИНИМУМ(ПериодДействияНачало),
		|	МАКСИМУМ(ПериодДействияКонец)
		|ПО
		|	Сотрудник,
		|	Подразделение";
	
	АлгоритмыРасчетаОтработанногоВремени	= Новый Массив(1);
	АлгоритмыРасчетаОтработанногоВремени[0]	= Перечисления.АлгоритмыРасчетаНачислений.ЗаОтработанноеВремя;
//	АлгоритмыРасчетаОтработанногоВремени[1]	= Перечисления.АлгоритмыРасчетаНачислений.ПропорциональноОтработанномуВремени;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("АлгоритмыРасчетаОтработанногоВремени", АлгоритмыРасчетаОтработанногоВремени);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСотрудник = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСотрудник.Следующий() Цикл
		ВыборкаПодразделение = ВыборкаСотрудник.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПодразделение.Следующий() Цикл
			ПредыдущаяДата	= ВыборкаПодразделение.ПериодДействияКонец;
			Выборка	= ВыборкаПодразделение.Выбрать();
			Пока Выборка.Следующий() Цикл
				Движение = Движения.НачисленияСотрудникам.Добавить();
				Движение.ПериодРегистрации = НачалоМесяца(Дата);
				Движение.Сотрудник = Выборка.Сотрудник;
				Движение.Подразделение = Выборка.Подразделение;
				Движение.График = Выборка.График;
				Движение.ВидРасчета = Выборка.ВидРасчета;
				Движение.ПериодДействияНачало = Макс(Выборка.Период, Выборка.ПериодДействияНачало);
				Движение.ПериодДействияКонец = Мин(ПредыдущаяДата, Выборка.ПериодДействияКонец);
				Движение.БазовыйПериодНачало	= НачалоМесяца(Дата);
				Движение.БазовыйПериодКонец		= КонецМесяца(Дата);
				Движение.Параметр	= Выборка.Тариф;
				
				ПредыдущаяДата	= Движение.ПериодДействияНачало - 24*60*60;
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;

	//----- Алгоритмы расчета начислений не по отработанному времени ------
	Для каждого стр Из ОсновныеНачисления Цикл
		Если стр.ВидРасчета.АлгоритмРасчета <> Перечисления.АлгоритмыРасчетаНачислений.ЗаОтработанноеВремя	Тогда 
			Движение		= Движения.НачисленияСотрудникам.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, стр);
			Движение.ПериодРегистрации = НачалоМесяца(Дата);
			Движение.ПериодДействияНачало = стр.ДатаНачала;
			Движение.ПериодДействияКонец = стр.ДатаОкончания;
			Движение.БазовыйПериодНачало	= НачалоМесяца(Дата);
			Движение.БазовыйПериодКонец		= КонецМесяца(Дата);
		КонецЕсли; 	
	КонецЦикла; 
	
	//------------------------------------------------------
	
	// регистр ДопНачисления
	Движения.ДопНачисления.Очистить();
	Движения.ДопНачисления.Записывать = Истина;
	Для Каждого стр Из ДополнительныеНачисления Цикл
		Движение = Движения.ДопНачисления.Добавить();
		Движение.ВидРасчета = стр.ВидРасчета;
		Движение.ПериодРегистрации = НачалоМесяца(Дата);
		Движение.Сотрудник = стр.Сотрудник;
		Движение.Подразделение = стр.Подразделение;
		Движение.Параметр = стр.Параметр;
		Движение.БазовыйПериодНачало	= НачалоМесяца(ДобавитьМесяц(Дата, -1));
		Движение.БазовыйПериодКонец		= КонецМесяца(ДобавитьМесяц(Дата, -1));
	КонецЦикла;
	
	Движения.Записать();
	
	Расчет.РассчитатьНачисления(ССылка);
	
КонецПроцедуры
