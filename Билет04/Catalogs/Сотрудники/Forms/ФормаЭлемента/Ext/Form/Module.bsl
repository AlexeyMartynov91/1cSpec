
&НаКлиенте
Процедура ВыбратьФото(Команда)
	
	ОповещениеОПомещенииФайла	= Новый ОписаниеОповещения("ПослеПомещенияФайла", ЭтаФорма);
	НачатьПомещениеФайлаНаСервер(ОповещениеОПомещенииФайла, , , , , УникальныйИдентификатор); // УникальныйИдентификатор - ВременноеХранилище живет столько, сколько живет форма, соответственно в ПередЗаписьюНаСервере есть ещё картинка 
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПомещенияФайла(Результат, Адрес) Экспорт
	
	Если Результат.ПомещениеФайлаОтменено Тогда Возврат; КонецЕсли; 
	
	СсылкаНаФото		= Результат.Адрес; 
//	Объект.ПутьКФото	= Результат.СсылкаНаФайл.Файл.ПолноеИмя;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьОтветСохраненияФайла", ЭтотОбъект, Результат.СсылкаНаФайл.Файл.ПолноеИмя);
	ПоказатьВопрос(Оповещение, "Сохранить фото в базе?", РежимДиалогаВопрос.ДаНет);

	Модифицированность	= Истина; 
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветСохраненияФайла (Результат, ВыбранноеИмяФайла) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		// Сохраняем путь к выбранному файлу (если на диске)
		Объект.ПутьКФото	= ВыбранноеИмяФайла;
	Иначе	
		Объект.ПутьКФото = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ПутьКФото = "" Тогда		
		Если ЭтоАдресВременногоХранилища(СсылкаНаФото) Тогда
			// Сохраняем в базе данных фото
			ТекущийОбъект.ДанныеФото = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(СсылкаНаФото));
			УдалитьИзВременногоХранилища(СсылкаНаФото);
		КонецЕсли;
	Иначе
		// Очищаем реквизит типа хранилище значения
		ТекущийОбъект.ДанныеФото = "";  
	КонецЕсли;		

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.ПутьКФото <> "" Тогда
		// Помещаем во временное хранилище фото, находящееся по пути, указанному в реквизите ПутьКФото
		ОповещениеОПомещенииФайла = Новый ОписаниеОповещения("ОткрытиеФайлаПоПути", ЭтотОбъект);
		НачатьПомещениеФайлаНаСервер(ОповещениеОПомещенииФайла, , ,СсылкаНаФото, Объект.ПутьКФото, УникальныйИдентификатор);
	Иначе
		// Помещаем в реквизит СсылкаНаФото навигационную ссылку на фото
		СсылкаНаФото = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ДанныеФото");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеФайлаПоПути(Результат, Адрес) Экспорт
	Если Результат.ПомещениеФайлаОтменено Тогда Возврат; КонецЕсли; 
	
	СсылкаНаФото		= Результат.Адрес; 
	
КонецПроцедуры







