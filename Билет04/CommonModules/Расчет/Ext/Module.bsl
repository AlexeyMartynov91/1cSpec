Процедура РассчитатьНачисления(Регистратор) Экспорт

	НаборЗаписей		= РегистрыРасчета.НачисленияСотрудникам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();	
//	РассчитатьОсновныеНачисления(Регистратор, НаборЗаписей);		
	РассчитатьОсновныеНачисленияПоАлгоритмамРасчета(Регистратор, НаборЗаписей);
	НаборЗаписей.Записать(, Истина);
	
	ДопНаборЗаписей		= РегистрыРасчета.ДопНачисления.СоздатьНаборЗаписей();
	ДопНаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	ДопНаборЗаписей.Прочитать();	
	РассчитатьДопНачисления(Регистратор, ДопНаборЗаписей);		
	ДопНаборЗаписей.Записать(, Истина);

КонецПроцедуры

Процедура РассчитатьОсновныеНачисления(Регистратор, НаборЗаписей)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудникамДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	НачисленияСотрудникамДанныеГрафика.ЗначениеФактическийПериодДействия КАК Факт
		|ИЗ
		|	РегистрРасчета.НачисленияСотрудникам.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &ВидРасчета) КАК НачисленияСотрудникамДанныеГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоТарифуПоЧасам);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки",);
	
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Запись.Параметр * Выборка.Факт; 
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура РассчитатьДопНачисления(Регистратор, НаборЗаписей)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДопНачисленияБазаНачисленияСотрудникам.НомерСтроки КАК НомерСтроки,
		|	СУММА(ДопНачисленияБазаНачисленияСотрудникам.РезультатБаза) КАК РезультатБаза
		|ИЗ
		|	РегистрРасчета.ДопНачисления.БазаНачисленияСотрудникам(
		|			&ИзмеренияРегистра,
		|			&ИзмеренияРегистра,
		|			&Разрезы,
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации
		|				И ВидРасчета = &ВидРасчетаПремия) КАК ДопНачисленияБазаНачисленияСотрудникам
		|ГДЕ
		|	ДопНачисленияБазаНачисленияСотрудникам.Сотрудник <> ДопНачисленияБазаНачисленияСотрудникам.СотрудникРазрез
		|
		|СГРУППИРОВАТЬ ПО
		|	ДопНачисленияБазаНачисленияСотрудникам.НомерСтроки
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	ИзмеренияРегистра		= Новый Массив(1);
	ИзмеренияРегистра[0]	= "Подразделение";
	
	Разрезы					= Новый Массив(1);
	Разрезы[0]				= "Сотрудник";
	
	Запрос.УстановитьПараметр("ИзмеренияРегистра", ИзмеренияРегистра);
	Запрос.УстановитьПараметр("Разрезы", Разрезы);
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ВидРасчетаПремия", ПланыВидовРасчета.ДополнительныеНачисления.Премия);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки",);
	
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			Запись.Результат	= Выборка.РезультатБаза * Запись.Параметр/100; 
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура РассчитатьОсновныеНачисленияПоАлгоритмамРасчета(Регистратор, НаборЗаписей)
	// для этого нужно у ПланыВидовРасчета.ОсновныеНачисления выбрать "Зависет по периоду действия" от ОсновныхНачислений, чтобы появилась база
	// потом уберу, для основного решения задачи
	
	
	//------------------------------------------
	НаборЗаписей		= РегистрыРасчета.НачисленияСотрудникам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();	
	//------------------------------------------

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НачисленияСотрудникамДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	НачисленияСотрудникамДанныеГрафика.ЗначениеФактическийПериодДействия КАК Факт,
		|	НачисленияСотрудникамДанныеГрафика.ЗначениеПериодДействия КАК Норма,
		|	НачисленияСотрудникамБазаНачисленияСотрудникам.РезультатБаза КАК База,
		|	НачисленияСотрудникамБазаНачисленияСотрудникам.ОтработаноДнейБаза КАК ОтработаноДнейБаза
		|ИЗ
		|	РегистрРасчета.НачисленияСотрудникам.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ПериодРегистрации = &ПериодРегистрации) КАК НачисленияСотрудникамДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.НачисленияСотрудникам.БазаНачисленияСотрудникам(
		|				&ИзмеренияРегистра,
		|				&ИзмеренияРегистра,
		|				,
		|				Регистратор = &Регистратор
		|					И ПериодРегистрации = &ПериодРегистрации) КАК НачисленияСотрудникамБазаНачисленияСотрудникам
		|		ПО НачисленияСотрудникамДанныеГрафика.Регистратор = НачисленияСотрудникамБазаНачисленияСотрудникам.Регистратор
		|			И НачисленияСотрудникамДанныеГрафика.НомерСтроки = НачисленияСотрудникамБазаНачисленияСотрудникам.НомерСтроки
		|			И НачисленияСотрудникамДанныеГрафика.Сотрудник = НачисленияСотрудникамБазаНачисленияСотрудникам.Сотрудник
		|			И НачисленияСотрудникамДанныеГрафика.Подразделение = НачисленияСотрудникамБазаНачисленияСотрудникам.Подразделение
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	ИзмеренияРегистра		= Новый Массив(2);
	ИзмеренияРегистра[0]	= "Сотрудник";
	ИзмеренияРегистра[1]	= "Подразделение";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(Регистратор.Дата));
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ИзмеренияРегистра", ИзмеренияРегистра);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка	= РезультатЗапроса.Выбрать();
	
	СтруктураПоиска	= Новый Структура("НомерСтроки",);
	
	Для каждого Запись Из НаборЗаписей Цикл
		СтруктураПоиска.НомерСтроки	= Запись.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
		#Область ВыборАлгоритмаРасчета
			Если Запись.ВидРасчета.АлгоритмРасчета	= Перечисления.АлгоритмыРасчетаНачислений.ПропорциональноОтработанномуВремени Тогда
				Запись.Результат	= Запись.Параметр	* Выборка.Факт / Выборка.Норма;
			ИначеЕсли Запись.ВидРасчета.АлгоритмРасчета = Перечисления.АлгоритмыРасчетаНачислений.ПроцентОтБазы Тогда
				Запись.Результат	= Выборка.База	* Запись.Параметр / 100;
			ИначеЕсли Запись.ВидРасчета.АлгоритмРасчета	= Перечисления.АлгоритмыРасчетаНачислений.ЗаОтработанноеВремя Тогда
				Запись.Результат	= Запись.Параметр * Выборка.Факт;
			ИначеЕсли Запись.ВидРасчета.АлгоритмРасчета = Перечисления.АлгоритмыРасчетаНачислений.ФиксированнаяСумма Тогда
				Запись.Результат	= Запись.Параметр;
			ИначеЕсли Запись.ВидРасчета.АлгоритмРасчета = Перечисления.АлгоритмыРасчетаНачислений.ПоСреднему Тогда
				Запись.Результат	= Выборка.Факт * Выборка.База / Выборка.ОтработаноДнейБаза * ?(Запись.Параметр<>0, Запись.Параметр, 100) / 100;
			//ИначеЕсли Запись.ВидРасчета = ПланыВидовРасчета.Начисления.ОплатаПоТабелю Тогда
			//	Отбор				= Новый Структура;
			//	Отбор.Вставить("Сотрудник", Запись.Сотрудник);
			//	Обороты				= РегистрыНакопления.Табель.Обороты(Запись.ПериодДействияНачало, Запись.ПериодДействияКонец, Отбор, , "Статус");
			//	ПоТабелю			= Обороты[0].Статус;
			//	Запись.Результат	= ПоТабелю / Норма * Запись.Ставка;
			//	Запись.ОтработаноДней= ПоТабелю;	
			КонецЕсли;	
		#КонецОбласти	
		
			Если Запись.ВидРасчета.УвеличиваетОтработанныеДни Тогда
				Запись.ОтработаноДней	= Выборка.Факт;	
			КонецЕсли;	
			
			//------------ сторно -----------------
			Если Запись.Сторно Тогда
				Запись.ОтработаноДней	= -Запись.ОтработаноДней;
				Запись.Результат		= -Запись.Результат;
			КонецЕсли;
			//-------------------------------------
		
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры
